
#Kernel functions for use with hacvariance estimator
abstract KernelFunction
type KernelEpanechnikov <: KernelFunction ; end
type KernelGaussian <: KernelFunction ; end
type KernelUniform <: KernelFunction ; end
type KernelBartlett <: KernelFunction ; end
function symbol_to_kernel_func(s::Symbol)::KernelFunction
	s == :epanechnikov && return(KernelEpanechnikov())
	s == :gaussian && return(KernelGaussian())
	s == :uniform && return(KernelUniform())
	s == :bartlett && return(KernelBartlett())
	error("Kernel function $(s) not recognised")
end
kernel_eval(x::Number, bw::Number, kf::KernelEpanechnikov)::Float64 = (-1.0 <= x / bw <= 1.0) ? (0.75 / bw) * (1 - (x^2 / bw^2)) : 0.0
kernel_eval(x::Number, bw::Number, kf::KernelGaussian)::Float64 = (1 / bw) * (1 / sqrt(2*pi)) * exp(-1 * (x^2 / (2 * bw^2)))
kernel_eval(x::Number, bw::Number, kf::KernelUniform)::Float64 = (-1.0 <= x / bw <= 1.0) ? 1 / 2*bw : 0.0
kernel_eval(x::Number, bw::Number, kf::KernelBartlett)::Float64 = (-1.0 <= x / bw <= 1.0) ? (1 - abs(x / bw)) : 0.0


"""
	hacvariance{T<:Number}(x::AbstractVector{T} ; kf::Symbol=:epanechnikov, bw::Int=-1)::Tuple{Float64, Int}

Get the heteroskedasticity and autocorrelation consistent variance estimator of data vector x.
The function has the following keyword arguments:
	kf <- Kernel function used in estimator. Valid values are :epanechnikov, :gaussian, :uniform, :bartlett
	bw <- Bandwidth used in estimator. If <= -1, then estimate bandwidth using Politis (2003) "Adaptive Bandwidth Choice"
"""
function hacvariance{T<:Number}(x::AbstractVector{T} ; kf::Symbol=:epanechnikov, bw::Int=-1)::Tuple{Float64, Int}
	if bw <= -1
		(bw, v, xCov) = DependentBootstrap.bandwidth_politis_2003(x)
	else
		(v, xCov) = (var(x), Array(Float64, 0))
	end
	length(xCov) < bw && append!(xCov, autocov(x, length(xCov)+1:bw)) #Get any additional autocovariances that we might need
	kf = symbol_to_kernel_func(kf)
	kernelAdjTerm = 1 / kernel_eval(0.0, bw, kf) #Used to scale kernel functions that don't satisfy k(0) = 1
	for m = 1:bw
		v += 2 * kernelAdjTerm * kernel_eval(m, bw, kf) * xCov[m]
	end
	return(max(v, 0.0), bw)
end
