module SymataIO

import Base: show

import Symata: Mxpr, SJSym, SSJSym, ListT, TimesT, PowerT,
       getsym, symname, mhead, margs,  getoptype, mtojsym,
       mxpr, mxprcf, Infinity, getkerneloptions, unicode_output, Qsym,
       CurrentContext, wrapout, using_unicode_output, comparison_translation,
       symnumerator, symdenominator, Formatting

const infix_with_space = Dict( :&& => true , :|| => true, :| => true)

# A space, or maybe not.
opspc(sym) = haskey(infix_with_space,sym) ? " " : getkerneloptions(:compact_output) ? "" : " "
const binaryops_space = collect(keys(comparison_translation) )

## Some of these, at least "^:=" can't be parsed as quoted symbols, but can be
## read as command line input.... So, we use some strings.
const binaryops_space_strings = [ "=>",  "->" , ":>" , "^:=" , "⇒", "∈" ]

function binaryopspc(ss)
    ss in binaryops_space && return " "
    s = string(ss)
    s in binaryops_space_strings && return " "
    return ""
end

## TODO: pass formatting choices to the functions rather than hardcoding them.

# Julia-like syntax
const FUNCL = '('
const FUNCR = ')'
const LISTL = '['
const LISTR = ']'

Istring() =  using_unicode_output() ? "𝕚" : "I"

# We could probably replace all instances of mtojsym below with this.
# It depends on which positions unicode symbols may occur.
function outsym(s)
    haskey(unicode_output, s) && using_unicode_output() && return unicode_output[s]
    os = mtojsym(s)
    haskey(unicode_output, os) && using_unicode_output() && return unicode_output[os]
    os
end

type FullFormData
    lfunc::String
    rfunc::String
end

const juliafullformdata = FullFormData("(",")")
const mmafullformdata = FullFormData("[","]")

fullform(io::IO, mx::Mxpr) = fullform(io,mx,juliafullformdata)

function fullform(io::IO, mx::Mxpr, data::FullFormData)
    print(io,mhead(mx))
    print(io, data.lfunc)
    if length(mx) > 0 fullform(io,mx[1],data) end
    for i in 2:length(mx)
        print(io,",")
        fullform(io,mx[i],data)
    end
    print(io, data.rfunc)
end

fullform(io::IO,x,data::FullFormData) = show(io,x)
fullform(x,data::FullFormData) = fullform(STDOUT,x,data)
fullform(x) = fullform(STDOUT,x,juliafullformdata)

mmafullform(io::IO,x) = fullform(io,x,mmafullformdata)
mmafullform(x) = mmafullform(STDOUT,x)

Base.show(io::IO, mx::Mxpr{:FullForm}) = fullform(io,mx[1],juliafullformdata)

symata_to_mma_fullform_string(x) = print_with_function_to_string(mmafullform, wrapout(x))

## Copied from base. There is probably a trick to avoid this.
function print_with_function_to_string(printfunc::Function, xs...; env=nothing)
    # specialized for performance reasons
    s = IOBuffer(Array{UInt8}(Base.tostr_sizehint(xs[1])), true, true)
    # specialized version of truncate(s,0)
    s.size = 0
    s.ptr = 1
    if env !== nothing
        env_io = IOContext(s, env)
        for x in xs
            printfunc(env_io, x)
        end
    else
        for x in xs
            printfunc(s, x)
        end
    end
    String(resize!(s.data, s.size))
end



# This puts unecessary parens on prefix functions.
#needsparen(x::Mxpr) = length(x) > 1
needsparen(x::Mxpr) = (length(x) > 1  && getoptype(mhead(x)) == :infix)
needsparen{T<:Integer}(x::Rational{T}) = true

needsparen(x::Real) =  x < 0
needsparen{T<:Integer}(x::Complex{T}) = (real(x) != 0)
needsparen{T<:Real}(x::Complex{T}) = true

needsparen(x) = false

# Mma displays gensyms with all the linenoise.
# So, we try disabling de_gensym
de_gensym(x) = x
# function de_gensym{T<:AbstractString}(str::T)
#     if str[1] == '#' && str[2] == '#'  # De-gensym local variables for display
#         return split(str,['#'],keep=false)[1]
#     else
#         return str
#     end
# end

#### Wrap output

abstract AbstractWO

needsparen(y::AbstractWO) = needsparen(y.x)

immutable WOSymbol <: AbstractWO
    x::Symbol
end

immutable WOBool <: AbstractWO
    x::Bool
end

immutable WORational{T}  <: AbstractWO
    x::T
end

immutable WOComplexInteger{T}  <: AbstractWO
    x::T
end
wrapout{T<:Integer}(x::Complex{T})=  WOComplexInteger(x)
Base.show(io::IO, ws::WOComplexInteger) = show_complexinteger(io, ws.x)

immutable WOComplexReal{T}  <: AbstractWO
    x::T
end
wrapout{T<:Real}(x::Complex{T}) = WOComplexReal(x)
Base.show(io::IO,ws::WOComplexReal) = show_complexreal(io, ws.x)

immutable WOComplexRational{T}  <: AbstractWO
    x::T
end
wrapout{T<:Integer}(x::Complex{Rational{T}}) = WOComplexRational(x)
Base.show(io::IO,ws::WOComplexRational) = show_complexrational(io, ws.x)

needsparen(x::WORational) = true
needsparen(x::WOComplexRational) = true

immutable WOAbstractFloat{T}  <: AbstractWO
    x::T
end
wrapout(x::AbstractFloat) = WOAbstractFloat(x)
Base.show(io::IO, ws::WOAbstractFloat) = show_float(io,ws.x)
## Not performant. We do a dictionary lookup every time we print a float.
function show_float(io,x::Float64)
    _show_float(io,x,getkerneloptions(:float_format))    
end

function show_float(io,x::BigFloat)
    _show_float(io,x,getkerneloptions(:bigfloat_format))
end

function _show_float(io,x,fmt)
    if fmt == ""
        show(io,x)
    else
        s = Formatting.sprintf1(fmt,x)   # always print a `.` for floating point numbers
        s = ismatch(r"\.",s) ? s : s * "."
        print(io,s)
    end
end

function show_float(io,x)
    show(io,x)
end

# This breaks printing
#Base.string{T<:AbstractWO}(y::T) = string(y.x)

#wrapout(x) = x  # defined in wrapout.jl

function wrapout(mx::Mxpr)
    mxprcf(mhead(mx), map(wrapout, margs(mx)))
end

function wrapout(s::Symbol)
    WOSymbol(s)
end

function wrapout(s::Bool)
    WOBool(s)
end

function wrapout(x::Rational)
    WORational(x)
end

Base.show(io::IO,ws::WOSymbol) = show_symbol(io, ws.x)


#### Symbol and SSJSym

# NB: This comment is only relevant if we change the Symata symbol implementation.
# This may break. It will only work if the value of s
# is the symbol name in the symbol table that is associated
# with s; ie it is a 'free' symbol. SSJSym does not carry symbol name information.
# This is only stored as the key to the symbol table.

Base.show(io::IO, s::SSJSym) = show_symbol(io, symname(s))

# SJSym is just a symbol, so this translates everything
# We need this, because SSJSym symbols are sometimes stored as Symbol's (ie SJSym)
#Base.show(io::IO, s::SJSym) = show_symbol(io,s)

function show_symbol(io::IO, s::SJSym)
    if haskey(unicode_output, s) && using_unicode_output()
        Base.show_unquoted(io,unicode_output[s])
    else
        ss = string(symname(s))
        ss = de_gensym(ss) # remove gensym characters
        Base.show_unquoted(io,Symbol(ss))
    end
end


Base.show(io::IO, x::WORational) = show_rational(io,x.x)

function show_rational(io::IO, x::Rational)
    show(io, symnumerator(x))
    print(io, "/")
    show(io, symdenominator(x))
end

# Yes, it is Base.REPLCompletions.latex_symbols
function Base.show(io::IO, x::Mxpr{:Subscript})
    if using_unicode_output()
        try
            s1 = string(x[1].x) # unwrap symbol
            for i in 2:length(x)
                y = typeof(x[i]) <: AbstractWO ? string(x[i].x) : string(x[i])
                s1 *=  Base.REPLCompletions.latex_symbols["\\_" * y]
            end
            show(io, wrapout(Symbol(s1)))
        catch
            show_prefix_function(io,x)
        end
    else
        show_prefix_function(io,x)
    end
end

# We display real part if it is 0.0
function show_complexreal{T<:Real}(io::IO, z::Complex{T})
#    show(io,real(z))
    show_float(io,real(z))    
    print(io," + ")
    show_float(io,imag(z))
#    show(io,imag(z))    
    print(io,Istring())
end

# Do not display real part if it is 0
function show_complexinteger{T<:Integer}(io::IO, z::Complex{T})
    if real(z) != 0
        show(io,real(z))
        print(io," + ")
    end
    if imag(z) == 1
        print(io,Istring())
    else
        iz = imag(z)
        if iz == -1
            print(io,"-")
        else
            show(io,iz)
        end
        print(io,Istring())
    end
end

function show_complexrational{T<:Integer}(io::IO, z::Complex{Rational{T}})
    if real(z) != 0
        show_rational(io,real(z))
        print(io," + ")
    end
    if imag(z) == 1
        print(io,Istring())
    else
        print(io,Istring(), " * ")
        show_rational(io,imag(z))
    end
end

function  Base.show(io::IO, mx::Mxpr{:DirectedInfinity})
    if length(mx) == 0
        show_symbol(io, :ComplexInfinity)
    elseif mx[1] == 1
        show_symbol(io, :Infinity)
    elseif mx[1] == -1
        Base.show(io, wrapout(mxprcf(:Times, -1, Infinity)))
    else
        print(io, "DirectedInfinity(")
        print(io, mx[1])
        print(io, ")")
    end
end

# This is causing stack overflow in some cases.
# Base.show{T<:BigFloat}(io::IO,x::T) = Base.showcompact(io,x)

Base.show(io::IO, v::WOBool) = show_bool(io,v.x)

function show_bool(io::IO, v::Bool)
    v ? Base.show_unquoted(io,:True) : Base.show_unquoted(io,:False)
end

# For Holdform, arguments are not evaluated, as in Hold.
# But, in addition, Holdform is not printed.
function Base.show(io::IO, s::Mxpr{:HoldForm})
    Base.show(io,s[1])
end

function Base.show(io::IO, s::Mxpr)
    if getoptype(mhead(s)) == :binary
        return show_binary(io,s)
    elseif getoptype(mhead(s)) == :infix
        return show_infix(io,s)
    end
    show_prefix_function(io,s)
end

function Base.show(io::IO, mx::Mxpr{:Comparison})
    args = margs(mx)
    for i in 1:length(args)-1
        show(io, outsym(args[i]))
        print(io," ")
    end
    isempty(args) || show(io,args[end])
end


function show_prefix_function(io::IO, mx::Mxpr)
    if ! isa(mx,ListT)
        mh = mhead(mx)
        wantparen::Bool = false
        if isa(mh,Mxpr)
            wantparen = getoptype(mhead(mh)) == :infix ? true : false
        end
        if wantparen
            print(io,"(")  # refactor this! --> maybeparen(wantparen,obj) or s.t.
        end
        show(io, wrapout(mhead(mx)))
        if wantparen
            print(io,")")
        end
    end
    args = mx.args
    print(io,mhead(mx) == getsym(:List) ? LISTL : FUNCL)
    wantparens = true
    if mhead(mx) == :List wantparens = false end
    for i in 1:length(args)-1
        if needsparen(args[i]) && wantparens
            print(io,"(")
        end
        show(io,args[i])
        if needsparen(args[i]) && wantparens
            print(io,")")
        end
        print(io,",")
    end
    isempty(args) || show(io,args[end])
    print(io,mx.head == getsym(:List) ? LISTR : FUNCR)
end

function show_binary(io::IO, mx::Mxpr)
    if length(mx) != 2
        show_prefix_function(io,mx)
    else
        opstr = outsym(mhead(mx))
        lop = mx[1]
        if needsparen(lop)
            print(io,"(")
            show(io,lop)
            print(io,")")
        else
            show(io,lop)
        end
        print(io, binaryopspc(opstr), opstr , binaryopspc(opstr))
        rop = mx[2]
        if  needsparen(rop)
            print(io,"(")
            show(io,rop)
            print(io,")")
        else
            show(io,rop)
        end
    end
end

function Base.show(io::IO, mx::Mxpr{:Part})
    args = margs(mx)
    show(io,args[1])
    print(io,"[")
    show(io,args[2])
    for i in 3:length(args)
        print(io,",")
        show(io,args[i])
    end
    print(io,"]")
end

# unary minus
function Base.show(io::IO, mx::Mxpr{:Minus})
    arg = mx.args[1]
    if isa(arg,Number) || isa(arg,SJSym)
        print(io,"-")
        show(io,arg)
    else
        print(io,"-(")
        show(io,arg)
        print(io,")")
    end
end

function Base.show(io::IO, mx::Mxpr{:Plus})
    args = margs(mx)
    if length(args) < 1
        error("show: can't show Plus with no args.")
    end
    show(io,args[1])
    for i in 2:length(args)
        if isa(args[i],Mxpr{:Minus})
            print(io, " - ")
            show(io,(args[i]).args[1])
        else
            if isa(args[i], TimesT) && typeof(args[i][1])  <:Union{AbstractFloat,Integer} && args[i][1] < 0
                show_infix(io, args[i], true)
            else
                print(io, " + ")
                show(io, args[i])
            end
        end
    end
end

# This includes Times. No spaces surround the infix symbol

show_infix(io::IO, mx::Mxpr)  = show_infix(io,mx, false)
function show_infix(io::IO, mx::Mxpr, spaceminus::Bool)
    args = margs(mx)
    np = false
    sepsym = mtojsym(mhead(mx))
    spc = opspc(sepsym)
    # uncomment following to print space for multiplication rather than *. But, I want "InputForm" for now,
    # so we can copy output to input.
#    if mhead(mx) == :Times sepsym = " " end # not a sym. Maybe we should make them all strings
    startind = 1
    if isa(mx,TimesT) && ! isempty(args)
        a1 = args[1]
        if a1 == -1
            print(io, spaceminus ? " - " : "-")
            startind = 2
        elseif typeof(a1) <:Union{AbstractFloat,Integer}
            if a1 < 0 && spaceminus
                print(io, " - ", - a1)
            else
                print(io, a1)
                if length(args) > 1 && isa(args[2],Union{Number,PowerT})
                    print(io, spc, sepsym, spc)
                end
            end
            startind = 2
        end
    end
    for i in startind:length(args)-1
        arg = args[i]
        if needsparen(arg)
            np = true
            print(io,"(")
        else
            np = false
        end
        show(io,arg)
        if np
            print(io,")")
        end
        print(io, spc, sepsym, spc)
    end
    if ! isempty(args)
        if needsparen(args[end])
            np = true
            print(io,"(")
        else
            np = false
        end
        show(io, args[end])
        if np
            print(io,")")
        end
    end
end

function Base.show(io::IO, mx::Mxpr{:Blank})
    print(io,"_")
    if length(mx) > 0
        show(io,mx[1])
    end
end

function Base.show(io::IO, mx::Mxpr{:BlankSequence})
    print(io,"__")
    if length(mx) > 0
        show(io,mx[1])
    end
end

function Base.show(io::IO, mx::Mxpr{:BlankNullSequence})
    print(io,"__")
    if length(mx) > 0
        show(io,mx[1])
    end
end

function Base.show(io::IO, mx::Mxpr{:Pattern})
    show(io,mx[1])
    if isa(mx[2],Mxpr{:Blank})
        show(io,mx[2])
    else
        print(io,"::(")
        show(io,mx[2])
        print(io,")")
    end
end

function Base.show(io::IO, mx::Mxpr{:Element})
    if using_unicode_output()
        show_binary(io,mx)
    else
        show_prefix_function(io,mx)
    end
end

function Base.show(io::IO, qs::Qsym)
    if qs.context != CurrentContext.name
        show(io,qs.context)
        print(io,".")
    end
    show(io,qs.name)
end


end # module SymataIO
