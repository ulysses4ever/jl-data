# Covariance Matrix Adaptation Evolution Strategy
# ==================
#
# Implementation: (μ/μ_I,λ)-CMA-ES
#
# μ is the number of parents
# λ is the number of offspring.
#
function cmaes{T}(objfun::Function, val::T, stg::Strategy;
              μ::Integer = 1,
              λ::Integer = 1,
              iterations::Integer = 1_000,
              tol::Float64 = 1e-10,
              verbose = false)

    @assert μ < λ "Offspring population must be larger then parent population"

    # Initialize parent population
    N = length(val)
    parent = copy(val)
    C = eye(N)
    s = zeros(N)
    s_σ = zeros(N)
    σ = 1.0
    E = zeros(N, λ)
    W = zeros(N, λ)

    population = fill(val, μ)
    offspring = Array(T, λ)
    fitpop = fill(Inf, μ)
    fitoff = fill(Inf, λ)

    count = 0
    while true
        SqrtC = (C + C')/2.0
        try
            SqrtC = chol(SqrtC)
        catch
            break
        end

        for i in 1:λ
            # offspring are generated by transforming standard normally distributed random vectors using a transformation matrix
            E[:,i] = randn(N)
            W[:,i] = σ * (SqrtC * E[:,i])
            offspring[i] = parent + W[:,i]   # (L1)
            fitoff[i] = objfun(offspring[i]) # Evaluate fitness
        end

        # Select new parent population
        idx = sortperm(fitoff)[1:μ]
        for i in 1:μ
            population[i] = offspring[idx[i]]
            fitpop[i] = fitoff[idx[i]]
        end

        w = vec(mean(W[:,idx], 2))
        ɛ = vec(mean(E[:,idx], 2))
        parent += w # (L2) forming recombinant perent for next generation
        s = (1.0 - 1.0/stg[:τ])*s + (sqrt(μ/stg[:τ] * (2.0 - 1.0/stg[:τ]))/σ)*w      # (L3)
        C = (1.0 - 1.0/stg[:τ_c]).*C + (s./stg[:τ_c])*s'                             # (L4)
        s_σ = (1.0 - 1.0/stg[:τ_σ])*s_σ + sqrt(μ/stg[:τ_σ]*(2.0 - 1.0/stg[:τ_σ]))*ɛ  # (L5)
        σ = σ*exp(((s_σ'*s_σ)[1] - N)/(2*N*sqrt(N)))                                  # (L6)

        # termination condition
        count += 1
        if count == iterations || σ < tol
            break
        end
        verbose && println("BEST: $(fitpop[1]): $(σ)")
    end

    return population[1], fitpop[1], count
end