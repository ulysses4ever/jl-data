for (fname, elty) in ((:mumps_associate_matrix_float, Float32),
                      (:mumps_associate_matrix_double, Float64),
                      (:mumps_associate_matrix_float_complex, Complex64),
                      (:mumps_associate_matrix_double_complex, Complex128))

  @eval begin

    """Register the matrix `A` with the `Mumps` object `mumps`.
    This function makes it possible to define the matrix on the host
    only. If the matrix is defined on all nodes, there is no need to
    use this function."""
    function associate_matrix!{Ti <: MUMPSIntDataType}(mumps :: Mumps{$elty}, A :: SparseMatrixCSC{$elty,Ti})

      n = size(A, 1);
      size(A, 2) == n || throw(MUMPSException("Input matrix must be square"))

      # Symmetric factorization only accesses the lower triangle.
      B = mumps.__sym > 0 ? tril(A) : A;

      # Obtain B in coordinate format.
      nz = nnz(B);
      vals = convert(Array{$elty,1}, B.nzval);    # Necessary?
      irow = convert(Array{Int32,1}, B.rowval);   # Necessary?
      jcol = zeros(Int32, nz, 1);
      for i = 1 : n
        jcol[B.colptr[i] : B.colptr[i+1]-1] = i;
      end

      id = reinterpret(Ptr{Void}, mumps.__id)
      @mumps_call($(string(fname)), Void,
                  (Ptr{Void}, Int32, Int32, Ptr{$elty}, Ptr{Int32}, Ptr{Int32}),
                          id,     n,    nz,       vals,       irow,       jcol);
      mumps.n = n;
      mumps.nnz = mumps.infog[29];
      return mumps;
    end

  end
end

# Associate a generally-typed matrix with a Mumps type. Attempt conversion.
# An InexactError should be raised if, e.g., mumps is Float64 and A is Complex128.
associate_matrix!{Tm <: MUMPSValueDataType, Tv <: Number, Ti <: Integer}(mumps :: Mumps{Tm}, A :: SparseMatrixCSC{Tv,Ti}) = associate_matrix!(mumps, convert(SparseMatrixCSC{Tm,Int64}, A));

# associate_matrix for dense matrices.
associate_matrix!{Tm <: MUMPSValueDataType, Tv <: Number}(mumps :: Mumps{Tm}, A :: Array{Tv,2}) = associate_matrix!(mumps, convert(SparseMatrixCSC{Tm,Int64}, sparse(A)));


for (fname, infoname, elty, infoty) in ((:mumps_factorize_float, :mumps_get_info_float, Float32, Float32),
                                        (:mumps_factorize_double, :mumps_get_info_double, Float64, Float64),
                                        (:mumps_factorize_float_complex, :mumps_get_info_float_complex, Complex64, Float32),
                                        (:mumps_factorize_double_complex, :mumps_get_info_double_complex, Complex128, Float64))

  @eval begin

    """Factorize the matrix registered with the `Mumps` instance.
    The matrix must have been previously registered with `associate_matrix()`.
    After the factorization, the determinant, if requested, is stored in
    `mumps.det`. The MUMPS error code is stored in `mumps.err`. """
    function factorize!(mumps :: Mumps{$elty})

      id = reinterpret(Ptr{Void}, mumps.__id)
      @mumps_call($(string(fname)), Void, (Ptr{Void},), id)
      @mumps_call($(string(infoname)), Void,
                  (Ptr{Void}, Ptr{Int32},  Ptr{$infoty}),
                          id, mumps.infog, mumps.rinfog)

      if mumps.icntl[33] == 1
        mumps.det = mumps.rinfog[12] * 2.0^(mumps.infog[34]);
      end
      mumps.err = mumps.infog[1];
      return mumps;
    end

  end
end

"""Register the right-hand side(s) `rhs` with the `Mumps`
object `mumps`. This function makes it possible to define the right-
-hand side(s) on the host only. If the right-hand side(s) are defined
on all nodes, there is no need to use this function.

This variant makes a copy of the right-hand side to avoid overwriting it."""
function associate_rhs{Tv <: MUMPSValueDataType}(mumps :: Mumps{Tv}, rhs :: Array{Tv})
  associate_rhs!(mumps, copy(rhs))
end


for (fname, elty) in ((:mumps_associate_rhs_float, Float32),
                      (:mumps_associate_rhs_double, Float64),
                      (:mumps_associate_rhs_float_complex, Complex64),
                      (:mumps_associate_rhs_double_complex, Complex128))

  @eval begin

    """Register the right-hand side(s) `rhs` with the `Mumps`
    object `mumps`. This function makes it possible to define the right-
    -hand side(s) on the host only. If the right-hand side(s) are defined
    on all nodes, there is no need to use this function."""
    function associate_rhs!(mumps :: Mumps{$elty}, rhs :: Array{$elty})

      n = size(rhs, 1);
      n == mumps.n || throw(MUMPSException("rhs has incompatible dimension"))

      nrhs = size(rhs, 2);
      x = rhs[:];  # Make a copy; will be overwritten with solution.

      id = reinterpret(Ptr{Void}, mumps.__id)
      @mumps_call($(string(fname)), Void,
                  (Ptr{Void}, Int32, Ptr{$elty}),
                          id,  nrhs,          x)
      return mumps
    end

  end
end

# Associate a generally-typed rhs with a Mumps type. Attempt conversion.
# An InexactError should be raised if, e.g., mumps is Float64 and rhs is Complex128.
associate_rhs!{Tm <: MUMPSValueDataType, Tv <: Number}(mumps :: Mumps{Tm}, rhs :: Array{Tv}) = associate_rhs!(mumps, convert(Array{Tm}, rhs));


for (fname, infoname, elty, infoty) in ((:mumps_solve_float, :mumps_get_info_float, Float32, Float32),
                                        (:mumps_solve_double, :mumps_get_info_double, Float64, Float64),
                                        (:mumps_solve_float_complex, :mumps_get_info_float_complex, Complex64, Float32),
                                        (:mumps_solve_double_complex, :mumps_get_info_double_complex, Complex128, Float64))

  @eval begin

    """Solve the system registered with the `Mumps` object `mumps`.
    The matrix and right-hand side(s) must have been previously registered
    with `associate_matrix()` and `associate_rhs()`. The optional keyword
    argument `transposed` indicates whether the user wants to solve the
    forward or transposed system. The solution is stored internally and must
    be retrieved with `get_solution()`."""
    function solve!(mumps :: Mumps{$elty}; transposed :: Bool=false)

      id = reinterpret(Ptr{Void}, mumps.__id)
      @mumps_call($(string(fname)), Void,
                  (Ptr{Void}, Int32),
                          id, transposed ? 1 : 0)
      @mumps_call($(string(infoname)), Void,
                  (Ptr{Void}, Ptr{Int32},  Ptr{$infoty}),
                          id, mumps.infog, mumps.rinfog)

      mumps.err = mumps.infog[1]
      return mumps
    end

  end
end


for (fname, solname, elty) in ((:mumps_get_nrhs_float, :mumps_get_solution_float, Float32),
                               (:mumps_get_nrhs_double, :mumps_get_solution_double, Float64),
                               (:mumps_get_nrhs_float_complex, :mumps_get_solution_float_complex, Complex64),
                               (:mumps_get_nrhs_double_complex, :mumps_get_solution_double_complex, Complex128))

  @eval begin

    """Retrieve the solution of the system solved by `solve()`. This
    function makes it possible to ask MUMPS to assemble the final solution
    on the host only, and to retrieve it there."""
    function get_solution(mumps :: Mumps{$elty})

      id = reinterpret(Ptr{Void}, mumps.__id)
      nrhs = Int(@mumps_call($(string(fname)), Int32, (Ptr{Void},), id))
      x = zeros($elty, mumps.n * nrhs)
      @mumps_call($(string(solname)), Void,
                  (Ptr{Void}, Ptr{$elty}),
                          id,          x)

      return reshape(x, Int(mumps.n), nrhs)
    end

  end
end

# Convenience functions.

"""Combined associate_matrix / factorize.
Presume that `A` is available on all nodes."""
function factorize!{Tm <: MUMPSValueDataType, Tv <: Number, Ti <: Integer}(mumps :: Mumps{Tm}, A :: SparseMatrixCSC{Tv,Ti})
  mumps = associate_matrix!(mumps, A);  # A will be converted by associate_matrix.
  return factorize!(mumps);
end

"""Combined associate_matrix / factorize.
Presume that `A` is available on all nodes."""
factorize!{Tm <: MUMPSValueDataType, Tv <: Number}(mumps :: Mumps{Tm}, A :: Array{Tv}) = factorize!(mumps, convert(SparseMatrixCSC{Tm,Int64}, sparse(A)));


"""Combined associate_rhs / solve.
Presume that `rhs` is available on all nodes.
The optional keyword argument `transposed` indicates whether
the user wants to solve the forward or transposed system.
The solution is retrieved and returned."""
function solve{Tm <: MUMPSValueDataType, Tv <: Number}(mumps :: Mumps{Tm}, rhs :: Array{Tv}; transposed :: Bool=false)
  mumps = associate_rhs!(mumps, rhs);  # rhs will be converted by associate_rhs.
  mumps = solve!(mumps, transposed=transposed);
  return get_solution(mumps);
end


"""Combined analyze / factorize / solve.
Presume that `A` and `rhs` are available on all nodes.
The optional keyword argument `transposed` indicates whether
the user wants to solve the forward or transposed system.
The solution is retrieved and returned."""
function solve{Tm <: MUMPSValueDataType, Tv <: Number, Tr <: Number, Ti <: Integer}(mumps :: Mumps{Tm}, A :: SparseMatrixCSC{Tv,Ti}, rhs :: Array{Tr}; transposed :: Bool=false)

  mumps = factorize!(mumps, A);
  return solve(mumps, rhs, transposed=transposed);
end

solve{Tm <: MUMPSValueDataType, Tv <: Number, Tr <: Number}(mumps :: Mumps{Tm}, A :: Array{Tv,2}, rhs :: Array{Tr}) = solve(mumps, sparse(A), rhs);


"""Combined initialize / analyze / factorize / solve.
Presume that `A` and `rhs` are available on all nodes.
The optional keyword argument `sym` indicates the symmetry of `A`.
The solution is retrieved and returned."""
function solve{Tv <: Number, Tr <: Number, Ti <: MUMPSIntDataType}(A :: SparseMatrixCSC{Tv,Ti}, rhs :: Array{Tr}; sym :: Int=mumps_unsymmetric)

  Tm = (Tv <: Complex || Tr <: Complex) ? Complex128 : Float64;  # Could be smarter.
  mumps = Mumps{Tm}(sym, default_icntl, default_cntl64);
  x = solve(mumps, A, rhs);
  finalize(mumps);
  return x;
end

solve{Tv <: Number, Tr <: Number}(A :: Array{Tv,2}, rhs :: Array{Tr}; sym :: Int=mumps_unsymmetric) = solve(sparse(A), rhs, sym=sym);
