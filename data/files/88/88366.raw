"""
Store a diagonally-scaled Toeplitz‚àòHankel matrix:

    DL(T‚àòH)DR

where the Hankel matrix `H` is non-negative definite. This allows a Cholesky decomposition in ùí™(K¬≤N) operations and ùí™(KN) storage, K = log N log …õ‚Åª¬π.
"""
immutable ToeplitzHankelPlan{S}
    T::TriangularToeplitz{S}
    C::Vector{Vector{S}}
    DL::Vector{S}
    DR::Vector{S}
    ToeplitzHankelPlan(T,C,DL,DR)=new(T,C,DL,DR)
end

function ToeplitzHankelPlan(T::TriangularToeplitz,C::Vector,DL::AbstractVector,DR::AbstractVector)
    S=promote_type(eltype(T),eltype(C[1]),eltype(DL),eltype(DR))
    ToeplitzHankelPlan{S}(T,C,collect(S,DL),collect(S,DR))
end
ToeplitzHankelPlan(T::TriangularToeplitz,C::Matrix) =
    ToeplitzHankelPlan(T,C,ones(size(T,1)),ones(size(T,2)))
ToeplitzHankelPlan(T::TriangularToeplitz,H::Hankel,D...) =
    ToeplitzHankelPlan(T,partialchol(H),D...)

*(P::ToeplitzHankelPlan,v::AbstractVector)=P.DL.*toeplitzcholmult(P.T,P.C,P.DR.*v)

function partialchol(H::Hankel)
    # Assumes positive definite
    œÉ=eltype(H)[]
    n=size(H,1)
    C=Vector{eltype(H)}[]
    v=[H[:,1];vec(H[end,2:end])]
    d=diag(H)
    @assert length(v) ‚â• 2n-1
    reltol=maxabs(d)*eps(eltype(H))*log(n)
    for k=1:n
        mx,idx=findmax(d)
        if mx ‚â§ reltol break end
        push!(œÉ,inv(mx))
        push!(C,v[idx:n+idx-1])
        for j=1:k-1
            nCjidxœÉj = -C[j][idx]*œÉ[j]
            Base.axpy!(nCjidxœÉj, C[j], C[k])
        end
        @simd for p=1:n
            @inbounds d[p]-=C[k][p]^2/mx
        end
    end
    for k=1:length(œÉ) scale!(C[k],sqrt(œÉ[k])) end
    C
end

function toeplitzcholmult(T,C,v)
    n,K = length(v),length(C)
    ret,temp1,temp2 = zero(v),zero(v),zero(v)
    un,ze = one(eltype(v)),zero(eltype(v))
    broadcast!(*, temp1, C[K], v)
    A_mul_B!(un, T, temp1, ze, temp2)
    broadcast!(*, ret, C[K], temp2)
    for k=K-1:-1:1
        broadcast!(*, temp1, C[k], v)
        A_mul_B!(un, T, temp1, ze, temp2)
        broadcast!(*, temp1, C[k], temp2)
        broadcast!(+, ret, ret, temp1)
    end
    ret
end


# Diagonally-scaled Toeplitz‚àòHankel polynomial transforms

function leg2chebTH{S}(::Type{S},n)
    Œª = Œõ(0:half(S):n-1)
    t = zeros(S,n)
    t[1:2:end] = Œª[1:2:n]
    T = TriangularToeplitz(2t/œÄ,:U)
    H = Hankel(Œª[1:n],Œª[n:end])
    DL = ones(S,n)
    DL[1] /= 2
    T,H,DL
end

function cheb2legTH{S}(::Type{S},n)
    t = zeros(S,n-1)
    t[1:2:end] = Œõ(0:one(S):div(n-2,2),-half(S),one(S))
    T = TriangularToeplitz(t,:U)
    h = Œõ(1:half(S):n-1,zero(S),3half(S))
    H = Hankel(h[1:n-1],h[n-1:end])
    DL = 3half(S):n-half(S)
    DR = -(one(S):n-one(S))/4
    T,H,DL,DR
end

function leg2chebuTH{S}(::Type{S},n)
    Œª = Œõ(0:half(S):n-1)
    t = zeros(S,n)
    t[1:2:end] = Œª[1:2:n]./(((1:2:n)-2))
    T = TriangularToeplitz(-2t/œÄ,:U)
    H = Hankel(Œª[1:n]./((1:n)+1),Œª[n:end]./((n:2n-1)+1))
    T,H
end

function ultra2ultraTH{S}(::Type{S},n,Œª‚ÇÅ,Œª‚ÇÇ)
    @assert abs(Œª‚ÇÅ-Œª‚ÇÇ) < 1
    DL = (zero(S):n-one(S))+Œª‚ÇÇ
    jk = 0:half(S):n-1
    t = zeros(S,n)
    t[1:2:n] = Œõ(jk,Œª‚ÇÅ-Œª‚ÇÇ,one(S))[1:2:n]
    T = TriangularToeplitz(scale!(inv(gamma(Œª‚ÇÅ-Œª‚ÇÇ)),t),:U)
    h = Œõ(jk,Œª‚ÇÅ,Œª‚ÇÇ+one(S))
    scale!(gamma(Œª‚ÇÇ)/gamma(Œª‚ÇÅ),h)
    H = Hankel(h[1:n],h[n:end])
    DR = ones(S,n)
    T,H,DL,DR
end

function jac2jacTH{S}(::Type{S},n,Œ±,Œ≤,Œ≥,Œ¥)
    @assert Œ≤ == Œ¥
    @assert abs(Œ±-Œ≥) < 1
    @assert Œ±+Œ≤ > -1
    jk = zero(S):n-one(S)
    DL = (2jk+Œ≥+Œ≤+one(S)).*Œõ(jk,Œ≥+Œ≤+one(S),Œ≤+one(S))
    T = TriangularToeplitz(Œõ(jk,Œ±-Œ≥,one(S)),:U)
    H = Hankel(Œõ(jk,Œ±+Œ≤+one(S),Œ≥+Œ≤+two(S)),Œõ(jk+n-one(S),Œ±+Œ≤+one(S),Œ≥+Œ≤+two(S)))
    DR = Œõ(jk,Œ≤+one(S),Œ±+Œ≤+one(S))/gamma(Œ±-Œ≥)
    T,H,DL,DR
end

th_leg2chebplan{S}(::Type{S},n)=ToeplitzHankelPlan(leg2chebTH(S,n)...,ones(S,n))
th_cheb2legplan{S}(::Type{S},n)=ToeplitzHankelPlan(cheb2legTH(S,n)...)
th_leg2chebuplan{S}(::Type{S},n)=ToeplitzHankelPlan(leg2chebuTH(S,n)...,1:n,ones(S,n))
th_ultra2ultraplan{S}(::Type{S},n,Œª‚ÇÅ,Œª‚ÇÇ)=ToeplitzHankelPlan(ultra2ultraTH(S,n,Œª‚ÇÅ,Œª‚ÇÇ)...)
th_jac2jacplan{S}(::Type{S},n,Œ±,Œ≤,Œ≥,Œ¥)=ToeplitzHankelPlan(jac2jacTH(S,n,Œ±,Œ≤,Œ≥,Œ¥)...)


immutable ChebyshevToLegendrePlan{TH}
    toeplitzhankel::TH
end

ChebyshevToLegendrePlan{S}(::Type{S},n) = ChebyshevToLegendrePlan(th_cheb2legplan(S,n))


function *(P::ChebyshevToLegendrePlan,v::AbstractVector)
    w = zero(v)
    S,n = eltype(v),length(v)
    w[1:2:end] = -one(S)./(one(S):two(S):n)./(-one(S):two(S):n-two(S))
    [dot(w,v);P.toeplitzhankel*view(v,2:n)]
end


th_leg2cheb(v)=th_leg2chebplan(eltype(v),length(v))*v
th_cheb2leg(v) = ChebyshevToLegendrePlan(eltype(v),length(v))*v

th_leg2chebu(v)=th_leg2chebuplan(eltype(v),length(v))*v
th_ultra2ultra(v,Œª‚ÇÅ,Œª‚ÇÇ)=th_ultra2ultraplan(eltype(v),length(v),Œª‚ÇÅ,Œª‚ÇÇ)*v
th_jac2jac(v,Œ±,Œ≤,Œ≥,Œ¥)=th_jac2jacplan(eltype(v),length(v),Œ±,Œ≤,Œ≥,Œ¥)*v
