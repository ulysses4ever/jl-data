#!/usr/bin/env julia
# vim:set ft=julia ts=4 sw=4 sts=4 autoindent:

# Parse CoNLL-X (also known as CoNLL 2006) style dependency data.
#
# Data format description:
#
#   http://nextens.uvt.nl/depparse-wiki/DataFormat
#
# Author:   Pontus Stenetorp    <pontus stenetorp se>
# Version:  2014-05-01

module CoNLLX

import Base: done, next, show, start

# TODO: Serialise, de-serialize test!
# TODO: Makefile sanity...
# TODO: Callable as a command-line script for shuffling, etc.

type Token
    # [2,|Sentence|+1] sentence-internal unique token identifier.
    id::Uint
    # Token form, the actual text.
    form::String
    # Lemma of the token form.
    lemma::String
    # Coarse part-of-speech.
    cpostag::String
    # Fine-grained part-of-speech.
    postag::String
    # Set of syntactic and/or morphological features.
    feats::String # TODO: Should be an ordered set, is there one in Julia?
    # [1,|Sentence|+1] id of the head of the token, 0 for no head.
    head::Uint
    # Dependency relation to the head.
    deprel::String
    # [1,|Sentence|+1] id of the projective head of the token, 0 for no phead.
    phead::Uint
    # Dependency relation to the projective head.
    pdeprel::String
end

const NOHEAD = 0
const NOVAL = "_"

function show(io::IO, t::Token)
    head, phead = map(i -> i == NOHEAD ? NOVAL : string(i - 1),
        (t.head, t.phead))

    print(io, string("$(t.id-1)\t$(t.form)\t$(t.lemma)\t$(t.cpostag)\t",
        "$(t.postag)\t$(t.feats)\t$head\t$(t.deprel)\t$phead\t",
        "$(t.pdeprel)"))
end

function blind!(t::Token)
    t.head, t.phead = (NOHEAD, NOHEAD)
    t.deprel, t.pdeprel = (NOVAL, NOVAL)
end

typealias Sentence Array{Token}
typealias Sentences Array{Sentence}

# TODO: Sentence from another Sentence with blind argument.
# TODO: Sentences from another Sentences with blind argument.

# Not the friendliest of expressions, but it will do the trick.
const CONLLXREGEX = Regex(string("^([0-9]+)\t([^\t]+)\t([^\t]+)\t([^\t]+)\t",
    "([^\t]+)\t([^\t]+)\t([0-9]+)\t([^\t]+)\t([0-9]+|_)\t([^\t]+)\n\$"))

# Parsing implemented using the iterator protocol.
type CoNLLXParse
    stream::IO
    blind::Bool
    linenum::Uint
end

conllxparse(stream::IO; blind=false) = CoNLLXParse(stream, blind, 0)

start(::CoNLLXParse) = nothing

function next(itr::CoNLLXParse, nada)
    sent = Token[]

    for line in eachline(itr.stream)
        itr.linenum += 1

        # Ignore comments (this is a format extension).
        if line[1] == '#'
            continue
        end

        # Empty lines are used as sentence separators.
        if line[1] == '\n'
            if length(sent) > 1
                return (sent, nothing)
                sent = Token[]
            end
            continue
        end

        soup = match(CONLLXREGEX, line)
        if soup == nothing
            warn(string("failed to parse:$(itr.linenum) \"",
                rstrip(line, '\n'), "\""))
            continue
        end

        (id_str, form, lemma, cpostag, postag, feats, head_str, deprel,
            phead_str, pdeprel) = soup.captures
        id, head = map(int, (id_str, head_str))

        # Bump the ids internally to allow it to double as an index (necessary
        #   due to Julia being 1-indexed).
        id += 1
        head += 1

        # The projective head is optional.
        if phead_str != NOVAL
            # Convert and bump the id.
            phead = int(phead_str) + 1
        else
            phead = NOHEAD
        end

        tok = Token(id, form, lemma, cpostag, postag, feats, head, deprel,
            phead, pdeprel)

        # Remove (blind) any gold annotations.
        if itr.blind
            blind!(tok)
        end

        push!(sent, tok)
    end

    # Yield what we have observed before EOF (could be an empty Sentence).
    return (sent, nothing)
end

done(itr::CoNLLXParse, nada) = eof(itr.stream)

#for a in conllxparse(open("res/debug.conllx"))
#    println(a)
#end

#a = collect(conllxparse(open("res/debug.conllx")))
#a = collect(conllxparse(open("res/talbanken/train.conllx")))

end
