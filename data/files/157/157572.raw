# Copyright (c) 2015 Michael Eastwood
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program. If not, see <http://www.gnu.org/licenses/>.

immutable Antenna
    position :: Position
end

immutable Baseline
    antenna1 :: Int
    antenna2 :: Int
end

type Metadata
    antennas  :: Vector{Antenna}
    baselines :: Vector{Baseline}
    channels  :: Vector{Float64}
    phase_center :: Direction
    time :: Epoch
end

function Metadata(ms::Table)
    antennas  = read_antennas(ms)
    baselines = read_baselines(ms)
    channels  = read_channels(ms)
    phase_center = read_phase_center(ms)
    time = read_time(ms)
    Metadata(antennas, baselines, channels, phase_center, time)
end

Nant(meta::Metadata)  = length(meta.antennas)
Nfreq(meta::Metadata) = length(meta.channels)
Nbase(meta::Metadata) = length(meta.baselines)

"Construct the reference frame of the interferometer."
function reference_frame(meta)
    frame = ReferenceFrame()
    set!(frame, meta.time)
    set!(frame, position(meta))
    frame
end

"Get the interferometer location by averaging the antenna positions."
function position(meta)
    x = 0.0
    y = 0.0
    z = 0.0
    for i = 1:Nant(meta)
        pos = meta.antennas[i].position
        x += pos.x
        y += pos.y
        z += pos.z
    end
    x /= Nant(meta)
    y /= Nant(meta)
    z /= Nant(meta)
    Position(pos"ITRF", x, y, z)
end

"Read antenna positions from the `ANTENNA` subtable."
function read_antennas(ms::Table)
    antenna_table = ms[kw"ANTENNA"] |> Table
    xyz = antenna_table["POSITION"]
    antennas = Antenna[]
    for i = 1:size(xyz, 2)
        x = xyz[1,i]
        y = xyz[2,i]
        z = xyz[3,i]
        position = Position(pos"ITRF", x, y, z)
        push!(antennas, Antenna(position))
    end
    unlock(antenna_table)
    antennas
end

"Read baselines from the main table."
function read_baselines(ms::Table)
    ant1 = ms["ANTENNA1"]
    ant2 = ms["ANTENNA2"]
    [Baseline(ant1[α]+1, ant2[α]+1) for α = 1:length(ant1)]
end

"Read frequency channels from the `SPECTRAL_WINDOW` subtable."
function read_channels(ms::Table)
    spw_table = ms[kw"SPECTRAL_WINDOW"] |> Table
    channels  = spw_table["CHAN_FREQ", 1]
    unlock(spw_table)
    channels
end

"Read the phase direction from the `PHASE_DIR` subtable."
function read_phase_center(ms::Table)
    field_table = ms[kw"FIELD"] |> Table
    dir = field_table["PHASE_DIR"]
    unlock(field_table)
    Direction(dir"J2000", dir[1]*radians, dir[2]*radians)
end

"Read the time from the main table."
function read_time(ms::Table)
    time = ms["TIME", 1]
    Epoch(epoch"UTC", time*seconds)
end

immutable UVW
    u :: Vector{Float64}
    v :: Vector{Float64}
    w :: Vector{Float64}
end

function UVW(meta::Metadata)
    u = zeros(Nbase(meta))
    v = zeros(Nbase(meta))
    w = zeros(Nbase(meta))
    for α = 1:Nbase(meta)
        antenna1 = meta.baselines[α].antenna1
        antenna2 = meta.baselines[α].antenna2
        r1 = meta.antennas[antenna1].position
        r2 = meta.antennas[antenna2].position
        u[α] = r1.x - r2.x
        v[α] = r1.y - r2.y
        w[α] = r1.z - r2.z
    end
    UVW(u, v, w)
end

