#################### MCMCParam Methods ####################

function Base.show(io::IO, p::MCMCParam)
  msg = string(ifelse(p.monitor, "A ", "An un"),
               "monitored node of type \"", summary(p), "\"\n")
  print(io, msg)
  show(io, p.data)
  print(io, "\n")
end

function Base.showall(io::IO, p::MCMCParam)
  show(io, p)
  print(io, "\nFunction:\n")
  show(io, p.eval.code)
  print(io, "\n\nNode Dependencies:\n")
  show(io, p.deps)
  print(io, "\n")
end


#################### MCMCLogical Constructors ####################

function MCMCLogical(data, expr::Expr, monitor::Bool)
  MCMCLogical(data, monitor, paramfx(expr), paramdeps(expr))
end

function MCMCLogical(expr::Expr, monitor::Bool=true)
  data = convert(VariateType, NaN)
  MCMCLogical(data, expr, monitor)
end

function MCMCLogical(length::Integer, expr::Expr, monitor::Bool=true)
  data = Array(VariateType, length)
  fill!(data, NaN)
  MCMCLogical(data, expr, monitor)
end

function MCMCLogical(m::Integer, n::Integer, expr::Expr, monitor::Bool=true)
  data = Array(VariateType, m, n)
  fill!(data, NaN)
  MCMCLogical(data, expr, monitor)
end


#################### MCMCLogical Methods ####################

identity(l::MCMCLogical, x) = x

initchain!(l::MCMCLogical, m::MCMCModel, chain::Integer) = update!(l, m)

invlink(l::MCMCLogical, x) = x

link(l::MCMCLogical, x) = x

logpdf(l::MCMCLogical, transform::Bool=false) = 0.0

function update!(l::MCMCLogical, m::MCMCModel)
  l[:] = l.eval(m)
  l
end


#################### MCMCStochastic Constructors ####################

function MCMCStochastic{T}(data::T, expr::Expr, monitor::Bool)
  MCMCStochastic(data, monitor, paramfx(expr), paramdeps(expr), NullDistribution(), Array(T,0))
end

function MCMCStochastic(expr::Expr, monitor::Bool=true)
  data = convert(VariateType, NaN)
  MCMCStochastic(data, expr, monitor)
end

function MCMCStochastic(length::Integer, expr::Expr, monitor::Bool=true)
  data = Array(VariateType, length)
  fill!(data, NaN)
  MCMCStochastic(data, expr, monitor)
end

function MCMCStochastic(m::Integer, n::Integer, expr::Expr, monitor::Bool=true)
  data = Array(VariateType, m, n)
  fill!(data, NaN)
  MCMCStochastic(data, expr, monitor)
end


#################### MCMCStochastic Methods ####################

function Base.showall(io::IO, s::MCMCStochastic)
  show(io, s)
  print(io, "\n\nDistribution:\n")
  show(io, s.distr)
  print(io, "\nFunction:\n")
  show(io, s.eval.code)
  print(io, "\n\nNode Dependencies:\n")
  show(io, s.deps)
  print(io, "\n")
end

function initchain!(s::MCMCStochastic, m::MCMCModel, chain::Integer)
  length(s.inits) > 0 || error("missing initial values for stochastic node")
  i = (chain - 1) % length(s.inits) + 1
  s[:] = s.inits[i]
  update!(s, m)
  if isa(s.distr, Array) && size(s.data) != size(s.distr)
    error("stochastic parameter and distribution dimensions must match")
  end
  s
end

identity(s::MCMCStochastic, x) = x

insupport(s::MCMCStochastic) = all(mapdistr(insupport, s, s.data))

invlink(s::MCMCStochastic, x) = mapdistr(invlink, s, x)

link(s::MCMCStochastic, x) =  mapdistr(link, s, x)

function logpdf(s::MCMCStochastic, transform::Bool=false)
  +(mapdistr(logpdf, s, s.data, transform))
end

function mapdistr(f::Function, s::MCMCStochastic, x, args...)
  if isa(s.distr, Array)
    map(i -> f(s.distr[i], x[i], args...), 1:length(s))
  else
    f(s.distr, x, args...)
  end
end

function update!(s::MCMCStochastic, m::MCMCModel)
  s.distr = s.eval(m)
  s
end


#################### Utility Functions ####################

function paramdeps(expr::Expr)
  if expr.head == :ref && expr.args[1] == :model && isa(expr.args[2], String)
    String[expr.args[2]]
  else
    mapreduce(paramdeps, union, expr.args)
  end
end

function paramdeps(expr)
  String[]
end

function paramfx(expr::Expr)
  eval(Main, Expr(:function, :(model::MCMCModel,), expr))
end
