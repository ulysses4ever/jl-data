# An archive saves information about interesting points during an optimization
# run. It also saves (at least) the two last best fitness values since they
# can be used to estimate a confidence interval for how much improvement
# potential there is.
abstract Archive

immutable ArchivedFitness
    timestamp::Float64    # when archived
    num_fevals::Int64     # number of fitness evaluations so far
    fitness::Float64      # current fitness
    fitness_improvement_ratio::Float64

    ArchivedFitness( a::Archive, fitness, num_fevals = -1 ) =
      new ( time(), num_fevals == -1 ? a.num_fitnesses : num_fevals,
            fitness, fitness_improvement_ratio(a, fitness) )
end

fitness( a::ArchivedFitness ) = a.fitness

immutable ArchivedIndividual
    params::Individual
    fitness::Float64
end

fitness( a::ArchivedIndividual ) = a.fitness

# A top list archive saves a top list of the best performing (best fitness)
# candidates/individuals seen.
type TopListArchive <: Archive
  start_time::Float64   # Time when archive created, we use this to approximate the starting time for the opt...
  numdims::Int          # Number of dimensions in opt problem. Needed for confidence interval estimation.

  num_fitnesses::Int    # Number of calls to add_candidate

  capacity::Int         # Max size of top lists
  candidates::Vector{ArchivedIndividual}  # Top candidates and their fitness values

  # We save a fitness history that we can later print to a csv file.
  # For each magnitude class (as defined by magnitude_class function below) we
  # we save the first entry of that class. The tuple saved for each magnitude
  # class is: (magnitude_class, time, num_fevals, fitness, width_of_confidence_interval)
  fitness_history::Vector{ArchivedFitness}

  function TopListArchive(numdims, capacity = 10)
    new(time(), numdims, 0, capacity, ArchivedIndividual[], ArchivedFitness[])
  end
end

capacity(a::Archive) = a.capacity
Base.length(a::Archive) = length(a.candidates)

best_candidate(a::Archive) = a.candidates[1].params
best_fitness(a::Archive) = fitness(a.candidates[1])
last_top_fitness(a::Archive) = fitness(a.candidates[end])

# Delta fitness is the difference between the top two candidates found so far.
#
function delta_fitness(a::Archive)
  if length(a.fitness_history) < 2
    Inf
  else
    abs(a.fitness_history[2].fitness - a.fitness_history[1].fitness)
  end
end

# Add a candidate with a fitness to the archive (if it is good enough).
function add_candidate!(a::TopListArchive, fitness, candidate, num_fevals = -1)
  a.num_fitnesses += 1

  if isempty(a.fitness_history) || fitness < best_fitness(a)
    # Save fitness history so we can reconstruct the most important events later.
    push!(a.fitness_history, ArchivedFitness(a, fitness, num_fevals))
  end

  if length(a) < capacity(a) || fitness < last_top_fitness(a)
    if length(a) >= capacity(a) pop!(a.candidates) end # pop the last candidate, the new one has better fitness
    new_cand = ArchivedIndividual(candidate, fitness)
    ix = searchsortedfirst( a.candidates, new_cand, by = BlackBoxOptim.fitness )
    insert!(a.candidates, ix, new_cand)
  end
end

# The magnitude class of a value is a tuple indicating its sign and scale in a
# tuple. This is used for filtering so that we only need to save one history
# value per magnitude class instead of saving them all.
function magnitude_class(f)
  f = float(f)
  if f == 0.0
    (-1.0, 1e100)
  else
    (sign(f), floor(10.0*log10(abs(f)))/10.0)
  end
end

function fitness_improvement_ratio(a::Archive, newFitness)
  try
    bestfitness = best_fitness(a)
    return abs( (bestfitness - newFitness) / bestfitness )
  catch
    return 0.0
  end
end

# Calculate the distance from a fitness value to the optimum/best known fitness value.
function distance_to_optimum(fitness, bestfitness)
  abs(fitness - bestfitness)
end

function fitness_history_csv_header(a::Archive)
  "Date,Time,ElapsedTime,Magnitude,NumFuncEvals,FitnessImprovementRatio,Fitness"
end

function save_fitness_history_to_csv_file(a::Archive, filename = "fitness_history.csv";
  header_prefix = "", line_prefix = "",
  include_header = true, bestfitness = nothing)

  fh = open(filename, "a+")

  if include_header

    header = [header_prefix, fitness_history_csv_header(a)]

    if bestfitness != nothing
      push!(header, "DistanceToBestFitness")
    end

    println(fh, join(header, ","))

  end

  for(event in a.fitness_history)

    t, nf, f, fir = event

    mc = magnitude_class(f)

    line = [line_prefix, strftime("%Y-%m-%d,%T", t), t-a.start_time,
      mc[1]*mc[2], nf, fir, f]

    if bestfitness != nothing
      push!(line, distance_to_optimum(f, bestfitness))
    end

    println(fh, join(line, ","))

  end

  close(fh)

end

# Merge multiple fitness histories and calculate the min, max, avg and median
# values for fitness and fir at all fitness eval change points.
function merge_fitness_histories(histories)
  numhist = length(histories)
  counts = ones(numhist)
  fitnesses = zeros(numhist)
  firs = zeros(numhist)
  current_feval = 1

  # Find max history length
  maxlen = mapreduce( length, max, histories )

  while maximum(counts) < maxlen

    # Find min feval for current events, this is the next current feval
    for i in 1:numhist
      t, nf, f, fir = histories[i][counts[i]]
      # FIXME ???
    end

  end

end

# Merge the fitness histories and save the average values of the fitness,
# and distance to best fitness for each change in any of the histories.
#function merge_fitness_histories_to_csv(archives::Archive[], filename = "fitness_history.csv";
#  header_prefix = "", line_prefix = "",
#  include_header = true, bestfitness = nothing)
#
#end

# Calculate the width of the confidence interval at a certain p-value.
# This is based on the paper:
#  Carvalho (2011), "Confidence intervals for the minimum of a
#    function using extreme value statistics"
#
# This means that the current estimate of the confidence interval for the minimum
# of the optimized function lies within the interval
#
#     ] l1 - w, l1 [
#
# with probability (1-p) as the number of sampled function points goes to infinity,
# where
#   w = width_of_confidence_interval(a, p)
#   l1 = best_fitness(a)
#
function width_of_confidence_interval(a::Archive, p = 0.01)
  if length(a) < 2
    return Inf
  else
    l1 = fitness(a.candidates[1])
    l2 = fitness(a.candidates[2])
    # We use abs below so it works also for maximization.
    abs(l2 - l1) / ( (1-p)^(-2/a.numdims) - 1)
  end
end

# We define the improvement potential as the percentage improvement
# that can be expected from the current fitness value at a given p value.
# In theory, an optimization run should be terminated when this value is very
# small, i.e. there is little improvement potential left in the run.
function fitness_improvement_potential(a::Archive, p = 0.01)
  width_of_confidence_interval(a, p) / abs(best_fitness(a))
end
