###############################################################################
#
#   fmpz_series.jl : Power series over flint fmpz integers
#
###############################################################################

export fmpz_series, FmpzSeriesRing

###############################################################################
#
#   Data types and memory management
#
###############################################################################

FmpzSeriesID = ObjectIdDict()

type FmpzSeriesRing <: Ring
   base_ring::Ring
   prec_max::Int
   S::Symbol

   function FmpzSeriesRing(prec::Int, s::Symbol)
      return try
         FmpzSeriesID[prec, s]
      catch
         FmpzSeriesID[prec, s] = new(ZZ, prec, s)
      end
   end
end

type fmpz_series <: PowerSeriesElem
   coeffs::Ptr{Void}
   alloc::Int
   length::Int
   prec :: Int
   parent::FmpzSeriesRing

   function fmpz_series()
      z = new()
      ccall((:fmpz_poly_init, :libflint), Void, 
            (Ptr{fmpz_series},), &z)
      finalizer(z, _fmpz_series_clear_fn)
      return z
   end
   
   function fmpz_series(a::Array{fmpz, 1}, len::Int, prec::Int)
      z = new()
      ccall((:fmpz_poly_init2, :libflint), Void, 
            (Ptr{fmpz_series}, Int), &z, len)
      for i = 1:len
         ccall((:fmpz_poly_set_coeff_fmpz, :libflint), Void, 
                     (Ptr{fmpz_series}, Int, Ptr{fmpz}), &z, i - 1, &a[i])
      end
      z.prec = prec
      finalizer(z, _fmpz_series_clear_fn)
      return z
   end
   
   function fmpz_series(a::fmpz_series)
      z = new()
      ccall((:fmpz_poly_init, :libflint), Void, (Ptr{fmpz_series},), &z)
      ccall((:fmpz_poly_set, :libflint), Void, 
            (Ptr{fmpz_series}, Ptr{fmpz_series}), &z, &a)
      finalizer(z, _fmpz_series_clear_fn)
      return z
   end
end

function _fmpz_series_clear_fn(a::fmpz_series)
   ccall((:fmpz_poly_clear, :libflint), Void, (Ptr{fmpz_series},), &a)
end

function O(a::fmpz_series)
   prec = length(a) - 1
   prec < 0 && throw(DomainError())
   z = fmpz_series(Array(fmpz, 0), 0, prec)
   z.parent = parent(a)
   return z
end

elem_type(::FmpzSeriesRing) = fmpz_series

base_ring(R::FmpzSeriesRing) = R.base_ring

var(a::FmpzSeriesRing) = a.S

###############################################################################
#
#   Basic manipulation
#
###############################################################################    
   
max_precision(R::FmpzSeriesRing) = R.prec_max

function coeff(x::fmpz_series, n::Int)
   if n < 0
      return ZZ(0)
   end
   z = ZZ()
   ccall((:fmpz_poly_get_coeff_fmpz, :libflint), Void, 
         (Ptr{fmpz}, Ptr{fmpz_series}, Int), &z, &x, n)
   return z
end

zero(R::FmpzSeriesRing) = R(0)

one(R::FmpzSeriesRing) = R(1)

function gen(R::FmpzSeriesRing)
   z = fmpz_series([ZZ(0), ZZ(1)], 2, max_precision(R) + 1)
   z.parent = R
   return z
end

function deepcopy(a::fmpz_series)
   z = fmpz_series(a)
   z.prec = a.prec
   z.parent = parent(a)
   return z
end

###############################################################################
#
#   String I/O
#
###############################################################################

function show(io::IO, x::fmpz_series)
   if length(x) == 0
      print(io, "0")
   else
      cstr = ccall((:fmpz_poly_get_str_pretty, :libflint), Ptr{Uint8}, 
        (Ptr{fmpz_series}, Ptr{Uint8}), &x, bytestring(string(var(parent(x)))))

      print(io, bytestring(cstr))

      ccall((:flint_free, :libflint), Void, (Ptr{Uint8},), cstr)
   end
   print(io, "+O(", string(var(parent(x))), "^", x.prec, ")")
end

function show(io::IO, a::FmpzSeriesRing)
   print(io, "Univariate power series ring in ", var(a), " over ")
   show(io, base_ring(a))
end

show_minus_one(::Type{fmpz_series}) = show_minus_one(fmpz)

###############################################################################
#
#   Unary operators
#
###############################################################################

function -(x::fmpz_series)
   z = parent(x)()
   ccall((:fmpz_poly_neg, :libflint), Void, 
                (Ptr{fmpz_series}, Ptr{fmpz_series}), 
               &z, &x)
   z.prec = x.prec
   return z
end

###############################################################################
#
#   Binary operators
#
###############################################################################

function +(a::fmpz_series, b::fmpz_series)
   check_parent(a, b)
   lena = length(a)
   lenb = length(b)
         
   prec = min(a.prec, b.prec)
 
   lena = min(lena, prec)
   lenb = min(lenb, prec)

   lenz = max(lena, lenb)
   z = parent(a)()
   z.prec = prec
   ccall((:fmpz_poly_add_series, :libflint), Void, 
                (Ptr{fmpz_series}, Ptr{fmpz_series}, Ptr{fmpz_series}, Int), 
               &z, &a, &b, lenz)
   return z
end

function -(a::fmpz_series, b::fmpz_series)
   check_parent(a, b)
   lena = length(a)
   lenb = length(b)
         
   prec = min(a.prec, b.prec)
 
   lena = min(lena, prec)
   lenb = min(lenb, prec)

   lenz = max(lena, lenb)
   z = parent(a)()
   z.prec = prec
   ccall((:fmpz_poly_sub_series, :libflint), Void, 
                (Ptr{fmpz_series}, Ptr{fmpz_series}, Ptr{fmpz_series}, Int), 
               &z, &a, &b, lenz)
   return z
end

function *(a::fmpz_series, b::fmpz_series)
   check_parent(a, b)
   lena = length(a)
   lenb = length(b)
   
   aval = valuation(a)
   bval = valuation(b)

   prec = min(a.prec + bval, b.prec + aval)
   
   lena = min(lena, prec)
   lenb = min(lenb, prec)
   
   z = parent(a)()
   z.prec = prec
      
   if lena == 0 || lenb == 0
      return z
   end

   lenz = min(lena + lenb - 1, prec)

   ccall((:fmpz_poly_mullow, :libflint), Void, 
                (Ptr{fmpz_series}, Ptr{fmpz_series}, Ptr{fmpz_series}, Int), 
               &z, &a, &b, lenz)
   return z
end

###############################################################################
#
#   Unsafe functions
#
###############################################################################

function setcoeff!(z::fmpz_series, n::Int, x::fmpz)
   ccall((:fmpz_poly_set_coeff_fmpz, :libflint), Void, 
                (Ptr{fmpz_series}, Int, Ptr{fmpz}), 
               &z, n, &x)
end

function mul!(z::fmpz_series, a::fmpz_series, b::fmpz_series)
   lena = length(a)
   lenb = length(b)
   
   aval = valuation(a)
   bval = valuation(b)

   prec = min(a.prec + bval, b.prec + aval)
   
   lena = min(lena, prec)
   lenb = min(lenb, prec)
   
   lenz = min(lena + lenb - 1, prec)
   if lenz < 0
      lenz = 0
   end

   z.prec = prec
   ccall((:fmpz_poly_mullow, :libflint), Void, 
                (Ptr{fmpz_series}, Ptr{fmpz_series}, Ptr{fmpz_series}, Int), 
               &z, &a, &b, lenz)
end

function addeq!(a::fmpz_series, b::fmpz_series)
   lena = length(a)
   lenb = length(b)
         
   prec = min(a.prec, b.prec)
 
   lena = min(lena, prec)
   lenb = min(lenb, prec)

   lenz = max(lena, lenb)
   a.prec = prec
   ccall((:fmpz_poly_add_series, :libflint), Void, 
                (Ptr{fmpz_series}, Ptr{fmpz_series}, Ptr{fmpz_series}, Int), 
               &a, &a, &b, lenz)
end

###############################################################################
#
#   Ad hoc binary operators
#
###############################################################################

function *(x::Int, y::fmpz_series)
   z = parent(y)()
   z.prec = y.prec
   ccall((:fmpz_poly_scalar_mul_si, :libflint), Void, 
                (Ptr{fmpz_series}, Ptr{fmpz_series}, Int), 
               &z, &y, x)
   return z
end

function *(x::fmpz, y::fmpz_series)
   z = parent(y)()
   z.prec = y.prec
   ccall((:fmpz_poly_scalar_mul_fmpz, :libflint), Void, 
                (Ptr{fmpz_series}, Ptr{fmpz_series}, Ptr{fmpz}), 
               &z, &y, &x)
   return z
end

###############################################################################
#
#   Shifting
#
###############################################################################

function shift_left(x::fmpz_series, len::Int)
   len < 0 && throw(DomainError())
   xlen = length(x)
   z = parent(x)()
   z.prec = x.prec + len
   ccall((:fmpz_poly_shift_left, :libflint), Void, 
                (Ptr{fmpz_series}, Ptr{fmpz_series}, Int), 
               &z, &x, len)
   return z
end

function shift_right(x::fmpz_series, len::Int)
   len < 0 && throw(DomainError())
   xlen = length(x)
   z = parent(x)()
   if len >= xlen
      z.prec = max(0, x.prec - len)
   else
      z.prec = x.prec - len
      ccall((:fmpz_poly_shift_right, :libflint), Void, 
                (Ptr{fmpz_series}, Ptr{fmpz_series}, Int), 
               &z, &x, len)
   end
   return z
end

###############################################################################
#
#   Truncation
#
###############################################################################

function truncate(x::fmpz_series, prec::Int)
   prec < 0 && throw(DomainError())
   if x.prec <= prec
      return x
   end
   z = parent(x)()
   z.prec = prec
   ccall((:fmpz_poly_set_trunc, :libflint), Void, 
                (Ptr{fmpz_series}, Ptr{fmpz_series}, Int), 
               &z, &x, prec)
   return z
end

###############################################################################
#
#   Powering
#
###############################################################################

function ^(a::fmpz_series, b::Int)
   b < 0 && throw(DomainError())
   if isgen(a)
      z = parent(a)()
      setcoeff!(z, b, ZZ(1))
      z.prec = a.prec + b - 1
   elseif length(a) == 0
      z = parent(a)()
      z.prec = a.prec + (b - 1)*valuation(a)
   elseif length(a) == 1
      return fmpz_series([coeffs(a, 0)^b], 1, a.prec)
   elseif b == 0
      return fmpz_series([ZZ(1)], 1, parent(a).prec_max)
   else
      z = parent(a)()
      z.prec = a.prec + (b - 1)*valuation(a)
      ccall((:fmpz_poly_pow_trunc, :libflint), Void, 
                (Ptr{fmpz_series}, Ptr{fmpz_series}, Int, Int), 
               &z, &a, b, z.prec)
   end
   return z
end

###############################################################################
#
#   Comparison
#
###############################################################################

function ==(x::fmpz_series, y::fmpz_series)
   check_parent(x, y)
   prec = min(x.prec, y.prec)
   
   n = max(length(x), length(y))
   n = min(n, prec)
   
   return Bool(ccall((:fmpz_poly_equal_trunc, :libflint), Cint, 
                (Ptr{fmpz_series}, Ptr{fmpz_series}, Int), 
               &x, &y, n))
end

function isequal(x::fmpz_series, y::fmpz_series)
   check_parent(x, y)
   if x.prec != y.prec || length(x) != length(y)
      return false
   end
   return Bool(ccall((:fmpz_poly_equal, :libflint), Cint, 
                (Ptr{fmpz_series}, Ptr{fmpz_series}, Int), 
               &x, &y, length(x)))
end

###############################################################################
#
#   Exact division
#
###############################################################################

function divexact(x::fmpz_series, y::fmpz_series)
   check_parent(x, y)
   y == 0 && throw(DivideError())
   v2 = valuation(y)
   v1 = valuation(x)
   if v2 != 0
      if v1 >= v2
         x = shift_right(x, v2)
         y = shift_right(y, v2)
      end
   end
   !isunit(y) && error("Unable to invert power series")
   prec = min(x.prec, y.prec - v2 + v1)
   z = parent(x)()
   z.prec = prec
   ccall((:fmpz_poly_div_series, :libflint), Void, 
                (Ptr{fmpz_series}, Ptr{fmpz_series}, Ptr{fmpz_series}, Int), 
               &z, &x, &y, prec)
   return z
end

###############################################################################
#
#   Ad hoc exact division
#
###############################################################################

function divexact(x::fmpz_series, y::Int)
   y == 0 && throw(DivideError())
   z = parent(x)()
   z.prec = x.prec
   ccall((:fmpz_poly_scalar_divexact_si, :libflint), Void, 
                (Ptr{fmpz_series}, Ptr{fmpz_series}, Int), 
               &z, &x, y)
   return z
end

function divexact(x::fmpz_series, y::fmpz)
   y == 0 && throw(DivideError())
   z = parent(x)()
   z.prec = x.prec
   ccall((:fmpz_poly_scalar_divexact_fmpz, :libflint), Void, 
                (Ptr{fmpz_series}, Ptr{fmpz_series}, Ptr{fmpz}), 
               &z, &x, &y)
   return z
end

divexact(x::fmpz_series, y::Integer) = divexact(x, ZZ(y))

###############################################################################
#
#   Inversion
#
###############################################################################

function inv(a::fmpz_series)
   a == 0 && throw(DivideError())
   !isunit(a) && error("Unable to invert power series")
   ainv = parent(a)()
   ainv.prec = a.prec
   ccall((:fmpz_poly_inv_series, :libflint), Void, 
                (Ptr{fmpz_series}, Ptr{fmpz_series}, Int), 
               &ainv, &a, a.prec)
   return ainv
end

###############################################################################
#
#   Promotion rules
#
###############################################################################

Base.promote_rule{T <: Integer}(::Type{fmpz_series}, ::Type{T}) = fmpz_series

Base.promote_rule(::Type{fmpz_series}, ::Type{fmpz}) = fmpz_series

###############################################################################
#
#   Parent object call overload
#
###############################################################################

function Base.call(a::FmpzSeriesRing)
   z = fmpz_series()
   z.prec = a.prec_max
   z.parent = a
   return z
end

function Base.call(a::FmpzSeriesRing, b::Integer)
   if b == 0
      z = fmpz_series()
      z.prec = a.prec_max
   else
      z = fmpz_series([ZZ(b)], 1, a.prec_max)
   end
   z.parent = a
   return z
end

function Base.call(a::FmpzSeriesRing, b::fmpz)
   base_ring(a) != ZZ && error("Unable to coerce to power series")
   if b == 0
      z = fmpz_series()
      z.prec = a.prec_max
   else
      z = fmpz_series([b], 1, a.prec_max)
   end
   z.parent = a
   return z
end

function Base.call(a::FmpzSeriesRing, b::fmpz_series)
   parent(b) != a && error("Unable to coerce power series")
   return b
end

function Base.call(a::FmpzSeriesRing, b::Array{fmpz, 1}, len::Int, prec::Int)
   z = fmpz_series(b, len, prec)
   z.parent = a
   return z
end

###############################################################################
#
#   PowerSeriesRing constructor
#
###############################################################################

function PowerSeriesRing(R::IntegerRing, prec::Int, s::String)
   S = symbol(s)

   parent_obj = FmpzSeriesRing(prec, S)
   
   return parent_obj, parent_obj([ZZ(0), ZZ(1)], 2, prec)
end

