module GaussQuadrature

# October 2013 by Bill McLean, School of Maths and Stats,
# The University of New South Wales.
#
# Based on earlier Fortran codes
#
# gaussq.f original version 20 Jan 1975 from Stanford
# gaussq.f modified 21 Dec by Eric Grosse
# gaussquad.f95 Nov 2005 by Bill Mclean
#
# This module provides functions to compute the abscissae x[j] and 
# weights w[j] for the classical Gauss quadrature rules, including 
# the Radau and Lobatto variants.  Thus, the sum
#
#           n
#          ---
#          \
#           |  w[j] f(x[j])
#          /
#          ---
#          j=1
#
# approximates
#
#          / b
#          |
#          |    f(x) w(x) dx
#          |
#          / a
#
# where weight function w(x) and interval a < x < b are as shown
# in the table below.
#
# Name                      Interval     Weight Function
#
# Legendre                 -1 < x < 1          1      
# Chebyshev (first kind)   -1 < x < 1     1 / sqrt(1-x^2)        
# Chebyshev (second kind)  -1 < x < 1       sqrt(1-x^2)          
# Jacobi                   -1 < x < 1   (1-x)^alpha (1+x)^beta  
# Laguerre                 -1 < x < oo     x^alpha exp(-x)
# Hermite                 -oo < x < oo      exp(-x^2)
#
# For the Jacobi and Laguerre rules we require alpha > -1 and
# beta > -1, so that the weight function is integrable.
#
# Use the endpt argument to include one or both of the end points
# of the interval of integration as an abscissa in the the quadrature 
# rule, as follows.
# 
# endpt = "Neither" Default,     a < x[j] < b, j = 1:n.
# endpt = "Left"    Left Radau,  a = x[1] < x[j] < b, j = 2:n.
# endpt = "Right"   Right Radau, a < x[j] < x[n] = b, j = 1:n-1.
# endpt = "Both"    Lobatto,     a = x[1] < x[j] < x[n] = b, j = 2:n-1.
#
# Only the first letter of endpt is significant, so for instance
# endpt = 'B' (or "B") gives a Lobatto rule.
#
# The code uses the Golub and Welsch algorithm, in which the abscissae
# x[j] are the eigenvalues of a symmetric tridiagonal matrix whose 
# entries depend on the coefficients in the 3-term recurrence relation
# for the othonormal polynomials generated by the weighted inner product.
#
# References:
#
#   1.  Golub, G. H., and Welsch, J. H., Calculation of gaussian
#       quadrature rules, Mathematics of Computation 23 (april,
#       1969), pp. 221-230.
#   2.  Golub, G. H., Some modified matrix eigenvalue problems,
#       Siam Review 15 (april, 1973), pp. 318-334 (section 7).
#   3.  Stroud and Secrest, Gaussian Quadrature Formulas, Prentice-
#       Hall, Englewood Cliffs, N.J., 1966.

using Base
export legendre, legendre_coeff
export chebyshev, chebyshev_coeff
export jacobi, jacobi_coeff
export laguerre, laguerre_coeff 
export hermite, hermite_coeff
export custom_gauss_rule, orthonormal_poly

function legendre(n, endpt="Neither")
    endpt = uppercase(endpt)
    @assert endpt[1] in {'N', 'B', 'L', 'R'}
    a, b, muzero = legendre_coeff(n, endpt)
    x, w = custom_gauss_rule(-1.0, 1.0, a, b, muzero, endpt)
    return x, w
end

function legendre_coeff(n, endpt)
    muzero = 2.0
    a = zeros(n)
    b = zeros(n)
    for i = 1:n
        b[i] = i / sqrt(4*i^2-1)
    end
    return a, b, muzero
end

function chebyshev(n, kind=1, endpt="Neither")
    @assert kind in {1, 2}
    endpt = uppercase(endpt)
    @assert endpt[1] in {'N', 'B', 'L', 'R'}
    a, b, muzero = chebyshev_coeff(n, kind, endpt)
    x, w = custom_gauss_rule(-1.0, 1.0, a, b, muzero, endpt)
    return x, w
end

function chebyshev_coeff(n, kind, endpt)
    muzero = pi
    a = zeros(n)
    b = fill(0.5, n)
    if kind == 1
        b[1] = sqrt(0.5)
    elseif kind == 2
        muzero /= 2
    else
        error("Unsupported value for kind")
    end
    return a, b, muzero
end

function jacobi(n, alpha, beta, endpt="Neither")
    @assert alpha > -1.0 && beta > -1.0
    endpt = uppercase(endpt)
    @assert endpt[1] in {'N', 'B', 'L', 'R'}
    a, b, muzero = jacobi_coeff(n, alpha, beta, endpt)
    x, w = custom_gauss_rule(-1.0, 1.0, a, b, muzero, endpt)
    return x, w
end

function jacobi_coeff(n, alpha, beta, endpt)
    ab = alpha + beta
    i = 2
    abi = ab + 2
    muzero = 2^(ab+1) * exp(
             lgamma(alpha+1) + lgamma(beta+1) - lgamma(abi) )
    a = zeros(n)
    b = zeros(n)
    a[1] = ( beta - alpha ) / abi
    b[1] = sqrt( 4*(alpha+1)*(beta+1) / ( (ab+3)*abi*abi ) )
    a2b2 = beta*beta - alpha*alpha
    for i = 2:n
        abi = ab + 2i
        a[i] = a2b2 / ( (abi-2)*abi )
        b[i] = sqrt( 4i*(alpha+i)*(beta+i)*(ab+i) /
                     ( (abi*abi-1)*abi*abi ) )
    end   
    return a, b, muzero
end

function laguerre(n, alpha=0.0, endpt="Neither")
    @assert alpha > -1.0
    endpt = uppercase(endpt)
    @assert endpt[1] in {'N', 'L'}
    a, b, muzero = laguerre_coeff(n, alpha, endpt)
    x, w = custom_gauss_rule(0.0, Inf, a, b, muzero, endpt)
    return x, w
end

function laguerre_coeff(n, alpha, endpt)
    muzero = gamma(alpha+1)
    a = zeros(n)
    b = zeros(n)
    for i = 1:n
        a[i] = 2i - 1 + alpha
        b[i] = sqrt( i*(alpha+i) )
    end
    return a, b, muzero
end

function hermite(n)
    a, b, muzero = hermite_coeff(n)
    x, w = custom_gauss_rule(-Inf, Inf, a, b, muzero, "Neither")
    return x, w
end

function hermite_coeff(n)
    muzero = sqrt(pi)
    a = zeros(n)
    b = zeros(n)
    for i = 1:n
        b[i] = sqrt(i/2)
    end
    return a, b, muzero
end

function custom_gauss_rule(lo, hi, a, b, muzero, endpt)
    endpt = uppercase(endpt)
    @assert endpt[1] in {'N', 'B', 'L', 'R'}
    n = length(a)
    @assert length(b) == n
    if endpt[1] == 'L'     # left end
        if n == 1
            a[1] = lo
        else
            a[n] = solve(n, lo, a, b) * b[n-1]^2 + lo
        end
    elseif endpt[1] == 'R' # right end
        if n == 1
            a[1] = hi
        else
            a[n] = solve(n, hi, a, b) * b[n-1]^2 + hi
        end
    elseif endpt[1] == 'B' # both ends
        if n == 1 
            error("Must have at least two points for both ends.")
        end 
        g = solve(n, lo, a, b)
        t1 = ( hi - lo ) / ( g - solve(n, hi, a, b) )
        b[n-1] = sqrt(t1)
        a[n] = lo + g * t1
    end
    A = SymTridiagonal(a, b[1:n-1])
    x, V = eig(A)
    w = zero(x)
    for i = 1:n
        w[i] = muzero * V[1,i]^2
    end
    return x, w
end

function solve(n, shift, a, b)

    t = a[1] - shift
    for i = 2:n-1
        t = a[i] - shift - b[i-1]^2 / t
    end
    return one(t) / t
end

function orthonormal_poly(x, a, b, muzero)
    # p[i,j] = value at x[i] of orthonormal polynomial of degree j-1.
    m = length(x)
    n = length(a)
    p = zeros(m,n+1)
    c = 1.0 / sqrt(muzero)
    rb = 1.0 / b[1]
    for i = 1:m
        p[i,1] = c
        p[i,2] = rb * ( x[i] - a[1] ) * c
    end 
    for j = 2:n
       rb = 1.0 / b[j]
       for i = 1:m
           p[i,j+1] = rb * ( (x[i]-a[j]) * p[i,j] 
                                - b[j-1] * p[i,j-1] )
       end
    end
    return p
end

end
