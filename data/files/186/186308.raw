module Histograms
using DataArrays, DataFrames

import Base.+, Base.-, Base.*, Base./, Base.==, Base.ndims
import Base.sum

import Base.show
import Base.getindex
import Base.size, Base.transpose

using Distributions

#represents a 1-D histogram with N bins
immutable Histogram

    #N floats with the number of unweighted entries in the bins
    bin_entries::Vector{Float64}

    #N floats with the number of weighted entries in the bins
    bin_contents::Vector{Float64}

    #N floats with the lower edges of the bins. bin_edges[N] is the upper edge.
    bin_edges::Vector{Float64}

    function Histogram(entries::AbstractVector, contents::AbstractVector, edges::AbstractVector)
        @assert length(entries)==length(contents)
        @assert length(entries)==length(edges)
        @assert all(entries .>= 0.0)
        @assert issorted(edges)
        new(
            convert(Vector{Float64}, entries),
            convert(Vector{Float64}, contents),
            convert(Vector{Float64}, edges)
        )
    end
end
#
# import Base: .==
# .==(h1::Histogram, h2::Histogram) =
#     all(h1.bin_edges .== h2.bin_edges) &&
#     all(h1.bin_contents .== h2.bin_contents) &&
#     all(h1.bin_entries .== h2.bin_entries)

#create empty histogram
Histogram{R<:Real}(edges::Vector{R}) = Histogram(
    Float64[0.0 for i=1:length(edges)],
    Float64[0.0 for i=1:length(edges)],
    edges
)

Histogram(h::Histogram) = Histogram(h.bin_entries, h.bin_contents, h.bin_edges)

#account for the under- and overflow bins
nbins(h::Histogram) = length(h.bin_contents)

contents(h::Histogram) = h.bin_contents
edges(h::Histogram) = h.bin_edges
entries(h::Histogram) = h.bin_entries
nentries(h::Histogram) = int(sum(h.bin_entries))

function subhist(h::Histogram, bins)
    return Histogram(
        h.bin_entries[bins],
        h.bin_contents[bins],
        h.bin_edges[bins]
    )
end

function errors(h::Histogram, replace_nan=true, replace_0=true, replaceval=0.0)
    const errs = h.bin_contents ./ sqrt(h.bin_entries)
    errs[h.bin_entries .== 0] = 0
    const T = eltype(errs)
    for i=1:nbins(h)
        if replace_nan && isnan(errs[i])
            errs[i] = replaceval
        end

        #in case of zero bins, put error to replaceval
        if replace_0 && errs[i] < eps(T)
            errs[i] = replaceval
        end
    end
    return errs
end

function findbin(h::Histogram, v)
    isna(v) && return 1 #put NA in underflow bin

    #guaranteed v::Real
    isnan(v::Real) && return 1 #put nans in underflow bin

    v < h.bin_edges[1] &&
        error("underflow v=$v")
    v >= h.bin_edges[nbins(h)] &&
        error("overflow v=$v, min=$(minimum(h.bin_edges)), max=$(maximum(h.bin_edges))")

    const idx = searchsortedfirst(h.bin_edges, v)::Int64 - 1
    if (idx<1 || idx>length(h.bin_entries))
        error("bin index out of range: i=$(idx), v=$(v)")
    end
    return idx
end


function hfill!{T <: Real, K <: Real}(h::Histogram, v::T, w::K=1.0)
    const low = findbin(h, v)
    h.bin_entries[low] += 1
    h.bin_contents[low] += isnan(w) ? 0.0 : w
end

function hfill!{T <: Real}(h::Histogram, v::NAtype, w::Union(T, NAtype)=1.0)
    h.bin_entries[1] += 1
    h.bin_contents[1] += isnan(w) ? 0.0 : w
end

function hfill!{T <: Real}(h::Histogram, v::T, w::NAtype)
    h.bin_entries[1] += 1
    h.bin_contents[1] += 0
end

#Histogram algebra
function +(h1::Histogram, h2::Histogram)
    @assert h1.bin_edges == h2.bin_edges
    h = Histogram(
        h1.bin_entries + h2.bin_entries,
        h1.bin_contents + h2.bin_contents,
        h1.bin_edges
    )
    abs(integral(h1)+integral(h2)-integral(h)) < 0.1 || #500.0 * eps(Float64) ||
        warn(
            "problem adding histograms: ",
            "$(integral(h1)) + $(integral(h2)) != $(integral(h)) ",
            "neps=", @sprintf(
                "%.2f",
                (integral(h1)+integral(h2)-integral(h))/eps(Float64)
            )
        )
    return h
end

function +(h1::Histogram, x::Real)
    nb = nbins(h1)
    h2 = Histogram([0.0 for n=1:nb], [x for n=1:nb], h1.bin_edges)
    return h1+h2
end

+(x::Real, h1::Histogram) = h1+x

function ==(h1::Histogram, h2::Histogram)
    ret = h1.bin_edges == h2.bin_edges
    ret = ret && (h1.bin_contents==h2.bin_contents)
    ret = ret && (h1.bin_entries==h2.bin_entries)
end

-(h1::Histogram, h2::Histogram) = h1 + (-1.0 * h2)

function *{T <: Real}(h1::Histogram, x::T)
    return Histogram(h1.bin_entries, h1.bin_contents * x, h1.bin_edges)
end

function *{T <: Real}(x::T, h1::Histogram)
    return h1 * x
end

function *(h1::Histogram, h2::Histogram)
    @assert(h1.bin_edges == h2.bin_edges, "bin edges must be the same for both histograms")
    conts = h1.bin_contents.* h2.bin_contents
    ents = 1.0 / (1.0 ./ entries(h1) + 1.0 ./ entries(h2))

    conts[isnan(conts)] = 0.0
    ents[isnan(ents)] = 0.0

    Histogram(ents, conts, h1.bin_edges)
end


function /{T <: Real}(h1::Histogram, x::T)
    return h1 * (1.0 / x)
end

#err / C = sqrt((err1/C1)^2 + (err2/C2)^2)
function /(h1::Histogram, h2::Histogram)
    @assert(h1.bin_edges == h2.bin_edges,
        "bin edges must be the same for both histograms"
    )

    const _contents = h1.bin_contents ./ h2.bin_contents
    const _entries = 1.0 ./ (1.0 ./ entries(h1) + 1.0 ./ entries(h2))

    #Replace NaN-s resulting from /0 by 0
    _contents[isnan(_contents)] = 0.0
    _entries[isnan(_entries)] = 0.0

    return Histogram(_entries, _contents, h1.bin_edges)

end

#
integral(h::Histogram) = sum(h.bin_contents)
integral(x::Real) = x

function integral(h::Histogram, x1::Real, x2::Real)
    if !(x1 in h.bin_edges) || !(x2 in h.bin_edges)
        warn("integration will be inexact due to binning")
    end
    a = searchsorted(h.bin_edges, x1).start
    b = searchsorted(h.bin_edges, x2).start
    return sum(h.bin_contents[a:b])
end

#returns the low edges of a list of histogram edges
lowedge(arr) = arr[1:length(arr)-1];
widths(arr) = [arr[i+1]-arr[i] for i=1:length(arr)-1]

function normed{T <: Histogram}(h::T)
    i = integral(h)
    return i > 0 ? h/i : error("histogram integral was $i")
end

#function fromdf(df::DataFrame)
#    edges = df[1]
#    conts = df[2][1:nrow(df)-1]
#    entries = df[3][1:nrow(df)-1]
#    return Histogram(entries, conts, edges)
#end

#assumes df columns are entries, contents, edges
#length(entries) = length(contents) = length(edges) - 1, edges are lower, lower, lower, ..., upper
# function fromdf(df::DataFrame; entries=:entries)
#     ent = df[2].data
#     cont = df[3].data

#     #entries column reflects poisson errors of the contents column
#     if entries == :poissonerrors
#         ent = (cont ./ ent ) .^ 2
#         ent = Float64[x > 0 ? x : 0 for x in ent]
#     #entries column reflects raw entries/events
#     elseif entries == :entries
#         ent = ent
#     else
#         error("unknown value for keyword :entries=>$(entries)")
#     end

#     Histogram(
#         ent, #entries
#         cont, #contents
#         df[1].data #edges
#     )
# end

flatten(h::Histogram) = reshape(h, prod(size(h)))

function rebin(h::Histogram, k::Integer)
    @assert((nbins(h)) % k == 0, "number of bins $(nbins(h))+1 is not divisible by k=$k")

    new_entries = Float64[]
    new_contents = Float64[]
    new_edges = Float64[]
    for i=1:k:nbins(h)
        push!(new_contents, sum(h.bin_contents[i:i+k-1]))
        push!(new_entries, sum(h.bin_entries[i:i+k-1]))
        push!(new_edges, h.bin_edges[i])
    end
    #push!(new_edges, h.bin_edges[nbins(h)+1])
    return Histogram(new_entries, new_contents, new_edges)
end

function cumulative(h::Histogram)
    #hc = Histogram(h)
    cont = deepcopy(h.bin_contents)
    ent = deepcopy(h.bin_contents)
    for i=1:length(h.bin_contents)
        cont[i] = sum(h.bin_contents[1:i])
        ent[i] = sum(h.bin_entries[1:i])
    end
    return Histogram(ent, cont, h.bin_edges)
end

#dices from the number of entries in the histogram
function sample_hist(h::Histogram, n=1000)
    pois = Any[e>0 ? Poisson(e) : Poisson(1) for e in h.bin_entries]

    #ratio between Nentries, Nweighted
    wr = sum(h.bin_contents) / sum(h.bin_entries)
    hists = Any[]

    for i=1:n
        bins = map(rand, pois)
        push!(hists, Histogram(bins, bins./wr, h.bin_edges))
    end
    hists
end

#Histogram comparison tests

#returns the Kolmogorov-Smirnov test statistic
function test_ks(h1::Histogram, h2::Histogram)
    ch1 = cumulative(h1)
    ch2 = cumulative(h2)
    ch1 = ch1 / integral(h1)
    ch2 = ch2 / integral(h2)
    return maximum(abs(ch1.bin_contents - ch2.bin_contents))
end

#dices the two histograms and returns the fraction(p-value) of KS-test
#values that are greater than between h1, h2
function ks_pvalue(h1::Histogram, h2::Histogram, n)
    const ks = test_ks(h1, h2)
    hs1 = sample_hist(h1, n)
    hs2 = sample_hist(h2, n)
    kss = Any[]
    for (_h1, _h2) in zip(hs1, hs2)
        push!(kss, test_ks(_h1, _h2))
    end
    mks = mean(kss)
    if ks > mks
        return count(x -> x > ks, kss) / length(kss)
    else
        return count(x -> x < ks, kss) / length(kss)
    end
end

#Multidimensional histogram
type NHistogram
    baseh::Histogram
    edges::Vector{Vector{Float64}}
end

edges(h::NHistogram) = h.edges

function NHistogram(edges)
    nb = prod([length(e) for e in edges])
    baseh = Histogram([1:nb])
    NHistogram(baseh, edges)
end

function NHistogram(
    entries::AbstractArray, contents::AbstractArray,
    edges::AbstractArray)
    fromarr(contents, entries, edges)
end

function ==(h1::NHistogram, h2::NHistogram)
    ret = h1.edges == h2.edges
    ret = ret && h1.baseh == h2.baseh
    return ret
end

nbins(h::NHistogram) = prod([length(e) for e in h.edges])
nbins(h::NHistogram, nd::Integer) = length(h.edges[nd])

#size(h::NHistogram) = length(h.edges)
ndims(h::NHistogram) = length(size(h))

errors(h::NHistogram) = reshape(errors(h.baseh), Int64[length(e) for e in h.edges]...)
nentries(h::NHistogram) = nentries(h.baseh)
integral(h::NHistogram) = integral(h.baseh)

function +(h1::NHistogram, h2::NHistogram)
    @assert h1.edges == h2.edges
    return NHistogram(h1.baseh+h2.baseh, h1.edges)
end

function *{T <: Real}(h::NHistogram, x::T)
    return NHistogram(x * h.baseh, h.edges)
end

function *{T <: Real}(x::T, h::NHistogram)
    return h * x
end

function /(h1::NHistogram, h2::NHistogram)
    @assert h1.edges == h2.edges
    return NHistogram(h1.baseh / h2.baseh, h1.edges)
end


function asarr(h::NHistogram)
    return reshape(h.baseh.bin_contents, Int64[length(e) for e in h.edges]...),
    reshape(h.baseh.bin_entries, Int64[length(e) for e in h.edges]...)
end

function fromarr(nc, ne, edges)
    nb = prod([length(e) for e in edges])
    rsh(x) = reshape(x, nb)
    NHistogram(
        Histogram(rsh(ne), rsh(nc), [1:nb]),
        edges
    )
end

function Base.transpose(nh::NHistogram)
    nc, ne = asarr(nh)
    fromarr(transpose(ne), transpose(nc), nh.edges|>reverse|>collect)
end

contents(h::NHistogram) = asarr(h)[1]
entries(h::NHistogram) = asarr(h)[2]
Base.size(h::NHistogram) = tuple([length(e) for e in h.edges]...)

function findbin_nd(h::NHistogram, v)
    const nd = length(v)
    @assert ndims(h)==nd
    const idxs = Int64[-1 for i=1:nd]
    for i=1:nd
        const x = v[i]
        const j = (isna(x) || isnan(x)) ? 1 : (searchsortedfirst(h.edges[i], x) - 1)
        (j < 1 || j > length(h.edges[i])) && error("overflow dim=$i, v=$x")
        idxs[i] = j
    end
    return idxs
end

#is slow
function hfill!{K <:Real}(h::NHistogram, v, w::K=1.0)
    a, b = asarr(h)
    xb = findbin_nd(h, v)
    a[xb...] += w
    b[xb...] += 1
end

function hfill!{K <: NAtype}(h::NHistogram, v, w::K)
    warn("NA weight in NHistogram")
    #a, b = asarr(h)
    #xb = findbin_nd(h, v)
    #a[xb...] += 0
    #b[xb...] += 1
end

function Base.getindex(nh::NHistogram, args...)
    a, b = asarr(nh)
    return a[args...]#, b[args...]
end


project_x(nh::NHistogram) = Histogram(sum(nh|>entries, 1)[:], sum(nh|>contents, 1)[:], nh.edges[2])
project_y(nh::NHistogram) = Histogram(sum(nh|>entries, 2)[:], sum(nh|>contents, 2)[:], nh.edges[1])

Base.show(io::IO, h::Histogram) =
    show(io, hcat(h.bin_edges, contents(h), errors(h), entries(h)))

function Base.sum(hs::AbstractArray{Histogram})
    fh = first(hs)
    reduce(+, Histogram(edges(fh)), hs)
end

function htodf(h::Histogram)
    return DataFrame(
        edges = edges(h),
        entries = entries(h),
        contents = contents(h)
    )
end

function htodf(h::NHistogram)
    return DataFrame(
        edges = edges(h),
        entries = entries(h),
        contents = contents(h)
    )
end

function set_zero(h::Histogram)
    ent = entries(h)|>copy
    ent[ent.<0] = 0
    cont = contents(h)|>copy
    cont[cont.<0] = 0

    return Histogram(ent, cont, h.bin_edges)
end


export Histogram, hfill!
export integral, nentries, normed, errors, findbin, nbins
export +, -, *, /, ==
#export todf, fromdf
export subhist
export htodf
export flatten
export lowedge, widths
export rebin
export cumulative
export writecsv
export test_ks, ks_pvalue
export NHistogram, findbin_nd, ndims, asarr, readhist
export contents, entries, edges
export makehist_2d
export project_x, project_y
end #module
