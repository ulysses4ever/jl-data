## utility functions ##

function reorder{T<:Real}(values::AbstractVector{T})
    newOrder = sortperm(values)
    oldOrder = sortperm(newOrder)
    return newOrder, oldOrder
end

reorder(pv::PValues) = reorder(values(pv))


function sort_if_needed(x; kws...)
    if issorted(x; kws...)
        return x
    else
        return sort(x; kws...)
    end
end

function sort_if_needed!(x; kws...)
    if !issorted(x; kws...)
        sort!(x; kws...)
    end
end


function unsort(x; kws...)
    y = copy(x)
    while issorted(y; kws...)
        sample!(x, y, replace = false)
    end
    return y
end


function valid_pvalues{T<:AbstractFloat}(x::AbstractVector{T})
    if !isin(x)
        throw(DomainError())
    end
end


function isin(x::Real, lower::Real = 0., upper::Real = 1.)
    x >= lower && x <= upper
end

function isin{T<:Real}(x::AbstractVector{T}, lower::Real = 0., upper::Real = 1.)
    ex = extrema(x)
    ex[1] >= lower && ex[2] <= upper
end


function isotonic_regression_reference{T<:AbstractFloat}(y::AbstractVector{T}, w::AbstractVector{T})
    # TODO ignore zero weights
    y = copy(y)
    w = copy(w)
    m = length(y)
    cnts = ones(Int, m)
    i = 2
    # ... not most efficient way but could be fun to (ab)use iterator protocol
    while !done(y, i)
        if y[i] < y[i-1]
            y[i-1] = (w[i]*y[i]+w[i-1]*y[i-1])/(w[i]+w[i-1])
            w[i-1] = w[i]+w[i-1]
            cnts[i-1] += cnts[i]
            deleteat!(y, i)
            deleteat!(w, i)
            deleteat!(cnts, i)
            i = max(i-2, 1)
        end
        i += 1
    end
    yisotonic = vcat([y[idx]*ones(Float64, cnt) for (idx, cnt) in enumerate(cnts)]...)
    return yisotonic
end

function isotonic_regression_reference{T<:AbstractFloat}(y::AbstractVector{T})
    isotonic_regression_reference(y, ones(y))
end


function isotonic_regression{T<:AbstractFloat}(y::AbstractVector{T}, weights::AbstractVector{T})
    n = length(y)
    if n <= 1
        return y
    end
    if n != length(weights)
        throw(DimensionMismatch("Lengths of values and weights mismatch"))
    end
    @inbounds begin
        n -= 1
        while true
            i = 1
            is_pooled = false
            while i <= n
                k = i
                while k <= n && y[k] >= y[k+1]
                    k += 1
                end
                if y[i] != y[k]
                    numerator = 0.0
                    denominator = 0.0
                    for j in i:k
                        numerator += y[j] * weights[j]
                        denominator += weights[j]
                    end
                    m = numerator / denominator
                    for j in i:k
                        y[j] = m
                    end
                    is_pooled = true
                end
                i = k + 1
            end
            if !is_pooled
               break
            end
        end
    end
    return y
end

function isotonic_regression{T<:AbstractFloat}(y::AbstractVector{T})
    isotonic_regression(y, ones(y))
end


function grenander{T<:AbstractFloat}(pv::AbstractVector{T})
    pv_sorted = sort(pv)
    # ecdf that handles duplicated values
    pv_sorted_unique, counts = rle(pv_sorted)
    ecdf_value = cumsum(counts)
    ecdf_value = ecdf_value ./ ecdf_value[end]

    Δx = diff(pv_sorted_unique)
    Δy = diff(ecdf_value)

    f = Δy ./ Δx
    f = -isotonic_regression(-f, Δx)
    F  = ecdf_value[1] + vcat(0, cumsum(f .* Δx))
    f = push!(f, f[end])

    return pv_sorted_unique, f, F
end
