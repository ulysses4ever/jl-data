### Abstract parameter NStates

abstract ParameterNState{F<:VariateForm, N<:Number} <: VariableNState{F, N}

abstract ContinuousParameterNState{F<:VariateForm, N<:AbstractFloat} <: ParameterNState{F, N}

typealias MarkovChain ParameterNState

typealias ContinuousMarkovChain ContinuousParameterNState

Base.eltype{F<:VariateForm, N<:Number}(::Type{ParameterNState{F, N}}) = N
Base.eltype{F<:VariateForm, N<:AbstractFloat}(::Type{ContinuousParameterNState{F, N}}) = N

### Parameter NState subtypes

## ContinuousUnivariateParameterNState

type ContinuousUnivariateParameterNState{N<:AbstractFloat} <: ContinuousParameterNState{Univariate, N}
  value::Vector{N}
  loglikelihood::Vector{N}
  logprior::Vector{N}
  logtarget::Vector{N}
  gradloglikelihood::Vector{N}
  gradlogprior::Vector{N}
  gradlogtarget::Vector{N}
  tensorloglikelihood::Vector{N}
  tensorlogprior::Vector{N}
  tensorlogtarget::Vector{N}
  dtensorloglikelihood::Vector{N}
  dtensorlogprior::Vector{N}
  dtensorlogtarget::Vector{N}
  diagnostickeys::Vector{Symbol}
  diagnosticvalues::Matrix
  n::Int
  copy::Function

  ContinuousUnivariateParameterNState(
    ::Type{N},
    n::Int,
    monitor::Vector{Bool}=[true; fill(false, 12)],
    diagnostickeys::Vector{Symbol}=Symbol[],
    diagnosticvalues::Matrix=Array(Any, length(diagnostickeys), isempty(diagnostickeys) ? 0 : n)
  ) = begin
    instance = new()

    l = Array(Int, 13)
    for i in 1:13
      l[i] = (monitor[i] == false ? zero(Int) : n)
    end

    fnames = fieldnames(ContinuousUnivariateParameterNState)
    for i in 1:13
      setfield!(instance, fnames[i], Array(N, l[i]))
    end

    instance.diagnostickeys = diagnostickeys
    instance.diagnosticvalues = diagnosticvalues
    instance.n = n

    instance.copy = eval(codegen_copy_continuous_univariate_parameter_nstate(instance, monitor))

    instance
  end
end

ContinuousUnivariateParameterNState{N<:AbstractFloat}(
  ::Type{N},
  n::Int,
  monitor::Vector{Bool}=[true; fill(false, 12)],
  diagnostickeys::Vector{Symbol}=Symbol[],
  diagnosticvalues::Matrix=Array(Any, length(diagnostickeys), isempty(diagnostickeys) ? 0 : n)
) =
  ContinuousUnivariateParameterNState{N}(N, n, monitor, diagnostickeys, diagnosticvalues)

function ContinuousUnivariateParameterNState{N<:AbstractFloat}(
  ::Type{N},
  n::Int,
  monitor::Vector{Symbol},
  diagnostickeys::Vector{Symbol}=Symbol[],
  diagnosticvalues::Matrix=Array(Any, length(diagnostickeys), isempty(diagnostickeys) ? 0 : n)
)
  fnames = fieldnames(ContinuousUnivariateParameterNState)
  ContinuousUnivariateParameterNState(
    N, n, [fnames[i] in monitor ? true : false for i in 1:13], diagnostickeys, diagnosticvalues
  )
end

typealias ContinuousUnivariateMarkovChain ContinuousUnivariateParameterNState

# To visually inspect code generation via codegen_copy_continuous_univariate_parameter_nstate, try for example
# using Lora
#
# nstate = ContinuousUnivariateMarkovChain(Float64, 4)
# Lora.codegen_copy_continuous_univariate_parameter_nstate(nstate, [true; fill(false, 12)])

function codegen_copy_continuous_univariate_parameter_nstate(
  nstate::ContinuousUnivariateParameterNState,
  monitor::Vector{Bool}
)
  body = []
  fnames = fieldnames(ContinuousUnivariateParameterNState)
  local f::Symbol # f must be local to avoid compiler errors. Alternatively, this variable declaration can be omitted

  for j in 1:13
    if monitor[j]
      f = fnames[j]
      push!(body, :(getfield($nstate, $(QuoteNode(f)))[$(:_i)] = getfield($(:_state), $(QuoteNode(f)))))
    end
  end

  if !isempty(nstate.diagnosticvalues)
    push!(body, :($(nstate).diagnosticvalues[:, $(:_i)] = $(:_state).diagnosticvalues))
  end

  @gensym copy_continuous_univariate_parameter_nstate

  quote
    function $copy_continuous_univariate_parameter_nstate(_state::ContinuousUnivariateParameterState, _i::Int)
      $(body...)
    end
  end
end

Base.eltype{N<:AbstractFloat}(::Type{ContinuousUnivariateParameterNState{N}}) = N
Base.eltype{N<:AbstractFloat}(s::ContinuousUnivariateParameterNState{N}) = N

Base.(:(==)){S<:ContinuousUnivariateParameterNState}(z::S, w::S) =
  reduce(&, [getfield(z, n) == getfield(w, n) for n in fieldnames(S)[1:16]])

Base.isequal{S<:ContinuousUnivariateParameterNState}(z::S, w::S) =
  reduce(&, [isequal(getfield(z, n), getfield(w, n)) for n in fieldnames(S)[1:16]])

## ContinuousMultivariateParameterNState

type ContinuousMultivariateParameterNState{N<:AbstractFloat} <: ContinuousParameterNState{Multivariate, N}
  value::Matrix{N}
  loglikelihood::Vector{N}
  logprior::Vector{N}
  logtarget::Vector{N}
  gradloglikelihood::Matrix{N}
  gradlogprior::Matrix{N}
  gradlogtarget::Matrix{N}
  tensorloglikelihood::Array{N, 3}
  tensorlogprior::Array{N, 3}
  tensorlogtarget::Array{N, 3}
  dtensorloglikelihood::Array{N, 4}
  dtensorlogprior::Array{N, 4}
  dtensorlogtarget::Array{N, 4}
  diagnostickeys::Vector{Symbol}
  diagnosticvalues::Matrix
  size::Int
  n::Int
  copy::Function

  ContinuousMultivariateParameterNState(
    ::Type{N},
    size::Int,
    n::Int,
    monitor::Vector{Bool}=[true; fill(false, 12)],
    diagnostickeys::Vector{Symbol}=Symbol[],
    diagnosticvalues::Matrix=Array(Any, length(diagnostickeys), isempty(diagnostickeys) ? 0 : n)
  ) = begin
    instance = new()

    fnames = fieldnames(ContinuousMultivariateParameterNState)
    for i in 2:4
      l = (monitor[i] == false ? zero(Int) : n)
      setfield!(instance, fnames[i], Array(N, l))
    end
    for i in (1, 5, 6, 7)
      s, l = (monitor[i] == false ? (zero(Int), zero(Int)) : (size, n))
      setfield!(instance, fnames[i], Array(N, s, l))
    end
    for i in 8:10
      s, l = (monitor[i] == false ? (zero(Int), zero(Int)) : (size, n))
      setfield!(instance, fnames[i], Array(N, s, s, l))
    end
    for i in 11:13
      s, l = (monitor[i] == false ? (zero(Int), zero(Int)) : (size, n))
      setfield!(instance, fnames[i], Array(N, s, s, s, l))
    end

    instance.diagnostickeys = diagnostickeys
    instance.diagnosticvalues = diagnosticvalues
    instance.size = size
    instance.n = n

    instance.copy = eval(codegen_copy_continuous_multivariate_parameter_nstate(instance, monitor))

    instance
  end
end

ContinuousMultivariateParameterNState{N<:AbstractFloat}(
  ::Type{N},
  size::Int,
  n::Int,
  monitor::Vector{Bool}=[true; fill(false, 12)],
  diagnostickeys::Vector{Symbol}=Symbol[],
  diagnosticvalues::Matrix=Array(Any, length(diagnostickeys), isempty(diagnostickeys) ? 0 : n)
) =
  ContinuousMultivariateParameterNState{N}(N, size, n, monitor, diagnostickeys, diagnosticvalues)

function ContinuousMultivariateParameterNState{N<:AbstractFloat}(
  ::Type{N},
  size::Int,
  n::Int,
  monitor::Vector{Symbol},
  diagnostickeys::Vector{Symbol}=Symbol[],
  diagnosticvalues::Matrix=Array(Any, length(diagnostickeys), isempty(diagnostickeys) ? 0 : n)
)
  fnames = fieldnames(ContinuousMultivariateParameterNState)
  ContinuousMultivariateParameterNState(
    N, size, n, [fnames[i] in monitor ? true : false for i in 1:13], diagnostickeys, diagnosticvalues
  )
end

typealias ContinuousMultivariateMarkovChain ContinuousMultivariateParameterNState

# To visually inspect code generation via codegen_copy_continuous_multivariate_parameter_nstate, try for example
# using Lora
#
# nstate = ContinuousMultivariateMarkovChain(Float64, 2, 4)
# Lora.codegen_copy_continuous_multivariate_parameter_nstate(nstate, [true; fill(false, 12)])

function codegen_copy_continuous_multivariate_parameter_nstate(
  nstate::ContinuousMultivariateParameterNState,
  monitor::Vector{Bool}
)
  body = []
  fnames = fieldnames(ContinuousMultivariateParameterNState)
  local f::Symbol # f must be local to avoid compiler errors. Alternatively, this variable declaration can be omitted
  statelen::Int

  for j in 2:4
    if monitor[j]
      f = fnames[j]
      push!(body, :(getfield($nstate, $(QuoteNode(f)))[$(:_i)] = getfield($(:_state), $(QuoteNode(f)))))
    end
  end

  for j in (1, 5, 6, 7)
    if monitor[j]
      f = fnames[j]
      push!(
        body,
        :(
          getfield($nstate, $(QuoteNode(f)))[1+($(:_i)-1)*$(:_state).size:$(:_i)*$(:_state).size] =
          getfield($(:_state), $(QuoteNode(f)))
        )
      )
    end
  end

  if monitor[8] || monitor[9] || monitor[10]
    statelen = (nstate.size)^2
  end
  for j in 8:10
    if monitor[j]
      f = fnames[j]
      push!(
        body,
        :(
          getfield($nstate, $(QuoteNode(f)))[1+($(:_i)-1)*$(statelen):$(:_i)*$(statelen)] =
          getfield($(:_state), $(QuoteNode(f)))
        )
      )
    end
  end

  if monitor[11] || monitor[12] || monitor[13]
    statelen = (nstate.size)^3
  end
  for j in 11:13
    if monitor[j]
      f = fnames[j]
      push!(
        body,
        :(
          getfield($nstate, $(QuoteNode(f)))[1+($(:_i)-1)*$(statelen):$(:_i)*$(statelen)] =
          getfield($(:_state), $(QuoteNode(f)))
        )
      )
    end
  end

  if !isempty(nstate.diagnosticvalues)
    push!(body, :($(nstate).diagnosticvalues[:, $(:_i)] = $(:_state).diagnosticvalues))
  end

  @gensym copy_continuous_multivariate_parameter_nstate

  quote
    function $copy_continuous_multivariate_parameter_nstate(_state::ContinuousMultivariateParameterState, _i::Int)
      $(body...)
    end
  end
end

Base.eltype{N<:AbstractFloat}(::Type{ContinuousMultivariateParameterNState{N}}) = N
Base.eltype{N<:AbstractFloat}(s::ContinuousMultivariateParameterNState{N}) = N

Base.(:(==)){S<:ContinuousMultivariateParameterNState}(z::S, w::S) =
  reduce(&, [getfield(z, n) == getfield(w, n) for n in fieldnames(S)[1:17]])

Base.isequal{S<:ContinuousMultivariateParameterNState}(z::S, w::S) =
  reduce(&, [isequal(getfield(z, n), getfield(w, n)) for n in fieldnames(S)[1:17]])
