using Hiccup, Lazy

import Base: MethodList

isuntitled(p) = ismatch(r"^(\.\\|\./)?untitled-[\d\w]+(:\d+)?$", p)

realpath′(p) = ispath(p) ? realpath(p) : p

function basepath(file)
  srcdir = joinpath(JULIA_HOME,"..","..","base")
  releasedir = joinpath(JULIA_HOME,"..","share","julia","base")
  normpath(joinpath(isdir(srcdir) ? srcdir : releasedir, file))
end

fullpath(path) =
  (isuntitled(path) || isabspath(path) ? path : basepath(path)) |> realpath′

function pkgpath(path)
  m = match(r"([^/\\]+[/\\]src[/\\].*)$", path)
  m == nothing ? basename(path) : m.captures[1]
end

appendline(path, line) = line > 0 ? "$path:$line" : path

baselink(path, line) =
  path == "no file" ? span(".fade", path) :
    isuntitled(path) ? link(path, line, Text(appendline("untitled", line))) :
    isabspath(path)  ?
      link(path, line, Text(pkgpath(appendline(path, line)))) :
      link(basepath(path), line, Text(normpath("base/$(appendline(path, line))")))

stripparams(t) = replace(t, r"\{([A-Za-z, ]*?)\}", "")

function methodarray(mt::MethodList)
  defs = collect(mt)
  file(m) = m.file |> string |> basename
  line(m) = m.line
  sort!(defs, lt = (a, b) -> file(a) == file(b) ?
                               line(a) < line(b) :
                               file(a) < file(b))
  return defs
end

methodarray(x) = methodarray(methods(x))

function view(m::Method)
  tv, decls, file, line = Base.arg_decl_parts(m)
  params = [span(c(x, isempty(T) ? "" : "::", strong(stripparams(T)))) for (x, T) in decls[2:end]]
  params = interpose(params, ", ")
  span(c(string(m.name),
         "(", params..., ")")),
  file == :null ? "not found" : baselink(string(file), line)
end

@render i::Inline m::Method begin
  sig, link = view(m)
  r(x) = render(i, x)
  span(c(r(sig), " at ", r(link)))
end

# TODO: factor out table view
@render i::Inline m::MethodList begin
  ms = methodarray(m)
  isempty(ms) && return "$(m.mt.name) has no methods."
  r(x) = render(i, x)
  length(ms) == 1 && return r(ms[1])
  Tree(span(c(span(".support.function", string(m.mt.name)),
              " has $(length(ms)) methods:")),
       [table(".methods", [tr(td(c(r(a))), td(c(r(b)))) for (a, b) in map(view, ms)])])
end
