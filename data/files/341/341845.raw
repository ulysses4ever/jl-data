push!(LOAD_PATH, "location_of_base_files");

using DataFrames
using Trajdata
using HDF5, JLD
using StreetMap
using Curves
using FilesystemUtils

function quat2euler{T <: Real}( quat::Array{T,1} )
	# convert a quaternion to roll-pitch-yaw
	
	den = norm(quat)
	w = quat[1]/den
	x = quat[2]/den
	y = quat[3]/den
	z = quat[4]/den

	roll  = atan2(y*z+w*x, 0.5-x^2-y^2)
	pitch = asin(-2*(x*z + w*y))
	yaw   = atan2(x*y+w*z, 0.5-y^2-z^2)

	return (roll, pitch, yaw)
end
function pull_samples_from_set!(to,from)
	n = length(to)
	m = length(from)
	@assert(n <= m)
	i = 1
	while i ≤ n
		candidate = from[rand(1:m)]
		if !in(candidate, to[1:i-1])
			to[i] = candidate
			i += 1
		end
	end
	return to
end
function fit_poly(
	x_arr :: AbstractVector{Float64},
	y_arr :: AbstractVector{Float64},
	)

	m = length(x_arr)
	@assert(length(y_arr) == m)
	X = [ones(Float64, m)  x_arr  x_arr.^2]
	Xp = X'
	return (Xp*X)\(Xp)*y_arr
end
function fit_error(
	x_arr :: AbstractVector{Float64},
	y_arr :: AbstractVector{Float64},
	coeffs:: AbstractVector{Float64}
	)

	#NOTE(tim): computes sum squares vertical distance from poly
	c1 = coeffs[1]
	c2 = coeffs[2]
	c3 = coeffs[3]

	m = length(x_arr)
	@assert(length(y_arr) == m)

	tot = 0.0
	for i = 1 : m
		x, y = x_arr[i], y_arr[i]
		δ = y - (c1 + x*(c2 + x*c3))
		tot += δ*δ
	end
	tot
end
function distace_to_polynomial(x::Float64, y::Float64, coeffs::AbstractVector{Float64})
	c1 = coeffs[1]
	c2 = coeffs[2]
	c3 = coeffs[3]
	abs(y - (c1 + x*(c2 + x*c3)))
end

immutable RANSAC_Result
	coefficients :: (Float64,Float64,Float64)
	n_iterations :: Int
	n_inliers    :: Int
	n_total_data :: Int
	fit_error    :: Float64
	outliers     :: Set{Int}
end
function RANSAC_polynomial(
	x_arr :: AbstractVector{Float64},
	y_arr :: AbstractVector{Float64},
	n_iter :: Integer,
	datum_fit_threshold :: Real, # threshold value for whether a data point fits model (abs distance from fit)
	n_inliers_for_fit :: Integer # number of inliers required to assert a model fits well
	)

	mean_x = mean(x_arr)
	x_arr .-= mean_x

	iterations = 0
	bestfit = [0.0,0.0,0.0] # [a0,a1,a2]
	besterr = Inf
	ninliers = 0

	m = length(x_arr)
	@assert(m == length(y_arr))
	maybeinliers = Array(Int, 3)
	all_pts = [1:m]

	while iterations < n_iter
		pull_samples_from_set!(maybeinliers, all_pts)
		maybemodel = fit_poly(x_arr[maybeinliers], y_arr[maybeinliers])
		allinliers = falses(m)
		for i = 1 : m
			if !in(i, maybeinliers)
				δ = distace_to_polynomial(x_arr[i], y_arr[i], maybemodel)
				allinliers[i] = (δ < datum_fit_threshold)
			else
				allinliers[i] = true # include pre-selected
			end
		end
		n_new_inliers = sum(allinliers)
		if n_new_inliers ≥ n_inliers_for_fit
			# this implies we may have found a good model
			# now test how good it is
			bettermodel = fit_poly(x_arr[allinliers], y_arr[allinliers])
			thiserr = fit_error(x_arr[allinliers], y_arr[allinliers], bettermodel) / sum(allinliers)
			if n_new_inliers > ninliers || (n_new_inliers == ninliers && thiserr < besterr)
				ninliers, bestfit, besterr = n_new_inliers, bettermodel, thiserr
			end
		end
		iterations += 1
	end

	coefficients = tuple(bestfit...)
	n_iterations = iterations
	n_total_data = m
	fiterror = besterr
	outliers = Set(filter(i->distace_to_polynomial(x_arr[i], y_arr[i], bestfit) > datum_fit_threshold, [1:m]))
	n_inliers = m - length(outliers)
	RANSAC_Result(coefficients, n_iterations, n_inliers, n_total_data, fiterror, outliers)
end
function sliding_window_RANSAC(
	t_arr :: AbstractVector{Float64}, # time, sorted smallest to highest
	y_arr :: AbstractVector{Float64},
	n_iter :: Integer, # number of iterations per
	datum_fit_threshold :: Real, # threshold value for whether a data point fits model (abs distance from fit)
	n_inliers_for_fit :: Integer, # number of inliers required to assert a model fits well
	window_n_samples :: Integer, # number of samples in window
	window_n_overlap :: Integer # number of samples overlapped between windows
	)

	n = length(t_arr)
	@assert(length(y_arr) == n)
	@assert(window_n_samples > 0)
	@assert(window_n_overlap ≥ 0)

	outliers = Set{Int}()
	advancement = window_n_samples - window_n_overlap

	window_lo = 1 - advancement
	window_hi = 0
	while window_hi < n
		window_lo += advancement
		window_hi = min(n, window_lo+window_n_samples-1)
		res = RANSAC_polynomial(t_arr[window_lo:window_hi], y_arr[window_lo:window_hi], n_iter, datum_fit_threshold, n_inliers_for_fit)
		union!(outliers, collect(res.outliers)+window_lo-1)
	end

	outliers
end

function make_angle_continuous!( arr::Vector{Float64} )
	# take an array of angles in [0,2pi] and unwind it without wraparound
	# (jumps from 0 -> 2pi become 0 -> < 0 instead)
	del = 0.0
	add = [0.0, 2pi, -2pi]
	for i = 2 : length(arr)
		δ = arr[i] + del - arr[i-1]
		del += add[indmin(abs(δ + add))]
		arr[i] += del
	end
	arr
end
function mod2pi_neg_pi_to_pi!( arr::Vector{Float64} )
	for i = 1 : length(arr)
		ψ = mod2pi(arr[i])
		if ψ > π
			ψ -= 2π
		end
		arr[i] = ψ
	end
	arr
end

function pad_linear(arr::Vector{Float64}, n_samples_each_side::Int)
	# NOTE(tim): extends both ends of arr using a two-point linear approximation

	n = length(arr)

	@assert(n > 1)
	@assert(n_samples_each_side ≥ 0)

	slope_start = arr[2]   - arr[1]
	slope_end   = arr[end] - arr[end-1]

	retval = Array(Float64, n + 2n_samples_each_side)
	retval[n_samples_each_side+1:n_samples_each_side+n] = arr

	ind_start = n_samples_each_side
	ind_end   = n + n_samples_each_side + 1
	for i = 1 : n_samples_each_side
		retval[ind_start] = retval[ind_start+1] - slope_start
		retval[ind_end]   = retval[ind_end-1]   + slope_end
		ind_start -= 1
		ind_end += 1
	end
	
	retval
end
function remove_pad(arr::Vector, n_samples_each_side::Int)
	n = length(arr)

	@assert(n ≥ 2n_samples_each_side)
	@assert(n_samples_each_side ≥ 0)

	lo = n_samples_each_side + 1
	hi = n - n_samples_each_side
	arr[lo:hi]
end

function smooth(x_arr::Vector{Float64}, y_arr::Vector{Float64}, variance::Float64, outliers::Set{Int}=Set{Int}())

	@assert(variance > 0.0)
	@assert(length(x_arr) == length(y_arr))
	@assert(length(x_arr) >  length(outliers))

	G = x -> exp(-x*x/(2*variance)) / sqrt(2pi*variance)
	threshold = 1e-9

	N = length(x_arr)
	y_arr2 = Array(Float64, N)
	for (i, x) in enumerate(x_arr)

		i_left  = i - 1 # number of indeces to the left
		i_right = N - i # number of indeces to the right
		i_lo,i_hi    = i_left < i_right ? (1,2i-1) : (2i-N,N)

        if in(i,outliers)
			w_tot = 0.0
			v_tot = 0.0
		else
			w_tot = G(x - x_arr[i])
			v_tot = w_tot*y_arr[i]
		end

		for ind = i-1 : -1 : i_lo
			w = G(x-x_arr[ind])
			if w < threshold
				break
			end
			if !in(ind, outliers)
				w_tot += w
				v_tot += w*y_arr[ind]
			end
		end
		for ind = i+1 : i_hi
			w = G(x-x_arr[ind])
			if w < threshold
				break
			end
			if !in(ind, outliers)
				w_tot += w
				v_tot += w*y_arr[ind]
			end
		end
		y_arr2[i] = v_tot / w_tot
	end
	y_arr2
end
function smooth(x_arr::Vector{Float64}, y_arr::Vector{Float64}, x_arr2::Vector{Float64}, variance::Float64, outliers::Set{Int}=Set{Int}())

	@assert(variance > 0.0)
	@assert(length(x_arr) == length(y_arr))
	@assert(length(x_arr) >  length(outliers))

	G = x -> exp(-x*x/(2*variance)) / sqrt(2pi*variance)
	threshold = 1e-9

	N = length(x_arr)
    y_arr2 = Array(Float64, length(x_arr2))
	for (i, x) in enumerate(x_arr2)
        
        ind_closest = indmin([abs(x-x0) for x0 in x_arr])
        
        i_left  = ind_closest - 1 # number of indeces to the left
		i_right = N - ind_closest # number of indeces to the right
		i_lo,i_hi    = i_left < i_right ? (1,2ind_closest-1) : (2ind_closest-N,N)

		if in(ind_closest, outliers)
			w_tot = v_tot = 0.0
		else
			w_tot = G(x - x_arr[ind_closest])
			v_tot = w_tot*y_arr[ind_closest]
		end

        for ind = ind_closest-1 : -1 : i_lo
			w = G(x-x_arr[ind])
			if w < threshold
				break
			end
			if !in(ind, outliers)
				w_tot += w
				v_tot += w*y_arr[ind]
			end
		end
		for ind = ind_closest+1 : i_hi
			w = G(x-x_arr[ind])
			if w < threshold
				break
			end
			if !in(ind, outliers)
				w_tot += w
				v_tot += w*y_arr[ind]
			end
		end
		y_arr2[i] = v_tot / w_tot
	end
	y_arr2
end
function resample_snap_to_closest{R<:Any}(x_arr::Vector{Float64}, y_arr::DataVector{R}, x_arr2::Vector{Float64})

	n = length(x_arr)
	@assert(length(y_arr) == n)
	m = length(x_arr2)

	retval = Array(R, m)

	x_ind = 1
	x = (x_arr[x_ind] + x_arr[x_ind+1])/2

	for i = 1 : m
		x2 = x_arr2[i]
		
		while x2 > x && x_ind < n
			x_ind += 1
			x = (x_arr[x_ind] + x_arr[min(x_ind+1,n)])/2
		end

		retval[i] = y_arr[x_ind]
	end

	retval
end

function continuous_segments(arr::AbstractVector{Bool})

	# INPUT: boolean or bit array
	# OUTPUT: set of continuous segments
	#
	# ex: [T,T,T,F,F,T,T]
	#     [(1,3)(6,7)]

	ind = 1
	N = length(arr)
	segmentset = (Int,Int)[] # (frameind_lo, frameind_hi)
	while ind <= N
		curseg_1 = findnext(arr, true, ind)
		if curseg_1 == 0
			break
		end

		ind = curseg_1 + 1
		curseg_2 = findnext(arr, false, ind)

		if curseg_2 == 0
			curseg_2 = N
		else
			ind = curseg_2
			curseg_2 -= 1
		end
		
		push!(segmentset, (curseg_1, curseg_2))
	end
	segmentset
end
function near_continuous_segments(arr::Vector{Int}, tol::Int)
	# this assumes frames are evenly spaced
	# arr :: list of integer values, sorted in increasing order
	# tol :: tolerance in jump between segments
	# outputs list of segments by index of arr

	# example:
	#   arr = [1,2,3,5,10,11,12]
	#   tol = 2
	#   output: [(1,4),(5,7)]

	segmentset = (Int,Int)[] # (lo,hi)
	prev_val, prev_ind = arr[1], 1
	curseg_1 = 1
	for (ind,val) in enumerate(arr)
		Δ = val - prev_val
		@assert(Δ >= 0)
		if Δ > tol # start a new segment
			push!(segmentset, (curseg_1, prev_ind))
			curseg_1 = ind
		end
		prev_ind = ind
		prev_val = val
	end

	push!(segmentset, (curseg_1, length(arr)))
	segmentset
end

function encompasing_indeces{I<:Integer}(inds::Vector{I}, sample_time_A::Vector{Float64}, sample_time_B::Vector{Float64})
	# given a set of indeces (in order, ascending) from sample_time_A (in order, ascending)
	# find the set of indeces in sample_time_B that encompass inds

	keep = falses(length(sample_time_B))
	
	Ni = length(inds)
	Na = length(sample_time_A)
	Nb = length(sample_time_B)

	indB = 0

	ia = 1
	while ia ≤ Ni
		ind = inds[ia]
		t_lo = sample_time_A[ind]

		while ia < Ni && inds[ia+1] == ind+1
			ia += 1
			ind += 1
		end

		t_hi = sample_time_A[ind]
		ia += 1

		# next project [t_lo ↔ t_hi] onto sample_time_B
		
		while indB < Nb && sample_time_B[indB+1] ≤ t_lo
			indB += 1
		end

		keep[indB] = true

		while indB < Nb && sample_time_B[indB+1] ≤ t_hi
			keep[indB += 1] = true
		end

		if !isapprox(t_hi, sample_time_B[indB]) && indB < Nb
			keep[indB+=1] = true
		end
	end

	retval = Array(I, sum(keep))
	count = 0
	for (i,k) in enumerate(keep)
		if k
			retval[count+=1] = i
		end
	end

	retval
end

function load_trajdata(csvfile::String)

    file = open(csvfile, "r")
    lines = readlines(file)
    close(file)

    n_cols = length(matchall(r",", lines[1]))+1

    temp_name = tempname()*".csv"

    file = open(temp_name, "w")
    for (i,line) in enumerate(lines)
        
        line = replace(line, "None", "Inf")
        
        cols = length(matchall(r",", lines[i]))+1
        @printf(file, "%s", lines[i][1:end-1]*(","^(n_cols-cols))*"\n" )
    end
    close(file)

    df = readtable(temp_name)
    rm(temp_name)

    # rename the columns
    # ----------------------------------------
    colnames = names(df)
    rename_scheme = ((:entry, :frame), (:timings, :time), (:control_node_status, :control_status), (:global_position_x, :posGx),
        (:global_position_y, :posGy), (:global_position_z, :posGz), (:global_rotation_w, :quatw), (:global_rotation_x, :quatx),
        (:global_rotation_y, :quaty), (:global_rotation_z, :quatz), (:odom_velocity_x, :velEx), (:odom_velocity_y, :velEy),
        (:odom_velocity_z, :velEz), (:odom_acceleration_x, :accEx), (:odom_acceleration_y, :accEy), (:odom_acceleration_z, :accEz))
    for (source, target) in rename_scheme
        if in(source, colnames)
            rename!(df, source, target)
        end
    end

    carind = 0
    while haskey(df, symbol(@sprintf("car_id%d", carind)))
       
        rename!(df, symbol(@sprintf("car_id%d",carind)), symbol(@sprintf("id_%d",    carind)))
        rename!(df, symbol(@sprintf("ego_x%d", carind)), symbol(@sprintf("posEx_%d", carind)))
        rename!(df, symbol(@sprintf("ego_y%d", carind)), symbol(@sprintf("posEy_%d", carind)))
        rename!(df, symbol(@sprintf("v_x%d",   carind)), symbol(@sprintf("velEx_%d", carind)))
        rename!(df, symbol(@sprintf("v_y%d",   carind)), symbol(@sprintf("velEy_%d", carind)))
        
        carind += 1
    end
    maxcarind = carind - 1

    # replace quatx, quaty, quatz with rollG, pitchG, yawG
    # ----------------------------------------
    rpy = zeros(size(df,1), 3)
    for i = 1 : size(df,1)
    	quat = [df[:quatw][i], df[:quatx][i], df[:quaty][i], df[:quatz][i]]
    	rpy[i,:] = [quat2euler(quat)...]
    end
    df[:quatx] = rpy[:,1]
    df[:quaty] = rpy[:,2]
    df[:quatz] = rpy[:,3]
    rename!(df, :quatx, :rollG)
    rename!(df, :quaty, :pitchG)
    rename!(df, :quatz, :yawG)
    delete!(df, :quatw)

	# add a column for every id seen
	# for each frame, list the car index it corresponds to or 0 if it is not in the frame
	# ----------------------------------------
	idset = Int[]
	for i = 1 : size(df,1)
		for carind = 0 : maxcarind
			id = df[symbol(@sprintf("id_%d", carind))][i]
			if !isa(id, NAtype) && !in( id, idset )
				push!(idset, id)
			end
		end
	end
	for id in sort!(idset)
		df[symbol(@sprintf("has_%d", id))] = -1*ones(size(df,1))
	end
	for frame = 1 : size(df,1)
		for carind = 0 : maxcarind
			carid = df[symbol(@sprintf("id_%d", carind))][frame]
			if !isa(carid, NAtype)
				df[symbol(@sprintf("has_%d", carid))][frame] = carind
			end
		end
	end

    df
end

type PrimaryDataExtractionParams
	
	resample_rate :: Float64 # seconds between re-sampled samples (>0)
	padding_size  :: Int     # number of samples to append to either end in smoothing (≥0)

	ransac_n_iter :: Int     # number of RANSAC iterations (>0)
	ransac_window_width :: Int # [samples] (>0)
	ransac_window_overlap :: Int # [samples] (≥0)
	ransac_n_inliers_for_first_fit :: Int # (>1)

	threshold_percent_outliers_warn  :: Float64 # will warn user if higher percent outliers than this
	threshold_percent_outliers_error :: Float64 # will throw an error if higher percent outliers than this

	buffer_frames                     :: Int     # number of frames to discard on either end of a drive log (≥0)
	min_frames_ego_on_freeway         :: Int     # min frames ego must be on freeway
	threshold_lane_angle_ego          :: Float64 # [rad]
	threshold_lane_lateral_offset_ego :: Float64 # [m]
	threshold_proj_sqdist_ego         :: Float64 # [m²]

	threshold_other_frame_gap         :: Int     # threshold for splitting other cars into continous segments. Minimum # of frames we can interpolate over
	threshold_other_segment_length    :: Int     # minimum length for a continuous segment to be included for other vehicle
	threshold_percent_outliers_toss   :: Float64 # if we have a higher percent outlier than this, toss the segment

	threshold_lane_lateral_offset_other :: Float64 # [m]
	threshold_proj_sqdist_other         :: Float64 # [m²]
	threshold_lane_angle_other          :: Float64 # [rad]
	threshold_other_from_lane_ends      :: Float64 # [m] distance from lane start & end that we want to be

	frameinds :: Vector{Int} # if empty, use all of them; otherwise use [x₁:x₂, x₃:x₄, ...]
	default_control_status :: Int

	function PrimaryDataExtractionParams()
		self = new()
		self.resample_rate = 1/20.0 # exactly 20 Hz
		self.padding_size  = 50
		self.ransac_n_iter = 50
		self.ransac_window_width = 20
		self.ransac_window_overlap = 5
		self.ransac_n_inliers_for_first_fit = 5
		self.threshold_percent_outliers_warn = 0.5
		self.threshold_percent_outliers_error = 2.5
		self.buffer_frames = 5
		self.min_frames_ego_on_freeway = 1200
		self.threshold_lane_angle_ego = deg2rad(30)
		self.threshold_lane_lateral_offset_ego = 2.5
		self.threshold_proj_sqdist_ego = self.threshold_lane_lateral_offset_ego * self.threshold_lane_lateral_offset_ego
		self.threshold_other_frame_gap = 5
		self.threshold_other_segment_length = 20
		self.threshold_percent_outliers_toss = 25.0
		self.threshold_lane_lateral_offset_other = 2.0
		self.threshold_proj_sqdist_other = self.threshold_lane_lateral_offset_other * self.threshold_lane_lateral_offset_other
		self.threshold_lane_angle_other = deg2rad(30.0)
		self.threshold_other_from_lane_ends = 1.0
		self.frameinds = Int[]
		self.default_control_status = Trajdata.CONTROL_STATUS_FAILURE
		self
	end
end
function gen_primary_data(trajdata::DataFrame, sn::StreetNetwork, params::PrimaryDataExtractionParams)

	@assert(params.resample_rate > 0.0)
	@assert(params.padding_size > 0)
	@assert(params.ransac_n_iter > 0)
	@assert(params.ransac_window_width > 0)
	@assert(params.ransac_window_overlap ≥ 0)
	@assert(params.ransac_n_inliers_for_first_fit > 1)
	@assert(params.threshold_percent_outliers_warn ≥ 0.0)
	@assert(params.threshold_percent_outliers_error ≥ params.threshold_percent_outliers_warn)
	@assert(params.buffer_frames ≥ 0)
	@assert(params.threshold_lane_angle_ego ≥ 0.0)
	@assert(params.threshold_lane_lateral_offset_ego ≥ 0.0)
	@assert(params.threshold_proj_sqdist_ego ≥ 0.0)
	@assert(params.threshold_other_frame_gap ≥ 0)
	@assert(params.threshold_other_segment_length > 0)
	@assert(params.threshold_percent_outliers_toss > 0.0)
	@assert(params.threshold_lane_lateral_offset_other ≥ 0.0)
	@assert(params.threshold_proj_sqdist_other ≥ 0.0)
	@assert(params.threshold_lane_angle_other > 0.0)
	@assert(params.threshold_other_from_lane_ends ≥ 0.0)

	# initial ego smoothing and outlier removal
	# ----------------------------------------

	n_initial_samples  = size(trajdata,1)

	arr_time  = array(trajdata[:time]) - trajdata[1, :time]

	n_resamples = int(floor((arr_time[end] - arr_time[1]) / params.resample_rate))
	arr_time_resampled = [0:n_resamples-1]*params.resample_rate + arr_time[1]
	arr_time_padded = pad_linear(arr_time, params.padding_size)
	arr_time_resampled_padded = pad_linear(arr_time_resampled, params.padding_size)

	trajdata_smoothed = DataFrame()
	trajdata[:yawG] = make_angle_continuous!(array(trajdata[:yawG]))
	for (variable, RANSAC_fit_threshold, smoothing_variance) in
		[(:posGx,  Inf,  0.5),
		 (:posGy,  Inf,  0.5),
		 (:yawG,   0.01, 0.5),
		 (:velEx,  0.1,  0.5),
		 (:velEy,  0.05, 0.5)]

		arr_orig_padded = pad_linear(array(trajdata[variable]), params.padding_size)

		if isinf(RANSAC_fit_threshold)
			outliers = Set{Int}()
		else
			outliers = sliding_window_RANSAC(arr_time_padded, arr_orig_padded, params.ransac_n_iter, 
							RANSAC_fit_threshold, params.ransac_n_inliers_for_first_fit,
							params.ransac_window_width, params.ransac_window_overlap)
		end

		percent_outliers = 100.0*length(outliers) / n_initial_samples
		percent_outliers < params.threshold_percent_outliers_error || error("TOO MANY OUTLIERS ($percent_outliers \%) FOR VARIABLE $variable")
		percent_outliers < params.threshold_percent_outliers_warn  ||  warn("TOO MANY OUTLIERS ($percent_outliers \%) FOR VARIABLE $variable")

		arr_smoothed_padded = smooth(arr_time_padded, arr_orig_padded, arr_time_resampled_padded, smoothing_variance, outliers)
		trajdata_smoothed[variable] = remove_pad(arr_smoothed_padded, params.padding_size)
	end

	# initialize the DataFrame
	# ----------------------------------------

	df_ego_primary = DataFrame(
	    time           = arr_time_resampled,              # time
	    frame          = [1:n_resamples],                 # frame
	    control_status = DataArray(Int, n_resamples),     # trajdata[:control_status], # enum identifier of control status (5==AUTO)
	    posGx          = trajdata_smoothed[:posGx],       # easting in the global frame
	    posGy          = trajdata_smoothed[:posGy],       # northing in the global frame
	    posGyaw        = trajdata_smoothed[:yawG],        # heading in the global frame
	    posFx          = DataArray(Float64, n_resamples), # longitudinal distance along RHS lane centerline
	    posFy          = DataArray(Float64, n_resamples), # lateral distance from RHS lane center
	    posFyaw        = DataArray(Float64, n_resamples), # heading angle (average between two closest lanes)
	    velFx          = DataArray(Float64, n_resamples), # longitudinal speed in lane
	    velFy          = DataArray(Float64, n_resamples), # lateral speed in lane
	    lanetag        = DataArray(LaneTag, n_resamples), # current lane definition
	    nll            = DataArray(Int8,    n_resamples), # number of lanes to the left
	    nlr            = DataArray(Int8,    n_resamples), # number of lanes to the right
	    curvature      = DataArray(Float64, n_resamples), # local lane curvature
	    d_cl           = DataArray(Float64, n_resamples), # lateral distance between center of car and closest centerline (true)
	    d_ml           = DataArray(Float64, n_resamples), # lateral distance from left lane marker
	    d_mr           = DataArray(Float64, n_resamples), # lateral distance from right lane marker
	    d_merge        = DataArray(Float64, n_resamples), # distance along the road until the next merge
	    d_split        = DataArray(Float64, n_resamples)  # distance along the road until the next split
	)

	# lane frame projection and feature extraction
	# --------------------------------------------

	ego_car_on_freeway = falses(n_resamples)

	if :control_status in names(trajdata)
		df_ego_primary[:,:control_status] = resample_snap_to_closest(arr_time, trajdata[:control_status], arr_time_resampled)
	else
		fill!(df_ego_primary[:control_status], params.default_control_status)
	end

	for frameind = params.buffer_frames : (n_resamples-params.buffer_frames)

	    posGx   = trajdata_smoothed[frameind, :posGx]
	    posGy   = trajdata_smoothed[frameind, :posGy]
	    posGyaw = trajdata_smoothed[frameind, :yawG ]

	    proj = project_point_to_streetmap(posGx, posGy, sn)
	    if proj.successful && proj.sqdist < params.threshold_proj_sqdist_ego
	        ptG = proj.curvept
	        s,d,θ = pt_to_frenet_xyy(ptG, posGx, posGy, posGyaw)

	        df_ego_primary[frameind, :posFx] = s
	        df_ego_primary[frameind, :posFy] = d
	        df_ego_primary[frameind, :posFyaw] = θ
	        
	        meets_lane_lateral_offset_criterion = abs(d) < params.threshold_lane_lateral_offset_ego
	        meets_lane_angle_criterion = abs(θ) < params.threshold_lane_angle_ego
	        ego_car_on_freeway[frameind] = meets_lane_lateral_offset_criterion && meets_lane_angle_criterion
	        
	        if ego_car_on_freeway[frameind]

	            # extract specifics
	            speed = hypot(trajdata_smoothed[frameind, :velEx], trajdata_smoothed[frameind, :velEy])
	            df_ego_primary[frameind, :velFx] = speed * cos(θ) # vel along the lane
	            df_ego_primary[frameind, :velFy] = speed * sin(θ) # vel perpendicular to lane

                tile = proj.tile
	            df_ego_primary[frameind,:lanetag] = LaneTag(tile, proj.laneid)
	            df_ego_primary[frameind,:curvature] = ptG[KIND]
	            
	            df_ego_primary[frameind,:d_cl   ] = d
	            
	            laneid = int(proj.laneid.lane)

	            seg = get_segment(tile, int(proj.laneid.segment))
	            d_merge = distance_to_lane_merge(seg, laneid, proj.extind)
	            d_split = distance_to_lane_split(seg, laneid, proj.extind)
	            df_ego_primary[frameind, :d_merge]  =isinf(d_merge)  ? NA : d_merge
	            df_ego_primary[frameind, :d_split] = isinf(d_split) ? NA : d_split
	            
	            nll, nlr = StreetMap.num_lanes_on_sides(seg, laneid, proj.extind)
	            @assert(nll >= 0)
	            @assert(nlr >= 0)
	            df_ego_primary[frameind,:nll    ] = nll # number of lanes to the left
	            df_ego_primary[frameind,:nlr    ] = nlr # number of lanes to the right
	            
	            lane_width_left, lane_width_right = marker_distances(seg, laneid, proj.extind)
	            df_ego_primary[frameind, :d_mr] = (d <  lane_width_left)  ?  lane_width_left - d  : Inf
	            df_ego_primary[frameind, :d_ml] = (d > -lane_width_right) ?  d - lane_width_right : Inf
	        end
	    end
	end

	if !isempty(params.frameinds)

		orig_index = 1
		t_lo = arr_time[params.frameinds[orig_index]]
		t_hi = arr_time[params.frameinds[orig_index+1]]

		for sample = 1 : n_resamples
			t = arr_time_resampled[sample]

			if t > t_hi
				orig_index += 2
				if orig_index < length(params.frameinds)
					t_lo = arr_time[params.frameinds[orig_index]]
					t_hi = arr_time[params.frameinds[orig_index+1]]
				else
					t_lo = Inf
					t_hi = Inf
				end
			end

			if t < t_lo
				ego_car_on_freeway[sample] = false
			end
		end
	end

	const n_frames_on_freeway = sum(ego_car_on_freeway)
	if n_frames_on_freeway < params.min_frames_ego_on_freeway
		print_with_color(:red, "EGO IS ONLY ON FREEWAY FOR $n_frames_on_freeway FRAMES!\n")
		return NaN
	end

	# run post-smoothing
	# --------------------------------------------

	arr_time_on_freeway = pad_linear(arr_time_resampled[ego_car_on_freeway], params.padding_size)

	df_ego_primary[ego_car_on_freeway, :posFyaw] = make_angle_continuous!(array(df_ego_primary[ego_car_on_freeway, :posFyaw]))
	for (variable, smoothing_variance) in
		[(:posFyaw,   0.2),
		 (:velFx,     0.2),
		 (:velFy,     0.2),
		 (:curvature, 0.2)]

		arr_orig = pad_linear(array(df_ego_primary[ego_car_on_freeway, variable]), params.padding_size)
		arr_smoothed = smooth(arr_time_on_freeway, arr_orig, smoothing_variance)
		df_ego_primary[ego_car_on_freeway, variable] = remove_pad(arr_smoothed, params.padding_size)
	end
	df_ego_primary[ego_car_on_freeway, :posFyaw] = mod2pi_neg_pi_to_pi!(array(df_ego_primary[ego_car_on_freeway, :posFyaw]))


	# Other Car Extraction
	#  - for each continuous freeway segment
	#      - for each car id
	#            - identify frames where it too is on the freeway
	#            - pull data for all frames where it is available
	#            - maintain list of missing frames
	#            - run smoothing / interpolation same as ego
	#            - map to frenet
	# --------------------------------------------

	const N = size(df_ego_primary, 1)

	const maxncars = get_max_num_cars(trajdata)
	const frameinds = [1:N]
	const freeway_frameinds = frameinds[ego_car_on_freeway]
	const freeway_frameinds_raw = encompasing_indeces(freeway_frameinds, arr_time_resampled, arr_time)
	const frameind2validfind = begin
									arr = fill(-1, N)
									for (validfind,frameind) in enumerate(freeway_frameinds)
										arr[frameind] = validfind
									end
									arr
							  	end

	# --------------------------------------------------
	# df_other_primary contains values for each validfind indexed by carind
	# to obtain values from a carid one needs to go through dict_other_idmap, and mat_other_indmap

	df_other_primary = DataFrame()
	add_symbol! = (str,ind,typ)->df_other_primary[symbol(@sprintf("%s_%d",str,ind))] = DataArray(typ, n_frames_on_freeway)
	add_slot!   = (cind)->begin
								add_symbol!("posGx",     cind, Float64)
								add_symbol!("posGy",     cind, Float64)
								add_symbol!("posGyaw",   cind, Float64)
								add_symbol!("posFx",     cind, Float64)
								add_symbol!("posFy",     cind, Float64)
								add_symbol!("posFyaw",   cind, Float64)
								add_symbol!("velFx",     cind, Float64)
								add_symbol!("velFy",     cind, Float64)
								add_symbol!("lanetag",   cind, LaneTag)
								add_symbol!("nlr",       cind, Int8)
								add_symbol!("nll",       cind, Int8)
								add_symbol!("curvature", cind, Float64)
								add_symbol!("d_cl",      cind, Float64)
								add_symbol!("d_mr",      cind, Float64)
								add_symbol!("d_ml",      cind, Float64)
								add_symbol!("d_merge",   cind, Float64)
								add_symbol!("d_split",   cind, Float64)
								add_symbol!("id",        cind, Uint32)
								add_symbol!("t_inview",  cind, Float64)
								add_symbol!("trajind",   cind, Uint32)
							  end
	for cind = 0 : maxncars # NOTE: indexing starts from 0, like in trajdata
		add_slot!(cind)
	end

	# --------------------------------------------------

	const carids     = get_carids(trajdata)     # a Set{Int} of carids
	dict_other_idmap = Dict{Uint32,Uint16}()    # dict carid -> matind,  index for mat_other_indmap
	mat_other_indmap = fill(int16(-1), n_frames_on_freeway, length(carids)) # [validfind,matind] -> carind, -1 if does not exist

	setc! = (str,carind,validfind,value) -> df_other_primary[validfind, symbol(@sprintf("%s_%d", str, carind))] = value
	next_available_carind = fill(int32(-1), n_frames_on_freeway)

	# --------------------------------------------------

	for (matind, carid) in enumerate(carids)

		tic()
		print(carid, ": ", matind, " / ", length(carids), "  ")

		# assign a matind to this carid
		dict_other_idmap[carid] = matind

		# all freeway_frameinds where the car exists
		car_frameinds_raw = filter(frameind->carid_exists(trajdata, carid, frameind), freeway_frameinds_raw)
		if isempty(car_frameinds_raw)
			toc()
			continue
		end
		# println(car_frameinds_raw)

		segment_count = 0
		data_arr_index = 0

		for (lo,hi) in near_continuous_segments(car_frameinds_raw, params.threshold_other_frame_gap)

			# println("\tseg: ($lo, $hi), [$(car_frameinds_raw[lo]), $(car_frameinds_raw[hi])]")

			segment_frameinds = [car_frameinds_raw[lo] : car_frameinds_raw[hi]] # array of all frames for this segment
			n_frames_in_seg = length(segment_frameinds)

			# println("n_frames_in_seg: ", n_frames_in_seg)

			# ------------------------------------------
			# enforce minimum segment length
			if n_frames_in_seg < params.threshold_other_segment_length
				print_with_color(:red, "skipping due to insufficient length ($n_frames_in_seg < $(params.threshold_other_segment_length))\n")
				continue
			end

			# ------------------------------------------
			# run smoothing + interpolation

			# whether car exists in frame
			car_exists = falses(n_frames_in_seg) 
			car_exists[car_frameinds_raw[lo:hi]-car_frameinds_raw[lo]+1] = true
			n_frames_exist = sum(car_exists)

			# println(map(b->b ? '1' : '0', car_exists))

			# map index to carind (-1 if no exist)
			carinds_raw = map(i->carid2ind(trajdata, carid, segment_frameinds[i]), 1:n_frames_in_seg)

			time_obs = arr_time[car_frameinds_raw[lo:hi]] # actual measured time

			time_resampled_ind_lo = findfirst(i->arr_time_resampled[i] ≥ time_obs[1], 1:n_resamples)
			time_resampled_ind_hi = findnext(i->arr_time_resampled[i+1] > time_obs[end], 1:n_resamples-1, time_resampled_ind_lo)
			@assert(time_resampled_ind_lo != 0 && time_resampled_ind_hi != 0)
			time_resampled = arr_time_resampled[time_resampled_ind_lo:time_resampled_ind_hi]
			smoothed_frameinds = df_ego_primary[time_resampled_ind_lo:time_resampled_ind_hi, :frame]

			# println(time_resampled_ind_lo, "  ", arr_time_resampled[time_resampled_ind_lo-1], " < ", time_obs[1], " ≤ ", arr_time_resampled[time_resampled_ind_lo])
			# println(time_resampled_ind_hi, "  ", arr_time_resampled[time_resampled_ind_hi], " ≤ ", time_obs[end], " < ", arr_time_resampled[time_resampled_ind_hi+1])
			# println(size(time_resampled))

			time_obs_padded = pad_linear(time_obs, params.padding_size)
			time_resampled_padded = pad_linear(time_resampled, params.padding_size)

			# TODO(tim): can this be optimized with pre-allocation outside of the loop?
			# NOTE(tim): this assumes zero sideslip
			data_obs = DataFrame()
			for sym in (:posGx, :posGy, :yawG, :velBx)
				data_obs[sym] = DataArray(Float64, n_frames_exist)
			end
			count = 0
			for (i,frameind) in enumerate(segment_frameinds)

				if car_exists[i]
					count += 1
					carind = carinds_raw[i]
					@assert(carind != -1)

					posEx = getc(trajdata, "posEx", carind, frameind)
					posEy = getc(trajdata, "posEy", carind, frameind)
					velEx = getc(trajdata, "velEx", carind, frameind) # NOTE(tim): velocity in the ego frame but pre-compensated for ego velocity
					velEy = getc(trajdata, "velEy", carind, frameind)

					posGx_ego = trajdata[frameind, :posGx]
					posGy_ego = trajdata[frameind, :posGy]
					yawG_ego  = trajdata[frameind, :yawG]

					posGx, posGy = Trajdata.ego2global(posGx_ego, posGy_ego, yawG_ego, posEx, posEy)
					data_obs[count, :posGx] = posGx
					data_obs[count, :posGy] = posGy

					velGx, velGy = Trajdata.ego2global(0.0, 0.0, yawG_ego, velEx, velEy)

					if hypot(velGx, velGy) > 3.0
						yawG = atan2(velGy, velGx)
					else
						yawG = yawG_ego # to fix problem with very low velocities
					end
					data_obs[count, :velBx] = hypot(velGx, velGy)
					data_obs[count, :yawG]  = yawG
				end
			end
			@assert(count == size(data_obs, 1))
			data_obs[:yawG] = make_angle_continuous!(array(data_obs[:yawG]))

			data_smoothed = DataFrame()
			should_toss_due_to_outliers = false
			for (variable, RANSAC_fit_threshold, smoothing_variance) in
				[(:posGx,  0.5,  0.5), # TODO(tim): tune these
				 (:posGy,  0.5,  0.5),
				 (:yawG,   0.05, 0.5),
				 (:velBx,  2.0,  0.5)] # NOTE(tim): many vehicles are sensitive here... need to investigate

				arr_orig_padded = pad_linear(array(data_obs[variable]), params.padding_size)

				outliers = sliding_window_RANSAC(time_obs_padded, arr_orig_padded, params.ransac_n_iter, 
									RANSAC_fit_threshold, params.ransac_n_inliers_for_first_fit,
									params.ransac_window_width, params.ransac_window_overlap)

				percent_outliers = 100.0*length(outliers) / n_frames_in_seg
				if percent_outliers > params.threshold_percent_outliers_toss
					should_toss_due_to_outliers = true
					print_with_color(:red, "skipping due to high outlier percentage in $variable ($percent_outliers > $(params.threshold_percent_outliers_toss))\n")
					break
				end

				arr_smoothed_padded = smooth(time_obs_padded, arr_orig_padded, time_resampled_padded, smoothing_variance, outliers)
				data_smoothed[variable] = remove_pad(arr_smoothed_padded, params.padding_size)
			end
			if should_toss_due_to_outliers
				continue
			end

			inds_to_keep = find(frameind->ego_car_on_freeway[frameind], smoothed_frameinds)
			smoothed_frameinds = smoothed_frameinds[inds_to_keep]
			data_smoothed = data_smoothed[inds_to_keep, :]

			if size(data_smoothed, 1) < params.threshold_other_segment_length
				print_with_color(:red, "skipping due to insufficient length after smoothing ($n_frames_in_seg < $(params.threshold_other_segment_length))\n")
				continue
			end

			# ------------------------------------------
			# map to frenet frame & extract values

			segment_count += 1
			for (i,frameind) = enumerate(smoothed_frameinds)

				@assert(ego_car_on_freeway[frameind])

				posGx   = data_smoothed[i, :posGx]
			    posGy   = data_smoothed[i, :posGy]
			    posGyaw = data_smoothed[i, :yawG ]

			    proj = project_point_to_streetmap(posGx, posGy, sn)
			    if proj.successful && proj.sqdist < params.threshold_proj_sqdist_other

			        ptG = proj.curvept
			        s,d,θ = pt_to_frenet_xyy(ptG, posGx, posGy, posGyaw)
			        
			       	laneid = int(proj.laneid.lane)
		            tile = proj.tile
		            seg = get_segment(tile, int(proj.laneid.segment))
		            d_end = distance_to_lane_end(seg, laneid, proj.extind)

			        meets_lane_lateral_offset_criterion = abs(d) < params.threshold_lane_lateral_offset_other
			        meets_lane_angle_criterion = abs(θ) < params.threshold_lane_angle_other
			        meets_lane_end_criterion = d_end > params.threshold_other_from_lane_ends

			        if meets_lane_lateral_offset_criterion && meets_lane_angle_criterion && meets_lane_end_criterion

			        	validfind = frameind2validfind[frameind]
						carind = (next_available_carind[validfind] += 1)

						# need to add another car slot
						if carind > maxncars
							maxncars += 1
							add_slot!(maxncars)
							carind = maxncars
						end

						#set mat_other_indmap to point to the carind for this car for all valid frames
						mat_other_indmap[validfind, matind] = carind

			        	setc!("posGx", carind, validfind, posGx)
				        setc!("posGy", carind, validfind, posGy)
				        setc!("posGyaw", carind, validfind, posGyaw)
				        setc!("posFx", carind, validfind, s)
				        setc!("posFy", carind, validfind, d)
				        setc!("posFyaw", carind, validfind, θ)

			            # extract specifics
			            speed = data_smoothed[i, :velBx]
			            setc!("velFx",     carind, validfind, speed * cos(θ)) # vel along the lane
			            setc!("velFy",     carind, validfind, speed * sin(θ)) # vel perpendicular to lane
			            setc!("lanetag",    carind, validfind, LaneTag(tile, proj.laneid))
			            setc!("curvature", carind, validfind, ptG[KIND])
			            setc!("d_cl",      carind, validfind, d)
			           
			            d_merge = distance_to_lane_merge(seg, laneid, proj.extind)
			            d_split = distance_to_lane_split(seg, laneid, proj.extind)
			            setc!("d_merge", carind, validfind, isinf(d_merge) ? NA : d_merge)
			            setc!("d_split", carind, validfind, isinf(d_split) ? NA : d_split)
			            
			            nll, nlr = StreetMap.num_lanes_on_sides(seg, laneid, proj.extind)
			            @assert(nll >= 0)
			            @assert(nlr >= 0)
			            setc!("nll", carind, validfind, nll)
			            setc!("nlr", carind, validfind, nlr)
			            
			            lane_width_left, lane_width_right = marker_distances(seg, laneid, proj.extind)
			            setc!("d_mr", carind, validfind, (d <  lane_width_left)  ?  lane_width_left - d  : Inf)
			            setc!("d_ml", carind, validfind, (d > -lane_width_right) ?  d - lane_width_right : Inf)

		            	setc!("id",        carind, validfind, carid)
						setc!("t_inview",  carind, validfind, time_resampled[end] - time_resampled[i])
						setc!("trajind",   carind, validfind, segment_count)
					else

						print_with_color(:red, "skipping frame due to failed criteria ($meets_lane_lateral_offset_criterion, $meets_lane_angle_criterion, $meets_lane_end_criterion)\n")
					end
			    end
			end
		end

		toc()
	end

	dict_trajmat = Dict{Uint32,DataFrame}()
	pdset = PrimaryDataset(df_ego_primary, df_other_primary, dict_trajmat, dict_other_idmap, mat_other_indmap, ego_car_on_freeway)
end

const PRIMARYDATA_DIR   = "some_directory"
const STREETMAP_BASE = "some_directory"
const CSVFILESETS = (
			("example.csv",  "example_streetmap", CONTROL_STATUS_READY, [1,10586]),
			("example2.csv", "example_streetmap", CONTROL_STATUS_READY, Int[]),
			#=
			ACTUAL DATA FILE LOCATIONS CENSORED
			=#
        ) 

params = PrimaryDataExtractionParams()

streetnet_cache = Dict{String, StreetNetwork}()

tic()

for (csvfile, streetmapbasename, default_control_status, frameinds) in CSVFILESETS

	if streetmapbasename == "skip"
		println("skipping ", csvfile)
		continue
	end

	params.frameinds = frameinds
	params.default_control_status = default_control_status

	if !haskey(streetnet_cache, streetmapbasename)
		streetnet_cache[streetmapbasename] = load(STREETMAP_BASE*streetmapbasename*".jld")["streetmap"]
	end

	tic()
	trajdata = load_trajdata(csvfile)
	sn       = streetnet_cache[streetmapbasename]
	pdset    = gen_primary_data(trajdata, sn, params)

	if isa(pdset, PrimaryDataset)
		csvfilebase = basename(csvfile)
		pdsetname = joinpath(PRIMARYDATA_DIR, toext("primarydata_" * csvfilebase, "jld"))
		JLD.save(pdsetname, "pdset", pdset::PrimaryDataset)
	end

	toc()
end

print("TOTAL TIME: "); toc()