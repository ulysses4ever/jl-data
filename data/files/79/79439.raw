#################### Model Initialization ####################

function reset!(m::Model)
  m.iter = 0
  for s in m.samplers
    s.tune["sampler"] = nothing
  end
  m
end

function setinits!(m::Model, inits::Dict{Symbol,Any})
  reset!(m)
  for key in m.dependents
    node = m[key]
    if isa(node, AbstractStochastic)
      haskey(inits, key) ||
        throw(ArgumentError("missing initial value for node : $key"))
      setinits!(node, m, inits[key])
    else
      setinits!(node, m)
    end
  end
  m.hasinits = true
  m
end

function setinits!{T<:Real}(m::Model, inits::Vector{T})
  reset!(m)
  relist!(m, inits)
  m.hasinits = true
  m
end

function setinputs!(m::Model, inputs::Dict{Symbol,Any})
  for key in keys(m, :input)
    haskey(inputs, key) ||
      throw(ArgumentError("missing inputs for node : $key"))
    isa(inputs[key], AbstractDependent) &&
      throw(ArgumentError("inputs cannot be Dependent types"))
    m.nodes[key] = deepcopy(inputs[key])
  end
  m.hasinputs = true
  m
end

function setsamplers!(m::Model, samplers::Vector{Sampler})
  m.samplers = deepcopy(samplers)
  for sampler in m.samplers
    sampler.targets = keys(m, :target, sampler.params)
  end
  m
end
