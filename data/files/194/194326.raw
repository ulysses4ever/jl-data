abstract DiffEvoCrossoverOperator{NP,NC} <: CrossoverOperator{NP,NC}

# FIXME is it possible somehow to do arithmetic operations with N?
immutable DiffEvoRandBin{N} <: DiffEvoCrossoverOperator{N,1}
  cr::Float64   # probability to crossover the dimension
  f::Float64    # scale parameter

  DiffEvoRandBin(cr::Number, f::Number) = new(cr, f)
  DiffEvoRandBin(options::Associative{Symbol,Any}) = new(options[:cr], options[:f])
end

crossover_parameters(xover::DiffEvoRandBin, pop, target_index) = xover.cr, xover.f

typealias DiffEvoRandBin1 DiffEvoRandBin{3}
typealias DiffEvoRandBin2 DiffEvoRandBin{5}

function apply!(xover::DiffEvoCrossoverOperator{3,1}, target, target_index, pop, parentIndices)
  @assert length(parentIndices) == 3
  cr, f = crossover_parameters(xover, pop, target_index)
  p1ix, p2ix, p3ix = parentIndices
  # Always ensure at least one parameter is xovered
  mut_ix = rand(1:length(target))
  @inbounds for i in 1:length(target)
    if i == mut_ix || rand() <= cr
      target[i] = pop[i,p3ix] + f * (pop[i,p1ix] .- pop[i,p2ix])
    end
  end
  return target
end

function apply!(xover::DiffEvoCrossoverOperator{5,1}, target, target_index, pop, parentIndices)
  @assert length(parentIndices) == 5
  cr, f = crossover_parameters(xover, pop, target_index)
  p1ix, p2ix, p3ix, p4ix, p5ix = parentIndices
  # Always ensure at least one parameter is xovered
  mut_ix = rand(1:length(target))
  @inbounds for i in 1:length(target)
    if i == mut_ix || rand() <= cr
      target[i] = pop[i,p3ix] +
                f .* (pop[i,p1ix] .- pop[i,p2ix]) +
                f .* (pop[i,p4ix] .- pop[i,p5ix])
    end
  end
  return target
end
