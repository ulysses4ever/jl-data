;; stock.jl -- gtk stock and so on -*- lisp -*-
;;
;; Copyright (C) 2000 John Harper <john@dcs.warwick.ac.uk>
;;
;; This file is part of sawfish.
;;
;; sawfish is free software; you can redistribute it and/or modify it
;; under the terms of the GNU General Public License as published by
;; the Free Software Foundation; either version 2, or (at your option)
;; any later version.
;;
;; sawfish is distributed in the hope that it will be useful, but WITHOUT
;; ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
;; FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License
;; for more details.
;;
;; You should have received a copy of the GNU General Public License
;; along with sawfish; see the file COPYING.  If not, write to
;; the Free Software Foundation, 675 Mass Ave, Cambridge, MA 02139, USA.

;; This file was delete once by mistake, in the commit dbe8c2235, and
;; it had been called "lisp/sawfish/gtk/stock.jl.gtk".
;; Re-added in the commit ce7e6c.

(define-structure sawfish.gtk.stock

    (export stock-button
            simple-dialog
            about-dialog)

    (open rep
          gui.gtk-2.gtk
          sawfish.gtk.widget)

  ;; same as in gnome now
  (define (stock-button type)
    (gtk-button-new-from-stock
     (case type
       ((ok) "gtk-ok")
       ((cancel) "gtk-cancel")
       ((revert) "gtk-revert-to-saved")
       ((apply) "gtk-apply")
       ((yes) "gtk-yes")
       ((no) "gtk-no")
       ((close) "gtk-close")
       ((help) "gtk-help"))))

  (define (simple-dialog title widget #!optional ok-callback main-window)

    (let ((window (gtk-window-new 'toplevel))
	  (vbox (gtk-vbox-new nil box-spacing))
	  (hbbox (gtk-hbutton-box-new))
	  (ok (stock-button 'ok))
	  (cancel (and ok-callback (stock-button 'cancel))))

      (define (on-cancel)
	(gtk-widget-destroy window))

      (define (on-ok)
	(ok-callback)
	(gtk-widget-destroy window))

      (gtk-window-set-title window title)
      (gtk-window-set-wmclass window "ok_cancel_dialog" "Nokogiri")
      (gtk-container-set-border-width window box-border)
      (when main-window
	(gtk-window-set-transient-for window main-window))

      (gtk-box-set-spacing hbbox button-box-spacing)
      (gtk-button-box-set-layout hbbox 'end)
      (when cancel
	(gtk-box-pack-end hbbox cancel))
      (gtk-box-pack-end hbbox ok)
      (gtk-box-pack-end vbox hbbox)
      (gtk-container-add window vbox)
      (gtk-widget-show-all vbox)

      (gtk-container-add vbox widget)

      (when cancel
	(g-signal-connect cancel "clicked" on-cancel))
      (g-signal-connect ok "clicked" (if ok-callback on-ok on-cancel))
      (g-signal-connect window "delete_event" on-cancel)

      (gtk-widget-show window)
      (gtk-window-set-modal window t)
      (gtk-widget-grab-focus widget)

      window))

  (define (about-dialog title version copyright
			authors comments #!key logo extra)
    (declare (unused logo))
    (let* ((box (gtk-vbox-new nil 4))
	   (text-view (gtk-text-view-new))
	   (text-buffer (gtk-text-view-get-buffer text-view)))
      (define (insert s)
	(gtk-text-buffer-insert-at-cursor text-buffer s (length s)))
      (insert (format nil "%s %s\n" title version))
      (insert (format nil "%s\n\n" copyright))
      (when authors
	(insert "Authors:\n")
	(mapc (lambda (x) (insert (format nil "    %s\n" x))) authors))
      (insert (format nil "\n%s\n" comments))
      (gtk-text-view-set-editable text-view nil)
      (gtk-container-add box text-view)
      (when extra
	(gtk-box-pack-end box extra))
      (gtk-widget-show-all box)
      (simple-dialog "About" box))))
