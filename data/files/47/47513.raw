["Backward-compatible types."]

## Comment/Aside. ##

immutable Comment
    id :: Symbol
    Comment() = new(gensym("comment"))
end

## Docs. ##

type Docs{format}
    data :: AbstractString
    obj

    Docs(data) = new(data)
end

## Entries. ##

abstract AbstractEntry

type Entry{category} <: AbstractEntry
    docs    :: Docs
    data    :: Dict{Symbol, Any}
    modname :: Module
end

## Manual pages. ##

readdocs(file) = Docs{format(file)}(readall(file))

format(file) = symbol(splitext(file)[end][2:end])

type Page
    docs :: Docs
    file :: AbstractString

    Page(file) = new(readdocs(file), file)
end

type Manual
    pages :: Vector{Page}

    Manual(root, files) = new([Page(abspath(joinpath(root, file))) for file in files])
end

"Usage from REPL, use current directory as root."
Manual(::Nothing, files) = Manual(pwd(), files)

## Metadata. ##

type Metadata
    modname :: Module
    entries :: ObjectIdDict
    root    :: UTF8String
    files   :: Set{UTF8String}
    data    :: Dict{Symbol, Any}
    loaded  :: Bool
end

function Metadata(m::Module)

    # Import needed data from actual cache.
    moduledata = Cache.getmodule(m)
    rawdocs    = Cache.getraw(m)
    metadata   = Cache.getmeta(m)

    # `Metadata` fields initialisation.
    modname = m
    entries = ObjectIdDict()
    root    = moduledata.rootfile
    files   = deepcopy(moduledata.files)
    data    = deepcopy(moduledata.metadata)
    loaded  = true

    # Add some default module metadata.
    format = get!(data, :format, :md)
    manual = get!(data, :manual, UTF8String[])

    # Populate the `entries` field.
    for (obj, raw) in rawdocs

        # Move the `:codesource` metadata to old `:source` field.
        if haskey(metadata[obj], :codesource)
            metadata[obj][:source] = metadata[obj][:codesource]
            delete!(metadata[obj], :codesource)
        end

        # Conversion of `Aside` to `Comment`.
        newobj, newcat = isa(obj, Collector.Aside) ?
            (Comment(), :comment) :
            (obj, metadata[obj][:category])

        # Build the `Entry` object and store it.
        entries[newobj] = Entry{newcat}(
            Docs{format}(raw),
            metadata[obj],
            modname
            )
    end

    Metadata(
        modname,
        entries,
        root,
        files,
        data,
        loaded
        )
end
