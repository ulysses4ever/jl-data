import OpenStreetMapParser
const OSM = OpenStreetMapParser

const ROAD_CLASSES = Dict(
    "motorway" => 1,
    "trunk" => 2,
    "primary" => 3,
    "secondary" => 4,
    "tertiary" => 5,
    "unclassified" => 6,
    "residential" => 6,
    "service" => 7,
    "motorway_link" => 1,
    "trunk_link" => 2,
    "primary_link" => 3,
    "secondary_link" => 4,
    "tertiary_link" => 5,
    "living_street" => 8,
    "pedestrian" => 8,
    "road" => 6)

#TOOLS from OpenStreetMapParser
highway(w::OSM.Way) = haskey(w.tags, "highway")
roadway(w::OSM.Way) = get(ROAD_CLASSES,w.tags["highway"],0.)
function oneway(w::OSM.Way)
    v = get(w.tags,"oneway", "")

    if v == "false" || v == "no" || v == "0"
        return false
    elseif v == "-1" || v == "true" || v == "yes" || v == "1"
        return true
    end

    highway = get(w.tags,"highway", "")
    junction = get(w.tags,"junction", "")

    return (highway == "motorway" ||
            highway == "motorway_link" ||
            junction == "roundabout")
end

visible{T <: OSM.OSMElement}(obj::T) = (get(obj.tags, "visible", "") != "false")
services(w::OSM.Way) = (get(w.tags,"highway", "") == "services")
reverse(w::OSM.Way) = (get(w.tags,"oneway", "") == "-1")
toradians(degree::Float64) = degree * Ï€ / 180.0

"distance between the two points in meters"
function distance(pt1::Node, pt2::Node)
    dLat = toradians(pt2.lat - pt1.lat)
    dLon = toradians(pt2.lon - pt1.lon)
    lat1 = toradians(pt1.lat)
    lat2 = toradians(pt2.lat)
    a = sin(dLat/2)^2 + sin(dLon/2)^2 * cos(lat1) * cos(lat2)
    2.0 * atan2(sqrt(a), sqrt(1-a)) * 6373.0 * 1000
end

"""
Opens a OSM file,
and returns a network object from the OSM data
"""
function osm2network(filename::AbstractString)
  @time osm = OSM.parseOSM(filename);


  # fetch all nodes ids
  osm_id = Int[n.id for n in osm.nodes]
  # construct inverse dictionary
  node_id = Dict{Int,Int}()
  sizehint!(node_id, length(osm_id))
  for i in 1:length(osm_id)
      node_id[osm_id[i]] = i
  end
  #construct the "Node" objects
  nodes = Array{Node}(length(osm_id))
  for i in 1:length(nodes)
    lon, lat = osm.nodes[i].lonlat
    nodes[i] = Node(lon,lat)
  end

  g = DiGraph(length(nodes))

  #We transform ways into edges

  roads = Dict{Tuple{Int,Int},Road}()
  for w in osm.ways
    if highway(w) && visible(w) && !(services(w)) && roadway(w) != 0
      for i in 2:length(w.nodes)
        if reverse(w)
          edge = Edge(node_id[w.nodes[i]],node_id[w.nodes[i-1]])
        else
          edge = Edge(node_id[w.nodes[i]],node_id[w.nodes[i-1]])
        end
        if !has_edge(g,edge)
          add_edge!(g,edge)
          roads[src(edge),dst(edge)] = Road(distance(nodes[src(edge)],nodes[dst(edge)]), roadway(w))
        end
        if !oneway(w)
          edge = Edge(dst(edge),src(edge))
          if !has_edge(g,edge)
            add_edge!(g,edge)
            roads[src(edge),dst(edge)] = Road(distance(nodes[src(edge)],nodes[dst(edge)]), roadway(w))
          end
        end
      end
    end
  end
  return Network(g, nodes, roads)
end
