#= Copyright 2015 Fengyang Wang

Licensed under the Apache License, Version 2.0 (the "License"); you may not use
this file except in compliance with the License. You may obtain a copy of the
License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software distributed
under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
CONDITIONS OF ANY KIND, either express or implied. See the License for the
specific language governing permissions and limitations under the License. =#

@reexport module Numeric

export english, unenglish

const ONES = [
    "one", "two", "three", "four", "five", "six", "seven", "eight", "nine"]
const TEENS = [
    "eleven", "twelve", "thirteen", "fourteen", "fifteen", "sixteen",
    "seventeen", "eighteen", "nineteen"]
const TENS = [
    "ten", "twenty", "thirty", "forty", "fifty", "sixty", "seventy", "eighty",
    "ninety"]
const POWERS = [
    "thousand", "million", "billion", "trillion", "quadrillion", "quintillion",
    "sextillion", "septillion", "octillion", "nonillion", "decillion",
    "undecillion", "duodecillion", "tredecillion", "quattuordecillion",
    "quindecillion", "sexdecillion", "septendecillion", "octodecillion",
    "novemdecillion", "vigintillion"]

function reverse_word_lookup(ones, teens, tens)
    result = Dict{ASCIIString, Int}()
    for (i, o) in enumerate(ones)
        result[o] = i
    end
    for (i, o) in enumerate(teens)
        result[o] = 10 + i
    end
    for (i, o) in enumerate(tens)
        result[o] = i * 10
    end
    result
end

function reverse_power_lookup(powers)
    result = Dict{ASCIIString, BigInt}()
    for (i, o) in enumerate(powers)
        result[o] = BigInt(1000)^i
    end
    result
end

const REVERSE_WORD = reverse_word_lookup(ONES, TEENS, TENS)
const REVERSE_POWER = reverse_power_lookup(POWERS)


"""Convert \$n\$ to English, given that \$0 < n < 100\$."""
function lth(n::Integer)
    if n < 10
        ONES[n]
    elseif n % 10 == 0
        TENS[n ÷ 10]
    elseif n < 20
        TEENS[n % 10]
    else
        string(TENS[n ÷ 10], '-', ONES[n % 10])
    end
end


"""Convert \$n\$ to English, given that \$0 < n < 1000\$."""
function ltt(n::Integer)
    if n < 100
        lth(n)
    elseif n % 100 == 0
        string(ONES[n ÷ 100], " hundred")
    else
        string(ONES[n ÷ 100], " hundred ", lth(n % 100))
    end
end


"""Convert \$n\$ to English, given that \$0 \\le n < 10^{66}\$."""
function english(n::Integer)
    if n == 0
        "zero"
    else
        result = ASCIIString[]
        segments = digits(n, 1000)
        if segments[1] ≠ 0
            push!(result, ltt(segments[1]))
        end
        for (i, part) in enumerate(segments[2:end])
            if part ≠ 0
                push!(result, POWERS[i], ltt(part))
            end
        end
        join(reverse(result), " ")
    end
end

"""
    unenglish(T <: Integer, data::AbstractString) → T

Convert `data` to an integral type. This function has the guarantee that
`unenglish(Int, english(x)) == x`, modulo any type differences. It is not
guaranteed to work well or throw exceptions on other inputs.
"""
function unenglish{T<:Integer}(::Type{T}, data::AbstractString)
    words = split(data)
    bigpart::T = zero(T)
    smallpart::T = zero(T)
    for word in words
        pieces = split(word, '-')
        for piece in pieces
            piece = lowercase(piece)
            if haskey(REVERSE_WORD, piece)
                smallpart += T(REVERSE_WORD[piece])
            elseif piece == "hundred"
                smallpart *= T(100)
            elseif haskey(REVERSE_POWER, piece)
                bigpart += T(REVERSE_POWER[piece] * smallpart)
                smallpart = zero(T)
            end
        end
    end
    bigpart + smallpart
end

end  # module Numeric
