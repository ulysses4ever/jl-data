_length(iter) = _length(iter, Base.iteratorsize(iter))
_length(iter, ::Base.HasLength) = length(iter)
_length(iter, ::Base.HasShape)  = length(iter)
_length(iter, ::Base.IsInfinite) = Inf
_length(iter, ::Base.SizeUnknown) = "NA"

Base.iteratoreltype{E,T}(::Type{DataIterator{E,T}}) = Base.HasEltype()
Base.eltype{E,T}(::Type{DataIterator{E,T}}) = E

# There is no contract that says these methods will work
# It may be that some DataIterator subtypes do not support getindex
# and/or don't support collect
getobs(iter::DataIterator) = getobs.(collect(iter))
getobs(iter::DataIterator, idx::Int) = getobs(iter[idx])

DataSubset{T<:DataIterator}(data::T, indices, obsdim) =
    throw(MethodError(DataSubset, (data,indices,obsdim)))

# To avoid overflow when infinite
_next_idx(iter, idx) = _next_idx(Base.iteratorsize(iter), idx)
_next_idx(::Base.IteratorSize, idx) = idx + 1
_next_idx(::Base.IsInfinite, idx) = 1

# --------------------------------------------------------------------
# ObsIterator

function Base.show{E,T}(io::IO, iter::ObsIterator{E,T})
    if get(io, :compact, false)
        print(io, typeof(iter).name.name, "{", E, ",", T, "} with " , _length(iter), " observations")
    else
        print(io, summary(iter), "\n Iterator providing ", _length(iter), " observations")
    end
end

# --------------------------------------------------------------------
# BatchIterator

function Base.show{E,T}(io::IO, iter::BatchIterator{E,T})
    if get(io, :compact, false)
        print(io, typeof(iter).name.name, "{", E, ",", T, "} with " , _length(iter), " batches")
    else
        print(io, summary(iter), "\n Iterator providing ", _length(iter), " batches")
    end
end

# --------------------------------------------------------------------

"""
    RandomObs(data, [count], [obsdim])

Description
============

Create an iterator that generates `count` randomly sampled
observations from `data`. In the case `count` is not provided,
it will generate random samples indefinitely.

Arguments
==========

- **`data`** : The object describing the dataset. Can be of any
    type as long as it implements [`getobs`](@ref) and
    [`nobs`](@ref) (see Details for more information).

- **`count`** : Optional. The number of randomly sampled
    observations that the iterator will generate before stopping.
    If omited, the iterator will generate randomly sampled
    observations forever.

- **`obsdim`** : Optional. If it makes sense for the type of
    `data`, `obsdim` can be used to specify which dimension of
    `data` denotes the observations. It can be specified in a
    typestable manner as a positional argument (see
    `?LearnBase.ObsDim`), or more conveniently as a smart keyword
    argument.

Details
========

For `RandomObs` to work on some data structure, the type of the
given variable `data` must implement the [`DataSubset`](@ref)
interface. See `?DataSubset` for more info.

Author(s)
==========

- Christof Stocker (Github: https://github.com/Evizero)
- Tom Breloff (Github: https://github.com/tbreloff)

Examples
=========

```julia
X, Y = load_iris()

# go over 500 randomly sampled observations in X
i = 0
for x in RandomObs(X, 500) # also: RandomObs(X, count = 500)
    @assert typeof(x) <: SubArray{Float64,1}
    @assert length(x) == 4
    i += 1
end
@assert i == 500

# if no count it provided the iterator will generate samples forever
for x in RandomObs(X)
    # this loop will never stop unless break is used
    if true; break; end
end

# also works for multiple data arguments (e.g. labeled data)
for (x,y) in RandomObs((X,Y), count = 100)
    @assert typeof(x) <: SubArray{Float64,1}
    @assert typeof(y) <: String
end
```

see also
=========

[`RandomBatches`](@ref), [`ObsView`](@ref), [`BatchView`](@ref),
[`shuffleobs`](@ref), [`DataSubset`](@ref), [`BufferGetObs`](@ref)
"""
immutable RandomObs{E,T,O,I} <: ObsIterator{E,T}
    data::T
    count::Int
    obsdim::O
end

function RandomObs{T,O}(data::T, count::Int, obsdim::O)
    count > 0 || throw(ArgumentError("count has to be greater than 0"))
    E = typeof(datasubset(data, 1, obsdim))
    RandomObs{E,T,O,Base.HasLength}(data, count, obsdim)
end

function RandomObs{T,O}(data::T, obsdim::O)
    E = typeof(datasubset(data, 1, obsdim))
    RandomObs{E,T,O,Base.IsInfinite}(data, 1337, obsdim)
end

RandomObs{T}(data::T, count::Int; obsdim = default_obsdim(data)) =
    RandomObs(data, count, obs_dim(obsdim))

# convenience constructor.
RandomObs(data, ::Void, obsdim) = RandomObs(data, obsdim)
function RandomObs(data; count = nothing, obsdim = default_obsdim(data))
    RandomObs(data, count, obs_dim(obsdim))
end

Base.start(iter::RandomObs) = 1
Base.done(iter::RandomObs, idx) = idx > _length(iter)
function Base.next(iter::RandomObs, idx)
    (datasubset(iter.data, rand(1:nobs(iter.data, iter.obsdim)), iter.obsdim),
     _next_idx(iter,idx))
end

Base.iteratorsize{E,T,O,I}(::Type{RandomObs{E,T,O,I}}) = I()
Base.length{E,T,O}(iter::RandomObs{E,T,O,Base.HasLength}) = iter.count
nobs(iter::RandomObs) = nobs(iter.data, iter.obsdim)

# --------------------------------------------------------------------

"""
    RandomBatches(data, [size], [count], [obsdim])

Description
============

Create an iterator that generates `count` randomly sampled
batches from `data` with a batch-size of `size` .
In the case `count` is not provided, it will generate random
batches indefinitely.

Arguments
==========

- **`data`** : The object describing the dataset. Can be of any
    type as long as it implements [`getobs`](@ref) and
    [`nobs`](@ref) (see Details for more information).

- **`size`** : Optional. The batch-size of each batch.
    I.e. the number of randomly sampled observations in each batch

- **`count`** : Optional. The number of randomly sampled batches
    that the iterator will generate before stopping. If omited,
    the iterator will generate randomly sampled observations
    forever.

- **`obsdim`** : Optional. If it makes sense for the type of
    `data`, `obsdim` can be used to specify which dimension of
    `data` denotes the observations. It can be specified in a
    typestable manner as a positional argument (see
    `?LearnBase.ObsDim`), or more conveniently as a smart keyword
    argument.

Details
========

For `RandomBatches` to work on some data structure, the type of
the given variable `data` must implement the [`DataSubset`](@ref)
interface. See `?DataSubset` for more info.

Author(s)
==========

- Christof Stocker (Github: https://github.com/Evizero)
- Tom Breloff (Github: https://github.com/tbreloff)

Examples
=========

```julia
X, Y = load_iris()

# go over 500 randomly sampled batches of batchsize 10
i = 0
for x in RandomBatches(X, 10, 500) # also: RandomObs(X, size = 10, count = 500)
    @assert typeof(x) <: SubArray{Float64,2}
    @assert size(x) == (4,10)
    i += 1
end
@assert i == 500

# if no count it provided the iterator will generate samples forever
for x in RandomBatches(X, 10)
    # this loop will never stop unless break is used
    if true; break; end
end

# also works for multiple data arguments (e.g. labeled data)
for (x,y) in RandomBatches((X,Y), 10, 500)
    @assert typeof(x) <: SubArray{Float64,2}
    @assert typeof(y) <: SubArray{String,1}
end
```

see also
=========

[`RandomObs`](@ref), [`BatchView`](@ref), [`ObsView`](@ref),
[`shuffleobs`](@ref), [`DataSubset`](@ref), [`BufferGetObs`](@ref)
"""
immutable RandomBatches{E,T,O,I} <: BatchIterator{E,T}
    data::T
    size::Int
    count::Int
    obsdim::O
end

function RandomBatches{T,O}(data::T, size::Int, count::Int, obsdim::O)
    size  > 0 || throw(ArgumentError("size has to be greater than 0"))
    count > 0 || throw(ArgumentError("count has to be greater than 0"))
    E = typeof(datasubset(data, rand(1:size, size), obsdim))
    RandomBatches{E,T,O,Base.HasLength}(data, size, count, obsdim)
end

function RandomBatches{T,O}(data::T, size::Int, obsdim::O)
    size > 0 || throw(ArgumentError("size has to be greater than 0"))
    E = typeof(datasubset(data, rand(1:size, size), obsdim))
    RandomBatches{E,T,O,Base.IsInfinite}(data, size, 1337, obsdim)
end

RandomBatches{T}(data::T, size::Int; obsdim = default_obsdim(data)) =
    RandomBatches(data, size, obs_dim(obsdim))

RandomBatches{T}(data::T, size::Int, count::Int; obsdim = default_obsdim(data)) =
    RandomBatches(data, size, count, obs_dim(obsdim))

# convenience constructor.
RandomBatches{T}(data::T, size::Int, ::Void, obsdim) =
    RandomBatches(data, size, obsdim)

function RandomBatches{T}(data::T; size::Int = -1, count = nothing, obsdim = default_obsdim(data))
    nobsdim = obs_dim(obsdim)
    nsize = size < 0 ? default_batch_size(data, nobsdim)::Int : size
    RandomBatches(data, nsize, count, nobsdim)
end

Base.start(iter::RandomBatches) = 1
Base.done(iter::RandomBatches, idx) = idx > _length(iter)
function Base.next(iter::RandomBatches, idx)
    # maybe use StatsBase.sample instead of rand in order to avoid
    # replacement. That said I would like to avoid keyword arguments
    # and currently sample needs "replace" to be specified as such
    indices = rand(1:nobs(iter.data, iter.obsdim), iter.size)
    (datasubset(iter.data, indices, iter.obsdim), _next_idx(iter, idx))
end

Base.iteratorsize{E,T,O,I}(::Type{RandomBatches{E,T,O,I}}) = I()
Base.length{E,T,O}(iter::RandomBatches{E,T,O,Base.HasLength}) = iter.count
nobs(iter::RandomBatches) = nobs(iter.data, iter.obsdim)
batchsize(iter::RandomBatches) = iter.size

# --------------------------------------------------------------------

"""
    BufferGetObs(iterator, [buffer])

A stateful iterator that stores the output of
`next(iterator,state)` into `buffer` using `getobs!(buffer,
...)`. Depending on the type of data provided by `iterator` this
may be more memory efficient than `getobs(...)`. In the case of
array data, for example, this allows for cache-efficient
processing of each element without allocating a temporary array.

Note that not all types of data support buffering, because it is
the author's choice to opt-in and implement a custom `getobs!`.
For those types that do not provide a custom `getobs!`, the
buffer will be ignored and the result of `getobs(...)` returned.

see [`eachobs`](@ref) and [`eachbatch`](@ref) for concrete examples.
"""
immutable BufferGetObs{TElem,TIter}
    iter::TIter
    buffer::TElem
end

function BufferGetObs{T}(iter::T)
    buffer = getobs(first(iter))
    BufferGetObs{typeof(buffer),T}(iter, buffer)
end

function Base.show{E,T}(io::IO, iter::BufferGetObs{E,T})
    if get(io, :compact, false)
        print(io, typeof(iter).name.name, "{", E, ",", T, "} with " , _length(iter), " elements")
    else
        print(io, summary(iter), "\n Iterator providing ", _length(iter), " elements")
    end
end

Base.start(b::BufferGetObs) = start(b.iter)
Base.done(b::BufferGetObs, idx) = done(b.iter, idx)
function Base.next(b::BufferGetObs, idx)
    subset, nidx = next(b.iter, idx)
    (getobs!(b.buffer, subset), nidx)
end

Base.eltype{E,T}(::Type{BufferGetObs{E,T}}) = E
Base.iteratorsize{E,T}(::Type{BufferGetObs{E,T}}) = Base.iteratorsize(T)
Base.length(b::BufferGetObs) = length(b.iter)
Base.size(b::BufferGetObs, I...) = size(b.iter, I...)
nobs(b::BufferGetObs) = nobs(b.iter)
batchsize(b::BufferGetObs) = batchsize(b.iter)

# --------------------------------------------------------------------

"""
    eachobs(data, [obsdim])

Iterate over `data` one observation at a time. If supported by
the type of `data`, a buffer will be preallocated and reused for
memory efficiency.

IMPORTANT: Avoid using `collect`, because in general each
iteration could return the same object with mutated values.
If that behaviour is undesired use `obsview` instead.

```julia
X = rand(4,100)
for x in eachobs(X)
    # loop entered 100 times
    @assert typeof(x) <: Vector{Float64}
    @assert size(x) == (4,)
end
```

In the case of arrays it is assumed that the observations are
represented by the last array dimension. This can be overwritten.

```julia
# This time flip the dimensions of the matrix
X = rand(100,4)
A = eachobs(X, obsdim=1)
# The behaviour remains the same as before
@assert eltype(A) <: Array{Float64,1}
@assert length(A) == 100
```

Multiple variables are supported (e.g. for labeled data)

```julia
for (x,y) in eachobs((X,Y))
    # ...
end
```

Note that internally `eachobs(data, obsdim)` maps to
`BufferGetObs(obsview(data, obsdim))`.

```julia
@assert typeof(eachobs(X)) <: BufferGetObs
@assert typeof(eachobs(X).iter) <: ObsView
```

This means that the following code:

```julia
for obs in eachobs(data, obsdim)
    # ...
end
```

is roughly equivalent to:

```julia
obs = getobs(data, 1, obsdim) # use first element to preallocate buffer
for _ in obsview(data, obsdim)
    getobs!(obs, _) # reuse buffer each iteration
    # ...
end
```

see [`BufferGetObs`](@ref), [`obsview`](@ref), and
[`getobs!`](@ref) for more info. also see [`eachbatch`](@ref) for
a mini-batch version.
"""
eachobs(data, obsdim) = BufferGetObs(ObsView(data, obsdim))
eachobs(data; obsdim = default_obsdim(data)) = eachobs(data, obs_dim(obsdim))

# --------------------------------------------------------------------

"""
    eachbatch(data, [size], [count], [obsdim])

Iterate over `data` one batch at a time. If supported by the type
of `data`, a buffer will be preallocated and reused for memory
efficiency.

IMPORTANT: Avoid using `collect`, because in general each
iteration could return the same object with mutated values.
If that behaviour is undesired use `BatchView` instead.

The (constant) batch-size can be either provided directly using
`size` or indirectly using `count`, which derives `size` based on
`nobs`. In the case that the size of the dataset is not dividable
by the specified (or inferred) `size`, the remaining observations
will be ignored.

```julia
X = rand(4,150)
for x in eachbatch(X, size = 10) # or: eachbatch(X, count = 15)
    # loop entered 15 times
    @assert typeof(x) <: Matrix{Float64}
    @assert size(x) == (4,10)
end
```

In the case of arrays it is assumed that the observations are
represented by the last array dimension. This can be overwritten.

```julia
# This time flip the dimensions of the matrix
X = rand(150,4)
A = eachbatch(X, size = 10, obsdim = 1)
# The behaviour remains the same as before
@assert eltype(A) <: Array{Float64,2}
@assert length(A) == 15
```

Multiple variables are supported (e.g. for labeled data)

```julia
for (x,y) in eachbatch((X,Y))
    # ...
end
```

Note that internally `eachbatch(data, ...)` maps to
`BufferGetObs(batchview(data, ...))`.

```julia
@assert typeof(eachbatch(X)) <: BufferGetObs
@assert typeof(eachbatch(X).iter) <: BatchView
```

This means that the following code:

```julia
for batch in eachbatch(data, batchsize, obsdim)
    # ...
end
```

is roughly equivalent to:

```julia
batch = getobs(data, collect(1:batchsize), obsdim) # use first element to preallocate buffer
for _ in batchview(data, batchsize, obsdim)
    getobs!(batch, _) # reuse buffer each iteration
    # ...
end
```

see [`BufferGetObs`](@ref), [`batchview`](@ref), and
[`getobs!`](@ref) for more info. also see [`eachobs`](@ref) for a
single-observation version.
"""
eachbatch(data; size = -1, count = -1, obsdim = default_obsdim(data)) =
    BufferGetObs(BatchView(data, size, count, obs_dim(obsdim)))

eachbatch{T<:Union{Tuple,ObsDimension}}(data, obsdim::T) =
    BufferGetObs(BatchView(data, -1, -1, obsdim))

eachbatch{T<:Union{Tuple,ObsDimension}}(data, size::Int, obsdim::T = default_obsdim(data)) =
    BufferGetObs(BatchView(data, size, -1, obsdim))

eachbatch{T<:Union{Tuple,ObsDimension}}(data, size::Int, count::Int, obsdim::T = default_obsdim(data)) =
    BufferGetObs(BatchView(data, size, count, obsdim))
