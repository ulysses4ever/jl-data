import Base.convert, Base.size
export AbstractCvxExpr, CvxExpr, Variable, Parameter, Constant, size

abstract AbstractCvxExpr
# every type inheriting from the AbstractCvxExpr type should have the following properties
# 	head --- a symbol
# 	vexity --- one of :linear, :convex, :concave, :constant
# 	sign   --- one of :pos, :neg, :any
# 	size   --- a tuple giving the size of the expression
vexities = [:constant, :linear, :convex, :concave]
signs    = [:pos, :neg, :any, :zero] # xxx add zero, useful for parameters
# Values consist of any type that can be a Constant
Value = Union(Number,AbstractArray)
size(x::AbstractCvxExpr) = x.size
sign(x::AbstractCvxExpr) = x.sign
vexity(x::AbstractCvxExpr) = x.vexity
# function display(x::CvxExpr)
# 	println("$(x.head)($(display(y) for y in x.args))")
# end

type CvxExpr <: AbstractCvxExpr
	head::Symbol
	args::Array{AbstractCvxExpr}
	vexity::Symbol
	sign::Symbol
	size::Tuple
	function CvxExpr(head::Symbol,args::Array,vexity::Symbol,sign::Symbol,size::Tuple)
		if !(sign in signs)
			error("sign must be one of :pos, :neg, :any; got $sign")
		elseif !(vexity in vexities)
			error("vexity must be one of :constant, :linear, :convex, :concave; got $vexity")
		else
			return new(head,args,vexity,sign,size)
		end
	end
end
CvxExpr(head::Symbol,arg,vexity::Symbol,sign::Symbol,size::Tuple) = CvxExpr(head,[arg],vexity,sign,size)

type Variable <: AbstractCvxExpr
	head::Symbol
	value
	vexity::Symbol
	sign::Symbol
	size::Tuple
	function Variable(head::Symbol,size::Tuple,sign::Symbol)
		if !(sign in signs)
			error("sign must be one of :pos, :neg, :zero, :any; got $sign")
		end
		if head == :variable
			new(head,nothing,:linear,sign,size)
		elseif head == :parameter
			new(head,nothing,:constant,sign,size)
		end		
	end	
end
Variable(size::Tuple,sign::Symbol; kwargs...) = Variable(:variable,size,sign; kwargs...)
Variable(size::Tuple; kwargs...) = Variable(size,:any; kwargs...)
Variable(size...; kwargs...) = Variable(size,:any; kwargs...)
Variable(size::Integer,sign::Symbol; kwargs...) = Variable(Tuple(size),sign; kwargs...)
Parameter(size::Tuple,sign::Symbol; kwargs...) = Variable(:parameter,size,sign; kwargs...)
Parameter(size::Tuple; kwargs...) = Parameter(size,:any; kwargs...)
Parameter(size...; kwargs...) = Parameter(size,:any; kwargs...)
Parameter(size::Integer,sign::Symbol; kwargs...) = Parameter(Tuple(size),sign; kwargs...)
function parameter!(x::Variable)
	x.head = :parameter
	x.vexity = :constant
	x
end
function variable!(x::Variable)
	x.head = :variable
	x.vexity = :linear
	x
end

type Constant <: AbstractCvxExpr
	head::Symbol
	value::Value
	vexity::Symbol
	sign::Symbol
	size::Tuple
	Constant(x::Value,sign) = sign in signs ? new(:constant,x,:constant,sign,size(x)) : error("sign must be one of :pos, :neg, :zero, :any; got $sign")
end
function Constant(x::Number)
	# find the sign for scalar constants
	if x > 0
		return Constant(x,:pos)
	elseif x < 0
		return Constant(x,:neg)	
	elseif x == 0
		return Constant(x,:zero)
	end
end
Constant(x::Value) = Constant(x,:any)

### Unique ids
unique_id(x::AbstractCvxExpr) = ccall(:jl_symbol_name, Ptr{Uint8}, (Any,), x)
#convert(::Type{Bool}, x::CvxConstr) = unique_id(x.lhs) == unique_id(x.rhs)