VERSION >= v"0.4.0" && __precompile__(true)

"""
Barycentric
===========

This module implements the Barycentric formula for polynomial interpolation on
equispaced points and Chebyshev points of the first and second kind. The
formulae used are taken from the paper of Berrut and Trefethen, SIAM Review,
2004.

Written by David A.W. Barton (david.barton@bristol.ac.uk) 2016 and licensed
under the MIT license <https://opensource.org/licenses/MIT>
"""
module Barycentric

export points_chebyshev1, points_chebyshev2, points_equispaced, weights,
    weights_chebyshev1, weights_chebyshev2, weights_equispaced, interpolate,
    interpolation_matrix, differentiation_matrix

# Unless otherwise specified, equation references refer to Berrut and Trefethen, SIAM Review, 2004

"""
Return an array of equispaced points with n intervals. For example
`x = points_equispaced(10)`
"""
function points_equispaced(n::Integer)
    # For completeness only
    collect(linspace(-1.0, 1.0, n+1))
end

"""
Return an array of Chebyshev points of the first kind. For example
`x = points_chebyshev1(10)`
"""
function points_chebyshev1(n::Integer)
    cos([(2j + 1)*π/(2n + 2) for j = 0:n])
end

"""
Return an array of Chebyshev points of the second kind. For example
`x = points_chebyshev2(10)`
"""
function points_chebyshev2(n::Integer)
    cos([j*π/n for j = 0:n])
end

"""
Return the Barycentric weights for arbitrary points. For example
`w = weights(collect(linspace(0, 1, 11)))`
"""
function weights{T <: Real}(x::Vector{T})
    1./map(i -> prod(x[i] - x[[1:i-1; i+1:end]]), 1:length(x))
end

"""
Return the Barycentric weights for equispaced points with n intervals. For
example `w = weights_equispaced(10)`
"""
function weights_equispaced(n::Integer)
    # Eq. (5.1)
    w = [Float64(binomial(n, j)) for j = 0:n]
    w[2:2:end] = -w[2:2:end]
    return w
end

"""
Return the Barycentric weights for Chebyshev points of the first kind. For
example `w = weights_chebyshev1(10)`
"""
function weights_chebyshev1(n::Integer)
    # Eq. (5.3)
    w = [sin((2j + 1)*π/(2n + 2)) for j = 0:n]
    w[2:2:end] = -w[2:2:end]
    return w
end

"""
Return the Barycentric weights for Chebyshev points of the second kind. For
example `w = weights_chebyshev2(10)`
"""
function weights_chebyshev2(n::Integer)
    # Eq. (5.4)
    w = ones(n+1)
    w[2:2:end] = -1.0
    w[1] = 0.5
    w[end] *= 0.5
    return w
end

"""
Return the interpolation matrix from the points x to the points xnew using the
Barycentric weights w. For example :

    x = points_chebyshev2(10)
    w = weights_chebyshev2(10)
    xnew = points_equispaced(5)
    P = interpolation_matrix(xnew, x, w)

Now ynew ≈ `P*y` for some vector y.
"""
function interpolation_matrix{T <: Real}(xnew::Vector{T}, x::Vector{T}, w::Vector{T})
    # Eq. (4.2)
    xdiff = broadcast(-, xnew, x')
    M = broadcast(./, w', xdiff)
    M = broadcast(./, M, sum(M, 2))
    M[isnan(M)] = 0.0
    M[xdiff .== 0] = 1.0
    return M
end

"""
Return the interpolated values from the points x with the values y to the
points xnew using the Barycentric weights w. For example :

    x = points_chebyshev2(10)
    w = weights_chebyshev2(10)
    y = sin(π*x)
    xnew = points_equispaced(5)
    ynew = interpolate(xnew, x, y, w)
"""
function interpolate{T <: Real}(xnew::Vector{T}, x::Vector{T}, y::Vector{T}, w::Vector{T})
    return interpolation_matrix(xnew, x, w)*y
end

"""
Return the differentiation matrix for the points x with the Barycentric weights
w. For example :

    x = points_chebyshev2(10)
    w = weights_chebyshev2(10)
    D = differentiation_matrix(x, w)

Now dy/dx ≈ `D*y` for some vector y.
"""
function differentiation_matrix{T <: Real}(x::Vector{T}, w::Vector{T})
    # Eqs. (9.4) and (9.5)
    D = broadcast(./, w', w)./broadcast(-, x, x')
    idx = diagind(D)
    D[idx] = 0.0
    D[idx] = -sum(D, 2)
    return D
end

end
