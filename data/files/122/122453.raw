using Base.Test
using BenchmarkTools

#########
# Trial #
#########

trial1 = BenchmarkTools.Trial(BenchmarkTools.Parameters(evals = 2))
push!(trial1, 2, 1, 4, 5)
push!(trial1, 21, 0, 41, 51)

trial2 = BenchmarkTools.Trial(BenchmarkTools.Parameters(tolerance = 0.15))
push!(trial2, 21, 0, 41, 51)
push!(trial2, 2, 1, 4, 5)

# test push! using GC_Diff, and deleteat!
init = Base.gc_num(); sleep(0.5);
push!(trial2, 0.5, Base.GC_Diff(Base.gc_num(), init))
@test length(trial2) == 3
deleteat!(trial2, 3)
@test length(trial1) == length(trial2) == 2
sort!(trial2)

@test trial1.params == BenchmarkTools.Parameters(evals = trial1.params.evals)
@test trial2.params == BenchmarkTools.Parameters(tolerance = trial2.params.tolerance)
@test trial1.times == trial2.times == [2.0, 21.0]
@test trial1.gctimes == trial2.gctimes == [1.0, 0.0]
@test trial1.memory == trial2.memory ==  4
@test trial1.allocs == trial2.allocs == 5

trial2.params = trial1.params

@test trial1 == trial2

@test trial1[2] == push!(BenchmarkTools.Trial(BenchmarkTools.Parameters(evals = 2)), 21, 0, 4, 5)
@test trial1[1:end] == trial1

@test time(trial1) == time(trial2) == 2.0
@test gctime(trial1) == gctime(trial2) == 1.0
@test memory(trial1) == memory(trial2) == trial1.memory
@test allocs(trial1) == allocs(trial2) == trial1.allocs
@test tolerance(trial1) == trial1.params.tolerance
@test tolerance(trial2) == trial2.params.tolerance
@test parameters(trial1) == trial2.params
@test parameters(trial2) == trial2.params

# outlier trimming
trial3 = BenchmarkTools.Trial(BenchmarkTools.Parameters(), [1, 2, 3, 10, 11],
                              [1, 1, 1, 1, 1], 1, 1)

trimtrial3 = rmskew(trial3)
rmskew!(trial3)

@test mean(trimtrial3) <= median(trimtrial3)
@test trimtrial3 == trial3

#################
# TrialEstimate #
#################

randtrial = BenchmarkTools.Trial(BenchmarkTools.Parameters())

for _ in 1:40
    push!(randtrial, rand(1:20), 1, 1, 1)
end

while mean(randtrial) <= median(randtrial)
    push!(randtrial, rand(10:20), 1, 1, 1)
end

rmskew!(randtrial)

tmin = minimum(randtrial)
tmed = median(randtrial)
tmean = mean(randtrial)
tmax = maximum(randtrial)

@test time(tmin) == time(randtrial)
@test gctime(tmin) == gctime(randtrial)
@test memory(tmin) == memory(tmed) == memory(tmean) == memory(tmax) == memory(randtrial)
@test allocs(tmin) == allocs(tmed) == allocs(tmean) == allocs(tmax) == allocs(randtrial)
@test tolerance(tmin) == tolerance(tmed) == tolerance(tmean) == tolerance(tmax) == tolerance(randtrial)

@test tmin <= tmed
@test tmean <= tmed # this should be true since we called rmoutliers!(randtrial) earlier
@test tmed <= tmax

##############
# TrialRatio #
##############

randrange = 1.0:0.01:10.0
x, y = rand(randrange), rand(randrange)

@test (ratio(x, y) == x/y) && (ratio(y, x) == y/x)
@test (ratio(x, x) == 1.0) && (ratio(y, y) == 1.0)
@test ratio(0.0, 0.0) == 1.0

ta = BenchmarkTools.TrialEstimate(rand(), rand(), rand(Int), rand(Int), rand())
tb = BenchmarkTools.TrialEstimate(rand(), rand(), rand(Int), rand(Int), rand())
tr = ratio(ta, tb)

@test time(tr) == ratio(time(ta), time(tb))
@test gctime(tr) == ratio(gctime(ta), gctime(tb))
@test memory(tr) == ratio(memory(ta), memory(tb))
@test allocs(tr) == ratio(allocs(ta), allocs(tb))
@test tolerance(tr) == max(tolerance(ta), tolerance(tb))

@test BenchmarkTools.gcratio(ta) == ratio(gctime(ta), time(ta))
@test BenchmarkTools.gcratio(tb) == ratio(gctime(tb), time(tb))

##################
# TrialJudgement #
##################

ta = BenchmarkTools.TrialEstimate(0.49, 0.0, 2, 1, 0.50)
tb = BenchmarkTools.TrialEstimate(1.00, 0.0, 1, 1, 0.05)
tr = ratio(ta, tb)
tj_ab = judge(ta, tb)
tj_r = judge(tr)

@test ratio(tj_ab) == ratio(tj_r) == tr
@test time(tj_ab) == time(tj_r) == :improvement
@test memory(tj_ab) == memory(tj_r) == :regression
@test allocs(tj_ab) == allocs(tj_r) == :invariant
@test tj_ab == tj_r

tj_ab_2 = judge(ta, tb, 2.0)
tj_r_2 = judge(tr, 2.0)

@test tj_ab_2 == tj_r_2
@test ratio(tj_ab_2) == ratio(tj_r_2) == tr
@test time(tj_ab_2) == time(tj_r_2) == :invariant
@test memory(tj_ab_2) == memory(tj_r_2) == :invariant
@test allocs(tj_ab_2) == allocs(tj_r_2) == :invariant

@test !(isinvariant(tj_ab))
@test !(isinvariant(tj_r))
@test isinvariant(tj_ab_2)
@test isinvariant(tj_r_2)

@test isregression(tj_ab)
@test isregression(tj_r)
@test !(isregression(tj_ab_2))
@test !(isregression(tj_r_2))

@test isimprovement(tj_ab)
@test isimprovement(tj_r)
@test !(isimprovement(tj_ab_2))
@test !(isimprovement(tj_r_2))

###################
# pretty printing #
###################

@test BenchmarkTools.prettypercent(.3120123) == "31.2%"

@test BenchmarkTools.prettydiff(0.0) == "-100.0%"
@test BenchmarkTools.prettydiff(1.0) == "+0.0%"
@test BenchmarkTools.prettydiff(2.0) == "+100.0%"

@test BenchmarkTools.prettytime(999) == "999.0 ns"
@test BenchmarkTools.prettytime(1000) == "1.0 μs"
@test BenchmarkTools.prettytime(999_999) == "1000.0 μs"
@test BenchmarkTools.prettytime(1_000_000) == "1.0 ms"
@test BenchmarkTools.prettytime(999_999_999) == "1000.0 ms"
@test BenchmarkTools.prettytime(1_000_000_000) == "1.0 s"

@test BenchmarkTools.prettymemory(1023) == "1023.0 bytes"
@test BenchmarkTools.prettymemory(1024) == "1.0 kb"
@test BenchmarkTools.prettymemory(1048575) == "1024.0 kb"
@test BenchmarkTools.prettymemory(1048576) == "1.0 mb"
@test BenchmarkTools.prettymemory(1073741823) == "1024.0 mb"
@test BenchmarkTools.prettymemory(1073741824) == "1.0 gb"
